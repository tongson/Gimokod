# Based upon the PKGBUILD from Arch Linux
pkgname=dhcpcd
pkgver=5.5.4
pkgrel=1
pkgdesc="RFC2131 compliant DHCP client daemon"
url="http://roy.marples.name/dhcpcd/"
arch=('i686' 'x86_64')
license=('BSD')
groups=('base')
install=dhcpcd.install
options=('emptydirs')  # We Need the Empty /var/lib/dhcpcd Directory
source=("http://roy.marples.name/downloads/"${pkgname}"/"${pkgname}"-"${pkgver}".tar.bz2"
        '0001-Set-hostname-via-proc-if-file-is-available.patch'
        '0001-set-MTU-via-sysfs-if-file-is-available.patch')
sha1sums=('8aa42abd28d49cce5b179998490c36f6c490e338'
          'ee85adac972a8172a9d9b3a1bdd46e0430301582'
          'afbed18dc5544f7d0b81e2266b322ca12becf9a4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # remove net-tools dep
  patch -Np1 -i "${srcdir}"/0001-Set-hostname-via-proc-if-file-is-available.patch
  patch -Np1 -i "${srcdir}"/0001-set-MTU-via-sysfs-if-file-is-available.patch

  # configure variables
  ./configure --libexecdir=/usr/lib/dhcpcd --dbdir=/var/lib/dhcpcd --rundir=/run

  # Build
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}"/usr/bin
  mv "${pkgdir}/sbin/dhcpcd" "${pkgdir}/usr/bin"
  rmdir "${pkgdir}/sbin"

  # Install License
  install -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  awk '{if(FNR<27)print $0}' "${srcdir}"/${pkgname}-${pkgver}/configure.h \
        >> "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  # Set Options in /etc/dhcpcd.conf
  echo noipv4ll >> "${pkgdir}"/etc/dhcpcd.conf # Disable ip4vall
}
