# Based upon the PKGBUILD from Arch Linux
pkgname=curl
pkgver=7.23.1
pkgrel=2
pkgdesc="An URL retrival utility and library"
arch=('i686' 'x86_64')
url="http://curl.haxx.se"
license=('MIT')
depends=('zlib' 'openssl' 'ca-certificates')
options=('!libtool')
source=("http://curl.haxx.se/download/"${pkgname}"-"${pkgver}".tar.gz"{,.asc}
        fix-J-with-O-regression.patch
        curlbuild.h)
md5sums=('8e23151f569fb54afef093ac0695077d'
         '5d8eb7e2e38be0fb00a043f714f6d49f'
         'aa4539ec4f4a2dad1663dc22dd3ab0a1'
         '751bd433ede935c8fae727377625a8ae')


case $(cpp <<<'__SIZEOF_POINTER__' | sed '/^#/d') in
  8) _curlbuild=curlbuild-64.h ;;
  4) _curlbuild=curlbuild-32.h ;;
  *) error "unsupported architecture: %s" "$CARCH"
    exit 1
    ;;
esac

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # upstream bugfix
  # http://curl.haxx.se/mail/archive-2011-11/0030.html
  patch -Np1 < "$srcdir/fix-J-with-O-regression.patch"
  SED=/usr/bin/sed GREP=/usr/bin/grep AR=/usr/bin/ar \
  ./configure \
      --with-random=/dev/urandom \
      --prefix=/usr \
      --build=${CBUILD} \
      --host=${CHOST}  \
      --mandir=/usr/share/man \
      --disable-dependency-tracking \
      --enable-ipv6 \
      --disable-ldaps \
      --disable-ldap \
      --without-libssh2 \
      --without-gssapi \
      --without-krb4 \
      --without-librtmp \
      --without-spnego \
      --without-libidn \
      --disable-gopher \
      --enable-rtsp \
      --enable-http \
      --enable-ftp \
      --enable-file \
      --enable-dict \
      --enable-smtp \
      --enable-pop3 \
      --enable-imap \
      --enable-telnet \
      --enable-manual \
      --enable-nonblocking \
      --enable-largefile \
      --enable-versioned-symbols \
      --without-gnutls \
      --without-nss \
      --with-ssl \
      --without-ca-bundle \
      --with-ca-path=/etc/ssl/certs \
      --enable-threaded-resolver \
      --enable-maintainer-mode
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  install -Dm644 docs/libcurl/libcurl.m4 "${pkgdir}"/usr/share/aclocal/libcurl.m4
  mv "${pkgdir}/usr/include/curl/curlbuild.h" "${pkgdir}/usr/include/curl/$_curlbuild"
  install -m644 "${srcdir}/curlbuild.h" "${pkgdir}/usr/include/curl/curlbuild.h"
}
