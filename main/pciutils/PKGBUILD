# Based upon the PKGBUILD from Arch Linux
pkgname=pciutils
pkgver=3.1.8
pkgrel=1
pkgdesc="PCI bus configuration space access library and tools"
arch=(i686 x86_64)
license=('GPL2')
url="http://mj.ucw.cz/pciutils.html"
source=(ftp://atrey.karlin.mff.cuni.cz/pub/linux/pci/${pkgname}-${pkgver}.tar.gz)
md5sums=('79312f138311d29291c7d44d624cd37e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i 's:wget --no-timestamping:wget:' ./update-pciids.sh
  ./update-pciids.sh
  make OPT="${CFLAGS} -fPIC -DPIC" \
       ZLIB=no \
       SHARED=no \
       PREFIX=/usr \
       SHAREDIR=/usr/share/hwdata \
       MANDIR=/usr/share/man \
       all

  cp lib/libpci.a "${srcdir}/"

  make clean
  make OPT="${CFLAGS}" \
       ZLIB=no \
       SHARED=yes \
       PREFIX=/usr \
       SHAREDIR=/usr/share/hwdata \
       MANDIR=/usr/share/man \
       all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make SHARED=yes PREFIX=/usr SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man DESTDIR="${pkgdir}" install install-lib
  install -m644 "${srcdir}/libpci.a" "${pkgdir}/usr/lib/"
  install -d -m755 "${pkgdir}/usr/bin"
  mv "${pkgdir}"/usr/sbin/* "${pkgdir}"/usr/bin
  rmdir "${pkgdir}"/usr/sbin
}
