# Based upon the PKGBUILD from Arch Linux
pkgname=pciutils
pkgver=3.1.9
pkgrel=2
pkgdesc="PCI bus configuration space access library and tools"
arch=('x86_64')
license=('GPL2')
url="http://mj.ucw.cz/sw/pciutils/"
depends=('hwids')
source=(ftp://atrey.karlin.mff.cuni.cz/pub/linux/pci/${pkgname}-${pkgver}.tar.gz)
md5sums=('e958f5ffb93b12972964381c38f27eb3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make OPT="${CFLAGS} -fPIC -DPIC" \
       ZLIB=no \
       SHARED=no \
       PREFIX=/usr \
       SHAREDIR=/usr/share/hwdata \
       MANDIR=/usr/share/man \
       all

  cp lib/libpci.a "${srcdir}/"

  make clean
  make OPT="${CFLAGS}" \
       ZLIB=no \
       SHARED=yes \
       PREFIX=/usr \
       SHAREDIR=/usr/share/hwdata \
       MANDIR=/usr/share/man \
       all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make SHARED=yes PREFIX=/usr SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man DESTDIR="${pkgdir}" install install-lib
  install -m644 "${srcdir}/libpci.a" "${pkgdir}/usr/lib/"
  install -d -m755 "${pkgdir}/usr/bin"
  mv "${pkgdir}"/usr/sbin/* "${pkgdir}"/usr/bin
  rm -rf $pkgdir/usr/{bin/update-pciids,share/{man/man8/update-pciids.8.gz,hwdata}}
  rmdir "${pkgdir}"/usr/sbin
}
