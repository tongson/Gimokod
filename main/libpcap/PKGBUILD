# Based upon the PKGBUILD from Arch Linux
pkgname=libpcap
pkgver=1.2.1
pkgrel=2
pkgdesc="A system-independent interface for user-level packet capture"
arch=('i686' 'x86_64')
url="http://www.tcpdump.org/"
license=('BSD')
depends=('libnl')
makedepends=('flex' 'bison')
source=(http://www.tcpdump.org/release/libpcap-${pkgver}.tar.gz{,.sig}
        libnl32.patch)
sha256sums=('a135a6ef7e539729a57c7ed345bdb9b64159e13404174006a7972eb33f00debd'
            'dc82ca7e7f737885969af0f78ad0f0eef8206062087e0261ca2799ef26d569b4'
            '7d0cbd49e55405cb2b55852223cd4965526e518cfb4f547ab9cc9b95b8f9c9e6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  Patch libnl32.patch
  autoreconf -f -i
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --with-libnl \
              --disable-bluetooth \
              --enable-ipv6
  make all shared
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -d -m755 "${pkgdir}"/usr/bin
  make DESTDIR="${pkgdir}" install
  # remove static library
  rm -rf "${pkgdir}"/usr/lib/libpcap.a

  #install the license
  install -D -m644 "${srcdir}"/"${pkgname}"-"${pkgver}"/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
