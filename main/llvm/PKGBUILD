# Based upon the PKGBUILD from Arch Linux
pkgbase=llvm
pkgname=('llvm' 'clang')
pkgver=3.0
pkgrel=1
arch=('i686' 'x86_64')
url="http://llvm.org/"
license=('custom:University of Illinois/NCSA Open Source License')
makedepends=('gcc-libs' 'libffi' "gcc" 'make' 'bison' 'flex')
source=(http://llvm.org/releases/$pkgver/$pkgname-$pkgver.tar.gz
        http://llvm.org/releases/$pkgver/clang-$pkgver.tar.gz
        clang-plugin-loader-registry.patch
        cindexer-clang-path.patch
        clang-pure64.patch)
sha256sums=('519eb11d3499ce99c6ffdb8718651fc91425ed7690eac91c8d6853474f7c0477'
            'b64e72da356d7c3428cfd7ac620d49ec042c84eaee13c26024879f555f4e126d'
            'a0a4494f2a692789670be37fd390906dcaa37b1824f740bdaaea21182f2f3a9c'
            '3074df5322900e087377a8e03a02115463ccc0011c25917c2f06df11facd9b92'
            '288a82fbff17bc554f5863734246500e637882af33ee8511019d5e0d6cd20524')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}.src"

  # At the present, clang must reside inside the LLVM source code tree to build
  # See http://llvm.org/bugs/show_bug.cgi?id=4840
  rm -rf tools/clang
  cp -r "${srcdir}/clang-${pkgver}.src" tools/clang

  # Fix installation directories, ./configure doesn't seem to set them right
  sed -i -e 's:\$(PROJ_prefix)/etc/llvm:/etc/llvm:' \
         -e 's:\$(PROJ_prefix)/lib:$(PROJ_prefix)/lib/llvm:' \
         -e 's:\$(PROJ_prefix)/docs/llvm:$(PROJ_prefix)/share/doc/llvm:' \
    Makefile.config.in

  # Fix insecure rpath (http://bugs.archlinux.org/task/14017)
  sed -i 's:$(RPATH) -Wl,$(\(ToolDir\|LibDir\|ExmplDir\))::g' Makefile.rules

  # Get the correct list of symbols to export
  # See http://lists.cs.uiuc.edu/pipermail/cfe-dev/2010-April/008559.html
  patch -Np1 -i ""${srcdir}"/clang-plugin-loader-registry.patch"

  # Fix clang path in CIndexer.cpp (https://bugs.archlinux.org/task/22799)
  patch -d tools/clang -Np0 -i "${srcdir}/cindexer-clang-path.patch"

  if [[ $CARCH == x86_64 ]]; then
    # Adjust linker paths
    patch -d tools/clang -Np0 -i "${srcdir}/clang-pure64.patch"
  fi

  # Apply strip option to configure
  _optimized_switch="enable"
  [[ $(check_option strip) == n ]] && _optimized_switch="disable"

  # Include location of libffi headers in CPPFLAGS
  export CPPFLAGS="$CPPFLAGS $(pkg-config --cflags libffi)"

  # Force the use of GCC instead of clang
  CC=gcc CXX=g++ \
  ./configure \
    --prefix=/usr \
    --build=${CBUILD} \
    --host=${CHOST} \
    --libdir=/usr/lib/llvm \
    --sysconfdir=/etc \
    --enable-shared \
    --enable-libffi \
    --enable-targets=all \
    --enable-bindings=none \
    --disable-expensive-checks \
    --disable-debug-runtime \
    --disable-assertions \
    --disable-docs \
    --$_optimized_switch-optimized

  make REQUIRES_RTTI=1 VERBOSE=1
}

package_llvm() {
  pkgdesc="Low Level Virtual Machine"
  depends=('perl' 'libffi')

  cd "${srcdir}/${pkgname}-${pkgver}.src"

  # We move the clang directory out of the tree so it won't get installed and
  # then we bring it back in for the clang package
  mv tools/clang "${srcdir}"
  make DESTDIR="${pkgdir}" install
  mv "${srcdir}/clang" tools

  # Fix permissions of static libs
  chmod -x "${pkgdir}"/usr/lib/llvm/*.a

  # Fix libdir in llvm-config (http://bugs.archlinux.org/task/14487)
  sed -i 's:\(ABS_RUN_DIR/lib\):\1/llvm:' "${pkgdir}/usr/bin/llvm-config"

  # Get rid of example Hello transformation
  rm "${pkgdir}"/usr/lib/llvm/*LLVMHello.*

  # Add ld.so.conf.d entry
  install -d "${pkgdir}/etc/ld.so.conf.d"
  echo /usr/lib/llvm > "${pkgdir}/etc/ld.so.conf.d/llvm.conf"

  install -Dm644 LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_clang() {
  pkgdesc="C language family frontend for LLVM"
  url="http://clang.llvm.org/"
  # It looks like clang still needs GCC to assemble and link object files
  # See http://old.nabble.com/%22clang--v%22-shows-a-GCC-call-td28378453.html
  depends=('llvm' 'gcc')

  cd "${srcdir}/llvm-${pkgver}.src/tools/clang"
  make DESTDIR="${pkgdir}" install

  # Fix permissions of static libs
  chmod -x "${pkgdir}"/usr/lib/llvm/*.a

  install -Dm644 LICENSE.TXT "${pkgdir}/usr/share/licenses/clang/LICENSE"
}
