# Based upon the PKGBUILD from Arch Linux
pkgname=sox
pkgver=14.3.2
pkgrel=1
pkgdesc="The Swiss Army knife of sound processing tools"
arch=('i686' 'x86_64')
url="http://sox.sourceforge.net/"
license=('GPL' 'LGPL')
depends=('libltdl')
makedepends=('pkg-config')
options=('!libtool')
# if plugins required:
# options=('libtool')
# find "${pkgdir}" -name '*.la' -exec sed -i -e "/^dependency_libs/s:=.*:='':" {} +
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        sox-14.3.2-ffmpeg.patch)
sha1sums=('026636c90d7accba76225a2821aaa2ffa6fe41a3'
          '86caa6aa86a0bf7a048522114d4efd11acbbda70')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/sox-14.3.2-ffmpeg.patch"
  sed -i 's|man1/sox.1 soxeffect.7|man1/sox.1.gz soxeffect.7.gz|' Makefile.am
  sed -i -e 's:CFLAGS="-g":CFLAGS="$CFLAGS":' configure
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --with-dyn-default \
              --with-distro="Gimokod" \
              --without-alsa \
              --disable-debug \
              --disable-gomp \
              --without-ao \
              --without-oss \
              --without-lame \
              --without-mad \
              --without-sndfile \
              --without-flac \
              --without-oggvorbis \
              --without-ffmpeg \
              --without-ladspa \
              --without-magic \
              --without-id3tag \
              --without-amrnb \
              --without-amrwb \
              --without-png \
              --without-pulseaudio \
              --without-wavpack
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
