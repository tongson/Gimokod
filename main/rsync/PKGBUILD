# Based upon the PKGBUILD from Arch Linux
pkgname=rsync
pkgver=3.0.9
pkgrel=1
pkgdesc="A file transfer program to keep remote files in sync"
arch=('x86_64')
url="http://samba.anu.edu.au/rsync/"
license=('GPL3')
depends=('acl' 'attr')
source=(http://rsync.samba.org/ftp/rsync/${pkgname}-${pkgver}.tar.gz)
sha1sums=('c64c8341984aea647506eb504496999fd968ddfc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./prepare-source
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --with-included-popt \
              --enable-acl-support \
              --enable-xattr-support \
              --enable-ipv6 \
              --disable-iconv
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make test || true # bogus FAIL
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
