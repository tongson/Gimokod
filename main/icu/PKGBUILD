# Based upon the PKGBUILD from Arch Linux
pkgname=icu
pkgver=4.8.1.1
pkgrel=1
pkgdesc="International Components for Unicode library"
arch=(i686 x86_64)
url="http://www.icu-project.org/"
license=('custom:"icu"')
depends=('gcc-libs')
source=(http://download.icu-project.org/files/${pkgname}4c/${pkgver}/${pkgname}4c-${pkgver//./_}-src.tgz
        icu.8198.revert.icu5431.patch)
md5sums=('ea93970a0275be6b42f56953cd332c17'
         'ebd5470fc969c75e52baf4af94a9ee82')

build() {
  cd "${srcdir}"/icu/source
  # fix Malayalam encoding https://bugzilla.redhat.com/show_bug.cgi?id=654200
  patch -Rp3 -i "${srcdir}"/icu.8198.revert.icu5431.patch
  ./configure --prefix=/usr \
              --sbindir=/usr/bin \
              --build=${CBUILD} \
              --host=${CHOST} \
              --disable-debug \
              --disable-samples \
              --sysconfdir=/etc \
              --mandir=/usr/share/man
  make
}

check() {
  cd "${srcdir}"/icu/source
  make -j1 check
}

package() {
  cd "${srcdir}"/icu/source
  make -j1 DESTDIR="${pkgdir}" install

  # Install license
  install -Dm644 "${srcdir}"/icu/license.html "${pkgdir}"/usr/share/licenses/icu/license.html
}
