# Based upon the PKGBUILD from Arch Linux
pkgname=dbus-glib
pkgver=0.98
pkgrel=1
pkgdesc="GLib bindings for DBUS"
arch=(i686 x86_64)
license=('GPL')
url="http://www.freedesktop.org/wiki/Software/DBusBindings"
depends=('dbus-core' 'glib')
makedepends=('pkg-config')
options=('!libtool' '!emptydirs')
source=(http://dbus.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('3f7f117dc7c8d899d9acfdcbf9253fb8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --enable-static=no \
              --enable-bash-completion=no \
              --disable-verbose-mode \
              --disable-asserts \
              --disable-gtk-doc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
