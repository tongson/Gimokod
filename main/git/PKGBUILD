# Based upon the PKGBUILD from Arch and ebuild from Gentoo Linux
pkgname=git
pkgver=1.7.10.1
pkgrel=1
pkgdesc="the fast distributed version control system"
arch=('x86_64')
url="http://git-scm.com/"
license=('GPL2')
depends=('zlib')
source=("http://git-core.googlecode.com/files/git-"${pkgver}".tar.gz"
        "http://git-core.googlecode.com/files/git-manpages-"${pkgver}".tar.gz"
        git-1.7.3.5-optional-cvs.patch)

build() {
  export PYTHON_PATH=""
  cd "${srcdir}"/"${pkgname}"-"${pkgver}"
  Patch git-1.7.3.5-optional-cvs.patch
  sed -i -e '/\/usr\/local/s/BASIC_/#BASIC_/' Makefile
  sed -i -e '/private-Error.pm/s,^,#,' perl/Makefile.PL
  make prefix=/usr gitexecdir=/usr/lib/git-core \
    CFLAGS="$CFLAGS" LDFLAGS="$LDFLAGS" \
    PERL_MM_OPT="" \
    GIT_TEST_OPTS="--no-color" \
    NO_CROSS_DIRECTORY_HARDLINKS=1 \
    NO_EXPAT=YesPlease \
    NO_FINK=YesPlease \
    NO_DARWIN_PORTS=YesPlease \
    INSTALL=install TAR=tar \
    SHELL_PATH=/usr/bin/sh \
    SANE_TOOL_PATH=\
    OLD_ICONV= \
    NO_EXTERNAL_GREP= \
    NO_ICONV=YesPlease \
    NO_TCLTK=YesPlease \
    NO_PERL=YesPlease \
    NO_PYTHON=YesPlease \
    NO_SVN_TESTS=YesPlease \
    THREADED_DELTA_SEARCH=YesPlease \
    NO_CVS=YesPlease \
    BLK_SHA1=YesPlease \
    all
}

package() {
  export PYTHON_PATH=""
  cd "${srcdir}"/"${pkgname}"-"${pkgver}"
  make prefix=/usr gitexecdir=/usr/lib/git-core \
    CFLAGS="$CFLAGS" LDFLAGS="$LDFLAGS" \
    PERL_MM_OPT="" \
    GIT_TEST_OPTS="--no-color" \
    NO_CROSS_DIRECTORY_HARDLINKS=1 \
    NO_EXPAT=YesPlease \
    NO_FINK=YesPlease \
    NO_DARWIN_PORTS=YesPlease \
    INSTALL=install TAR=tar \
    SHELL_PATH=/usr/bin/sh \
    SANE_TOOL_PATH=\
    OLD_ICONV= \
    NO_EXTERNAL_GREP= \
    NO_ICONV=YesPlease \
    NO_TCLTK=YesPlease \
    NO_PERL=YesPlease \
    NO_PYTHON=YesPlease \
    NO_SVN_TESTS=YesPlease \
    THREADED_DELTA_SEARCH=YesPlease \
    NO_CVS=YesPlease \
    BLK_SHA1=YesPlease \
    DESTDIR="${pkgdir}" install

  # more contrib stuff. Nah don't what them for now.
  # cp -a ./contrib "${pkgdir}"/usr/share/git/

  find "${pkgdir}" -name '*.py' -delete

  # how 'bout some manpages? Yes Please.
  for mansect in man1 man5 man7; do
    for manpage in "${srcdir}"/$mansect/*; do
      install -D -m644 $manpage "${pkgdir}"/usr/share/man/$mansect/$(basename $manpage)
    done
  done

  # remove perllocal.pod, .packlist, and empty directories.
  rm -rf "${pkgdir}"/usr/lib/perl5

  rm -f "${pkgdir}"/usr/lib/git-core/git-svn \
        "${pkgdir}"/usr/share/man/man1/git-svn.1*
}

sha1sums=('870361f93fad7999f121bc7bc91a743feda68bdb'
          '5bff761bc40a6b3cfe35ba824357dcf611dbd5d2'
          '58832c8141a0fd3681c6e9c98600526d290aa637')
