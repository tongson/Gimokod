# Based upon the PKGBUILD from Arch Linux
pkgname=vim
_topver=7.3
_patchlevel=475
__hgrev=74686ae0b181
_versiondir="vim${_topver//./}"
pkgver=${_topver}.${_patchlevel}
pkgrel=1
arch=('i686' 'x86_64')
license=('custom:vim')
url="http://www.vim.org"
source=(ftp://ftp.archlinux.org/other/vim/${pkgname}-${pkgver}.tar.xz)
depends=('ncurses')
install=vim.install
sha1sums=('56529692b35df97e8a0f8610122957ba93033545')

build() {
  cd "${srcdir}"

  cp -a ${pkgname}-${pkgver} vim-build

  # define the place for the global (g)vimrc file (set to /etc/vimrc)
  sed -i 's|^.*\(#define SYS_.*VIMRC_FILE.*"\) .*$|\1|' \
    vim-build/src/feature.h
  sed -i 's|^.*\(#define VIMRC_FILE.*"\) .*$|\1|' \
    vim-build/src/feature.h

  (cd vim-build/src && autoconf)

  cd "${srcdir}"/vim-build

  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --localstatedir=/var/lib/vim \
              --with-compiledby=GimokodLinux \
              --disable-gpm \
              --disable-acl \
              --with-x=no \
              --disable-gui \
              --disable-multibyte \
              --disable-cscope \
              --disable-netbeans \
              --disable-perlinterp \
              --disable-pythoninterp \
              --disable-python3interp \
              --disable-rubyinterp \
              --disable-luainterp \
              --disable-darwin \
              --disable-nls
  make

}

package() {
  cd "${srcdir}"/vim-build
  make -j1 VIMRCLOC=/etc DESTDIR="${pkgdir}" install

  # provided by (n)vi in core
  rm "${pkgdir}"/usr/bin/{ex,view}

  # delete some manpages
  find "${pkgdir}"/usr/share/man -type d -name 'man1' 2>/dev/null | \
    while read _mandir; do
    cd ${_mandir}
    rm -f ex.1 view.1 # provided by (n)vi
    rm -f evim.1    # this does not make sense if we have no GUI
  done

  # license
  install -Dm644 "${srcdir}"/vim-${pkgver}/runtime/doc/uganda.txt \
    "${pkgdir}"/usr/share/licenses/${pkgname}/license.txt
    
  # rgb.txt file
  install -Dm644 "${srcdir}"/vim-${pkgver}/runtime/rgb.txt \
    "${pkgdir}"/usr/share/vim/${_versiondir}/rgb.txt
}

