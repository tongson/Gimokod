# Based upon the PKGBUILD from Arch Linux
pkgname=giflib
pkgver=4.1.6
pkgrel=1
pkgdesc="A library for reading and writing gif images"
url="http://sourceforge.net/projects/giflib/"
arch=('i686' 'x86_64')
license=('MIT')
depends=('libx11')
provides=("libungif=${pkgver}")
conflicts=('libungif')
replaces=('libungif')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/giflib/${pkgname}-${pkgver}.tar.bz2)
md5sums=('7125644155ae6ad33dbc9fc15a14735f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i '/AC_PATH_XTRA/d' configure.ac
  libtoolize -f
  autoreconf
  ./configure --prefix=/usr --disable-static
  make
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

  # libungif compatibility - instructions taken from Redhat specfile
  MAJOR=`echo ${pkgver} | sed 's/\([0-9]\+\)\..*/\1/'`
  gcc -shared -Wl,-soname,libungif.so.${MAJOR} -Llib/.libs -lgif -o libungif.so.${pkgver}
  install -m755 libungif.so.${pkgver} "${pkgdir}/usr/lib/"
  ln -sf libungif.so.${pkgver} "${pkgdir}/usr/lib/libungif.so.4"
  ln -sf libungif.so.4 "${pkgdir}/usr/lib/libungif.so"
}
