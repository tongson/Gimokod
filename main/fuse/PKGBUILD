# Based upon the PKGBUILD from Arch Linux
pkgname=fuse
pkgver=2.8.5
pkgrel=1
pkgdesc="A library that makes it possible to implement a filesystem in a userspace program."
arch=('i686' 'x86_64')
url="http://fuse.sourceforge.net/"
license="GPL2"
makedepends=('pkg-config')
backup=(etc/fuse.conf)
install=fuse.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz
        'fuse.conf')
options=('!libtool')
sha1sums=('862320b56d6bc4ad5e7be2b1e0b5029166aae09b'
          '3b42e37a741d4651099225987dc40e7f02a716ad')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --enable-lib \
              --enable-util \
              --bindir=/bin \
              --disable-example
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # static device nodes will cause collision.
  rm -rf "${pkgdir}"/dev

  # Remove init script in wrong path
  # Don't add our own for now, as fusectl fs oopses on 2.6.18
  rm -rf "${pkgdir}"/etc/init.d
  #install -D -m755 "${srcdir}"/fuse.rc.d "${pkgdir}"/etc/rc.d/fuse

  install -d "${pkgdir}"/lib
  mv "${pkgdir}"/usr/lib/libfuse.so.${pkgver} "${pkgdir}"/lib/
  ln -s ../../lib/libfuse.so.${pkgver} "${pkgdir}"/usr/lib/libfuse.so.${pkgver}
  ln -s libfuse.so.${pkgver} "${pkgdir}"/lib/libfuse.so.2
  mv "${pkgdir}"/usr/lib/libulockmgr.so.1.0.1 "${pkgdir}"/lib/
  ln -s ../../lib/libulockmgr.so.1.0.1 "${pkgdir}"/usr/lib/libulockmgr.so.1.0.1
  ln -s libulockmgr.so.1.0.1 "${pkgdir}"/lib/libulockmgr.so.1

  # install sample config file
  install -Dm644 "${srcdir}"/fuse.conf "${pkgdir}"/etc/fuse.conf
  # Fix udev rule location
  install -d -m755 "${pkgdir}/lib"
  mv "${pkgdir}/etc/udev" "${pkgdir}/lib/"
}
