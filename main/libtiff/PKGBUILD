# Based upon the PKGBUILD from Arch Linux
pkgname=libtiff
pkgver=3.9.5
pkgrel=1
pkgdesc="Library for manipulation of TIFF images"
arch=('i686' 'x86_64')
url="http://www.remotesensing.org/libtiff/"
license=('custom')
depends=('zlib')
options=('!libtool')
source=(ftp://ftp.remotesensing.org/pub/libtiff/tiff-${pkgver}.tar.gz)
md5sums=('8fc7ce3b4e1d0cc8a319336967815084')
sha1sums=('f40aab20fb2f609b5cbc1171c40b66a1445e3773')

build() {
  cd "${srcdir}/tiff-${pkgver}"
  ./configure --prefix=/usr \
              --without-x \
              --disable-jpeg \
              --disable-jbig \
              --enable-zlib
  make
}

package() {
  cd "${srcdir}/tiff-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
