# Based upon the PKGBUILD from Arch Linux
pkgname=libtheora
pkgver=1.1.1
pkgrel=1
pkgdesc="An open video codec developed by the Xiph.org"
arch=('i686' 'x86_64')
url="http://www.xiph.org"
license=('BSD')
depends=('libogg')
makedepends=('libvorbis')
options=('!libtool')
source=("http://downloads.xiph.org/releases/theora/libtheora-${pkgver}.tar.bz2")
md5sums=('292ab65cedd5021d6b7ddd117e07cd8e')
sha1sums=('8dcaa8e61cd86eb1244467c0b64b9ddac04ae262')

build() {
  cd "${srcdir}/libtheora-${pkgver}"
  export ac_cv_prog_HAVE_DOXYGEN="false"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --enable-shared \
              --disable-static \
              --disable-dependency-tracking \
              --disable-spec \
              --enable-encode \
              --disable-examples
  make
}

package() {
  cd "${srcdir}/libtheora-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
