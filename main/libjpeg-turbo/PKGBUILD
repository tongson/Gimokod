# Based upon the PKGBUILD from Arch Linux
pkgname=libjpeg-turbo
pkgver=1.2.0
pkgrel=1
pkgdesc="libjpeg derivative with accelerated baseline JPEG compression and decompression"
arch=('i686' 'x86_64')
url="http://libjpeg-turbo.virtualgl.org/"
license=('GPL' 'custom')
makedepends=('nasm')
provides=('libjpeg=8.0.2')
conflicts=('libjpeg')
replaces=('libjpeg')
options=('!libtool')
source=(http://sourceforge.net/projects/${pkgname}/files/${pkgver}/${pkgname}-${pkgver}.tar.gz)
md5sums=('5329fa80953938cb4f097afae55059e2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i "/docdir = /s#/doc#/doc/libjpeg-turbo#" Makefile.in
  sed -i "/exampledir = /s#/doc#/doc/libjpeg-turbo#" Makefile.in

  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --with-jpeg8 \
              --mandir=/usr/share/man
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install

  # provide jpegint.h as it is required by various software
  install -m644 jpegint.h "${pkgdir}"/usr/include/

  # do not distributre libturbojpeg as it is unversioned
  rm "${pkgdir}"/usr/lib/libturbojpeg.{so,a}
  rm "${pkgdir}"/usr/include/turbojpeg.h
  rm "${pkgdir}"/usr/bin/tjbench # needs libturbojpeg.so

  install -dm755 $pkgdir/usr/share/licenses/libjpeg-turbo/
  ln -s ../../doc/libjpeg-turbo/README \
    $pkgdir/usr/share/licenses/libjpeg-turbo/README
  ln -s ../../doc/libjpeg-turbo/README-turbo.txt \
    $pkgdir/usr/share/licenses/libjpeg-turbo/README-turbo.txt
}
