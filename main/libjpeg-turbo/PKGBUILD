# Based upon the PKGBUILD from Arch Linux
pkgname=libjpeg-turbo
pkgver=1.1.1
pkgrel=1
pkgdesc="libjpeg derivative with accelerated baseline JPEG compression and decompression"
arch=('i686' 'x86_64')
url="http://libjpeg-turbo.virtualgl.org/"
license=('GPL' 'custom')
depends=
makedepends=('nasm')
provides=('libjpeg=8.0.2')
conflicts=('libjpeg')
replaces=('libjpeg')
options=('!libtool')
source=(http://sourceforge.net/projects/${pkgname}/files/${pkgver}/${pkgname}-${pkgver}.tar.gz)
md5sums=('03b9c1406c7bfdc204313c2917ce6962')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --with-jpeg8 --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install

  # provide jpegint.h as it is required by various software
  install -m644 jpegint.h "${pkgdir}"/usr/include/

  # do not distributre libturbojpeg as it is unversioned
  rm "${pkgdir}"/usr/lib/libturbojpeg.{so,a}
  rm "${pkgdir}"/usr/include/turbojpeg.h
  rm "${pkgdir}"/usr/bin/jpgtest

  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/libjpeg-turbo/LICENSE"
}
