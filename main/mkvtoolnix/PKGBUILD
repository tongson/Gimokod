# Based upon the PKGBUILD from Arch Linux
pkgname=mkvtoolnix
pkgver=5.6.0
pkgrel=1
arch=('x86_64')
license=('GPL')
url="http://www.bunkus.org/videotools/mkvtoolnix/index.html"
depends=('libmatroska' 'expat' 'flac' 'libvorbis' 'file' 'boost-libs' 'zlib')
makedepends=('libmatroska' 'flac' 'libvorbis' 'file'
             'boost-libs' 'boost' 'rake')
source=("http://www.bunkus.org/videotools/${pkgname}/sources/${pkgname}-${pkgver}.tar.bz2")
md5sums=('53e96b613752913da800dcde7b65cf6d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Disable automagic curl dep used for online update checking
  sed -i -e '/curl/d' configure.in
  export CURL_CFLAGS="" CURL_LIBS=""

  export CFLAGS="${CFLAGS} -DBOOST_FILESYSTEM_VERSION=3"
  export CXXFLAGS="${CXXFLAGS} -DBOOST_FILESYSTEM_VERSION=3"

  autoreconf

  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --with-boost-libdir=/usr/lib \
              --disable-precompiled-headers \
              --disable-gui \
              --disable-lzo \
              --disable-bz2 \
              --disable-wxwidgets \
              --disable-debug \
              --disable-qt \
              --without-gettext \
              --with-boost-regex=boost_regex \
              --with-boost-filesystem=boost_filesystem \
              --with-boost-system=boost_system

  rake
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  rake DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/bin/mmg
  rm -rf "${pkgdir}"/usr/share/man/ja/man1/mmg.1
  rm -rf "${pkgdir}"/usr/share/man/man1/mmg.1
  rm -rf "${pkgdir}"/usr/share/man/nl/man1/mmg.1
  rm -rf "${pkgdir}"/usr/share/man/zh_CN/man1/mmg.1
  rm -rf "${pkgdir}"/usr/share/man/uk/man1/mmg.1
  rm -rf "${pkgdir}"/usr/share/applications
  rm -rf "${pkgdir}"/usr/share/icons
  rm -rf "${pkgdir}"/usr/share/mime
}
