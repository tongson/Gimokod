# Based upon the PKGBUILD from Arch Linux
pkgname=zip
pkgver=3.0
_pkgver=30
pkgrel=1
pkgdesc="Creates PKZIP-compatible .zip files"
arch=(i686 x86_64)
url="http://www.info-zip.org/pub/infozip/Zip.html"
license=('BSD')
options=('!makeflags')
source=("ftp://ftp.info-zip.org/pub/infozip/src/${pkgname}${_pkgver}.zip")
sha256sums=('7061ceac0407682b6dc54bb480347205f680f4e56cf34fe1423df2309f18968a')

build() {
   cd "${srcdir}/${pkgname}${_pkgver}"
   patch -Np1 < ../zip-3.0-build.patch
   patch -Np1 < ../zip-3.0-exec-stack.patch
   patch -Np0 < ../zip-3.0-no-crypt.patch
   patch -Np0 < ../zip-3.0-pic.patch
   CFLAGS="${CFLAGS} -DNO_BZIP2_SUPPORT -DNO_CRYPT"
   make -f unix/Makefile LOCAL_ZIP="${CFLAGS} ${CXXFLAGS}" prefix=/usr cc=/usr/bin/gcc generic_gcc
}

package() {
   cd "${srcdir}/${pkgname}${_pkgver}"
   make -f unix/Makefile INSTALL=/bin/install prefix="${pkgdir}"/usr \
     MANDIR="${pkgdir}"/usr/share/man/man1 install
   install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

source=(${source[@]-}
        zip-3.0-build.patch
        zip-3.0-exec-stack.patch
        zip-3.0-no-crypt.patch
        zip-3.0-pic.patch)

sha256sums=(${sha256sums[@]-}
            'a66386c3a48019d8063a48c3610a28862412926f04a3b8f44662c6a87bb250f5'
            '5e46b247107c71f34e00c93d27d9f5cf0812d1c8ffc171fc71addee715308ff5'
            'e5bbe9ce46a3ab89e7dab3515cbe119c57f3330d289b7fca967c5da9b6844cff'
            '5f64ef0fc1afc0256fdb3a503dda10de6c5df576b8bfad328de720c68f8b777a')
