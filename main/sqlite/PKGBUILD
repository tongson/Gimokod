# Based upon the PKGBUILD from Arch Linux
pkgname=sqlite
_amalgamationver=3071000
_amalgamationver2=${_amalgamationver/00/}
_docver=${_amalgamationver}  #3070700
#pkgver=${_amalgamationver2//0/.}
pkgver=3.7.10
pkgrel=1
pkgdesc="A C library that implements an SQL database engine"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.sqlite.org/"
options=('!libtool' '!emptydirs')
source=(http://www.sqlite.org/sqlite-autoconf-$_amalgamationver.tar.gz
        license.txt)
md5sums=('9ed2ca93577b58cfa0d01f64b9312ab9'
         'c1cdbc5544034d9012e421e75a5e4890')

build() {
  export CFLAGS="${CFLAGS} -DSQLITE_ENABLE_FTS3=1 -DSQLITE_ENABLE_COLUMN_METADATA=1 -DSQLITE_ENABLE_UNLOCK_NOTIFY -DSQLITE_SECURE_DELETE -DNDEBUG -DSQLITE_ENABLE_RTREE"

  cd "${srcdir}"/sqlite-autoconf-$_amalgamationver
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --disable-readline \
              --disable-static
  make
}

package() {
  cd "${srcdir}"/sqlite-autoconf-$_amalgamationver
  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}"/license.txt "${pkgdir}"/usr/share/licenses/${pkgname}/license.txt
}
