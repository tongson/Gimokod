# Based upon the PKGBUILD from Arch Linux
pkgname=rasqal
pkgver=0.9.27
pkgrel=1
pkgdesc="library that handles RDF query syntaxes/construction/execution"
url="http://librdf.org/rasqal"
license=('GPL' 'LGPL')
arch=('i686' 'x86_64')
depends=('raptor' 'mpfr')
options=('!libtool')
source=(http://download.librdf.org/source/${pkgname}-${pkgver}.tar.gz)
md5sums=('dd48b9a80947a6136fbdb79276d476e2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --disable-static \
              --enable-release \
              --with-regex-library=posix \
              --with-digest-library=internal \
              --with-decimal=mpfr \
              --disable-pcre
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
