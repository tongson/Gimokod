# Based upon the PKGBUILD from Arch Linux
pkgname=rasqal
pkgver=0.9.29
pkgrel=1
pkgdesc="library that handles RDF query syntaxes/construction/execution"
url="http://librdf.org/rasqal"
license=('GPL' 'LGPL')
arch=('x86_64')
depends=('raptor' 'mpfr')
options=('!libtool')
source=(http://download.librdf.org/source/${pkgname}-${pkgver}.tar.gz)
md5sums=('49e4b75a0c67465edf55dd20606715fa')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --disable-static \
              --enable-release \
              --with-regex-library=posix \
              --with-digest-library=internal \
              --with-decimal=mpfr \
              --disable-pcre
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -k check || true
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
