# Based upon the PKGBUILD from Arch Linux
pkgname=libpng
pkgver=1.5.8
_apngver=1.5.8
pkgrel=2
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('i686' 'x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('zlib')
options=('!libtool')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.xz"
        CVE-2011-3026.patch
        "http://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-$_apngver-apng.patch.gz")
md5sums=('0f7ae352beadaff78073733905613041'
         'e3f19c889e57135eed66d0a3a22e2912'
         '158772fecdc6d8591bcd382c04da334c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Add animated PNG (apng) support
  # see http://sourceforge.net/projects/libpng-apng/
  patch -p1 -i "${srcdir}/libpng-${_apngver}-apng.patch"
  Patch CVE-2011-3026.patch

  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST}
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  cd contrib/pngminus
  make PNGLIB="-L${pkgdir}/usr/lib -lpng" -f makefile.std png2pnm pnm2png
  install -m755 png2pnm pnm2png "${pkgdir}/usr/bin/"
  install -D -m644 ../../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
