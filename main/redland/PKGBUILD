# Based upon the PKGBUILD from Arch Linux
pkgname=redland
pkgver=1.0.15
pkgrel=3
url="http://librdf.org/"
license=("GPL")
arch=('x86_64')
depends=('libltdl' 'libxml2' 'openssl' 'rasqal' 'raptor')
makedepends=('rasqal' 'raptor')
options=('!libtool')
source=(http://download.librdf.org/source/${pkgname}-${pkgver}.tar.gz
        rpath.diff)
md5sums=('b0deb87f3c7d3237a3d587c1e0f2f266'
         'acc85e784f01a656bd56777f95880787')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --enable-release \
              --disable-static \
              --with-raptor=system \
              --with-rasqal=system \
              --with-xml-parser=libxml \
              --with-openssl-digests \
              --without-mysql \
              --without-postgresql \
              --without-sqlite \
              --without-bdb \
              --without-virtuoso \
              --without-unixodbc \
              --without-iodbc
  # nuke rpath
  patch -Np0 -i "${srcdir}"/rpath.diff

  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/lib/redland
}




