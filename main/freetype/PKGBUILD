# Based upon the PKGBUILD from Arch Linux
pkgname=freetype
pkgver=2.4.7
pkgrel=1
pkgdesc="TrueType font rendering library"
arch=(i686 x86_64)
license=('GPL')
url="http://freetype.sourceforge.net"
depends=('zlib')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2
        freetype-2.4.6-memcpy-fix.patch
        freetype-2.3.0-enable-spr.patch
        freetype-2.2.1-enable-valid.patch)
md5sums=('dbadce8f0c5e70a0b7c51eadf2dd9394'
         '822951cf86a01494929c357a7e15504a'
         '816dc8619a6904a7385769433c0a8653'
         '214119610444c9b02766ccee5e220680')

build() {
  cd "${srcdir}/freetype-${pkgver}"
  patch -Np1 -i "${srcdir}/freetype-2.4.6-memcpy-fix.patch"
  patch -Np1 -i "${srcdir}/freetype-2.3.0-enable-spr.patch"
  patch -Np1 -i "${srcdir}/freetype-2.2.1-enable-valid.patch"

  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --mandir=/usr/share/man \
              --disable-static
  make
}

package() {
  cd "${srcdir}/freetype-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
