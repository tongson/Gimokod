# Based upon the PKGBUILD from Arch Linux
pkgname=stfl
pkgver=0.22
pkgrel=1
pkgdesc="Library implementing a curses-based widget set for text terminals"
arch=('i686' 'x86_64')
url="http://clifford.at/stfl/"
license=('GPL3')
depends=('ncurses')
options=('!libtool')
source=(http://clifford.at/stfl/"${pkgname}"-"${pkgver}".tar.gz
        "${pkgname}"-archlinux.patch)
sha256sums=('2dddafd331e61ad04baf1251ccecb3a69feaf99880dd75155be8423c1cc55418'
            'c7d7c787bcd287a776aad3b26e6a90a3b121c281125c34c7c35a87f36dcdc453')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i \
        -e 's/-Os -ggdb//' \
        -e 's/^\(all:.*\) example/\1/' \
        -e 's/$(CC) -shared/$(CC) $(LDFLAGS) -shared/' \
        Makefile

  patch -Np1 -i "${srcdir}"/"${pkgname}"-archlinux.patch

  make prefix=/usr Makefile.deps
  make prefix=/usr
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make prefix=/usr DESTDIR="${pkgdir}" install
}
