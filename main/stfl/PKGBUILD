# Based upon the PKGBUILD from Arch Linux
pkgname=stfl
pkgver=0.21
pkgrel=1
pkgdesc="Library implementing a curses-based widget set for text terminals"
arch=('i686' 'x86_64')
url="http://clifford.at/stfl/"
license=('GPL3')
depends=('ncurses')
options=('!libtool')
source=(http://clifford.at/stfl/"${pkgname}"-"${pkgver}".tar.gz 
        "${pkgname}"-archlinux.patch)
md5sums=('888502c3f332a0ee66e490690d79d404'
         'a6231f269038c76a54fd08d6417f3d6d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i \
        -e 's/-Os -ggdb//' \
        -e 's/^\(all:.*\) example/\1/' \
        -e 's/$(CC) -shared/$(CC) $(LDFLAGS) -shared/' \
        Makefile

  patch -Np1 -i "${srcdir}"/"${pkgname}"-archlinux.patch

  make prefix=/usr Makefile.deps
  make prefix=/usr
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make prefix=/usr DESTDIR="${pkgdir}" install
}
