# Based upon the PKGBUILD from Arch Linux
pkgname=schroedinger
pkgver=1.0.11
pkgrel=1
pkgdesc="An implemenation of the Dirac video codec in ANSI C code"
arch=('i686' 'x86_64')
url="http://www.diracvideo.org/"
license=('GPL2' 'LGPL2.1' 'MPL' 'MIT')
depends=('orc')
options=('!libtool')
source=(http://www.diracvideo.org/download/schroedinger/${pkgname}-${pkgver}.tar.gz)
md5sums=('da6af08e564ca1157348fb8d92efc891')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e '/AS_COMPILER_FLAG(-O3/d' configure.ac
  autoreconf -I m4 -fi
  ./configure --prefix=/usr \
              --disable-gtk-doc \
              --build=${CBUILD} \
              --host=${CHOST}

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m644 -D COPYING.MIT "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING.MIT
}
