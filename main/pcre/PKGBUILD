# Based upon the PKGBUILD from Arch Linux
pkgname=pcre
pkgver=8.30
pkgrel=1
pkgdesc="A library that implements Perl 5-style regular expressions"
arch=('i686' 'x86_64')
url="http://www.pcre.org/"
license=('BSD')
depends=('zlib')
makedepends=('pkg-config')
options=('!libtool')
source=(ftp://ftp.csx.cam.ac.uk/pub/software/programming/pcre/${pkgname}-${pkgver}.tar.bz2{,.sig})
md5sums=('98e8928cccc945d04279581e778fbdff'
         '01eee0333fa71687e0b6756db77f94ad')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  [ "${CARCH}" = "x86_64" ] && export CFLAGS="${CFLAGS} -fPIC"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --enable-shared \
              --disable-pcretest-libreadline \
              --enable-pcre16 \
              --enable-pcre8 \
              --enable-pcregrep-libz \
              --enable-jit \
              --enable-pcregrep-jit \
              --disable-cpp \
              --without-pcregrep-libbz2 \
              --enable-utf8 \
              --enable-unicode-properties
  make
}

check() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make check
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENCE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
