# Based upon the PKGBUILD from Arch Linux
pkgname=aalib
pkgver=1.4rc5
pkgrel=1
pkgdesc="A portable ASCII art graphic library"
arch=('i686' 'x86_64')
url="http://aa-project.sourceforge.net/aalib/"
license=('LGPL')
depends=('ncurses')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/aa-project/${pkgname}-${pkgver}.tar.gz
  aclocal-fixes.patch)
md5sums=('9801095c42bba12edebd1902bcf0a990' '863a96a6689aa7ee073ca448bc2f133d')
sha1sums=('a23269e950a249d2ef93625837cace45ddbce03b' '98d8c5a13672a8107945d694ff4520e0bc87d3e9')

build() {
  cd "${srcdir}/${pkgname}-1.4.0"
  sed -i -e 's:#include <malloc.h>:#include <stdlib.h>:g' src/*.c
  patch -p0 -i "${srcdir}/aclocal-fixes.patch"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --without-x11-driver \
              --without-slang-driver \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info
  make
}

package() {
  cd "${srcdir}/${pkgname}-1.4.0"
  make DESTDIR="${pkgdir}" install
}
