# Based upon the PKGBUILD from Arch Linux
pkgname=newsbeuter
pkgver=2.5
pkgrel=1
pkgdesc="A RSS feed reader for the text console with special Podcast support"
arch=('i686' 'x86_64')
url="http://www.newsbeuter.org/"
license=('custom: MIT')
depends=('json-c' 'curl' 'libxml2' 'sqlite' 'stfl')
makedepends=('gettext' 'pkg-config')
options=('!libtool')
source=(http://www.newsbeuter.org/downloads/$pkgname-$pkgver.tar.gz)
sha256sums=('abc54b7bee3e062cd166787edf35aadbeb0ae1f5fcc8d955b77143aaca096825')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e "s:-ggdb:${CXXFLAGS}:" Makefile
  make prefix=/usr
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix=/usr DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/share/locale/"
  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/newsbeuter/LICENSE
}
