# Based upon the PKGBUILD from Arch Linux
pkgname=ctorrent
pkgver=3.3.2
pkgrel=1
pkgdesc='A high-performance BitTorrent client with minimal system resources'
arch=('i686' 'x86_64')
url='http://www.rahul.net/dholmes/ctorrent/'
license=('GPL2')
depends=('openssl')
source=(http://downloads.sourceforge.net/dtorrent/${pkgname}-dnh${pkgver}.tar.gz
        2782875.diff
        ctorrent-3.3.2-negative-ints.patch)
sha256sums=('c87366c91475931f75b924119580abd06a7b3cb3f00fef47346552cab1e24863'
            '9f3fe15956db8e7f0484585adb9ef92b915733cc6e669e746a8c02356eb3d98e'
            'd24d04760a3480e921c54ea1af39e7bb094a8b774ee09bb8849f9c1f76731193')

build() {
  cd "${srcdir}"/${pkgname}-dnh${pkgver}

  patch -Np0 -i "${srcdir}"/2782875.diff
  patch -Np1 -i "${srcdir}"/ctorrent-3.3.2-negative-ints.patch

  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST}
  make
}

package() {
  cd "${srcdir}"/${pkgname}-dnh${pkgver}
  make DESTDIR="${pkgdir}" install
}
