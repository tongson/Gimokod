# Based upon the PKGBUILD from Arch Linux
pkgname=libvpx
pkgver=1.0.0
pkgrel=1
pkgdesc="The VP8 Codec SDK"
arch=('i686' 'x86_64')
url="http://www.webmproject.org/"
license=('BSD')
options=('!makeflags')
makedepends=('yasm')
source=(http://webm.googlecode.com/files/$pkgname-v$pkgver.tar.bz2)
sha1sums=('2919e11074fef72fc61ef1a75160050ffaad6074')

build() {
  cd "$srcdir/$pkgname-v$pkgver"
  CC=/usr/bin/gcc
  ./configure --prefix=/usr \
              --libdir=/usr/lib \
              --as=yasm \
              --enable-vp8 \
              --enable-runtime-cpu-detect \
              --enable-shared \
              --enable-postproc \
              --enable-pic \
              --disable-install-docs \
              --disable-install-srcs \
              --enable-multithread \
              --disable-debug \
              $([[ $CARCH = i686 ]] && echo "--target=x86-linux-gcc") \
              $([[ $CARCH = x86_64 ]] && echo "--target=x86_64-linux-gcc") \
              --disable-debug-libs
  make
}

package() {
  cd "$srcdir/$pkgname-v$pkgver"
  make DIST_DIR="${pkgdir}/usr" install
  install -D -m 0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
