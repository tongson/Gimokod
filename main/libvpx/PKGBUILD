# Based upon the PKGBUILD from Arch Linux
pkgname=libvpx
pkgver=1.0.0
pkgrel=1
pkgdesc="The VP8 Codec SDK"
arch=('x86_64')
url="http://www.webmproject.org/"
license=('BSD')
options=('!makeflags')
makedepends=('yasm')
source=(http://webm.googlecode.com/files/$pkgname-v$pkgver.tar.bz2
        libvpx-define-config_pic.patch)
sha1sums=('356af5f770c50cd021c60863203d8f30164f6021'
          '19abfcfbe23a10a19a3846d87c156861819f266e')

build() {
  cd "$srcdir/$pkgname-v$pkgver"
  Patch libvpx-define-config_pic.patch
  ./configure --prefix=/usr \
              --libdir=/usr/lib \
              --enable-vp8 \
              --enable-runtime-cpu-detect \
              --enable-shared \
              --enable-postproc \
              --enable-pic \
              --disable-install-docs \
              --disable-install-srcs \
              --enable-multithread \
              --disable-debug \
              --disable-debug-libs
  make
}

package() {
  cd "$srcdir/$pkgname-v$pkgver"
  make DIST_DIR="${pkgdir}/usr" install
  install -D -m 0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
