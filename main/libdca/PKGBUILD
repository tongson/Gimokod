# Based upon the PKGBUILD from Arch Linux
pkgname=libdca
pkgver=0.0.5
pkgrel=1
pkgdesc="Free library for decoding DTS Coherent Acoustics streams"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.videolan.org/developers/libdca.html"
options=('!libtool' '!emptydirs')
source=("http://download.videolan.org/pub/videolan/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('dab6b2795c66a82a6fcd4f8343343021')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --mandir=/usr/share/man \
              --disable-oss \
              --disable-dependency-tracking \
              --disable-debug
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}/usr/share/man/man1"/{extract_dts,dtsdec}.1
  ln -s dcadec.1.gz "${pkgdir}/usr/share/man/man1/dtsdec.1.gz"
  ln -s extract_dca.1.gz "${pkgdir}/usr/share/man/man1/extract_dts.1.gz"
  # remove dangling symlink
  rm "${pkgdir}/usr/lib/libdts.a"
}
