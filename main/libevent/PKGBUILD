# Based upon the PKGBUILD from Arch Linux
pkgname=libevent
pkgver=2.0.15
pkgrel=1
pkgdesc="An event notification library"
arch=('i686' 'x86_64')
url="http://www.monkey.org/~provos/libevent/"
license=('GPL2')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/levent/${pkgname}-${pkgver}-stable.tar.gz{,.asc})
md5sums=('2643abe7ba242df15c08b2cc14ec8759'
         '56e52e1b6bba54102d278da59af4495e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"
  sed -i 's#python#python2#' event_rpcgen.py
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"
  make DESTDIR="${pkgdir}" install
}
