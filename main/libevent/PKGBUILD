# Based upon the PKGBUILD from Arch Linux
pkgname=libevent
pkgver=2.0.16
pkgrel=1
pkgdesc="An event notification library"
arch=('i686' 'x86_64')
url="http://www.monkey.org/~provos/libevent/"
license=('GPL2')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/levent/${pkgname}-${pkgver}-stable.tar.gz{,.asc})
sha1sums=('9eb9fe3c0ec607525ed2dee6827687efcd0696ac'
          '679023511db0106cb84a36e4ff78604d00d0a611')


build() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"
  sed -i 's#python#python2#' event_rpcgen.py
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"
  make DESTDIR="${pkgdir}" install
}
