# Based upon the PKGBUILD from Arch Linux
pkgname=libevent
pkgver=2.0.19
pkgrel=1
pkgdesc="An event notification library"
arch=('x86_64')
url="http://www.monkey.org/~provos/libevent/"
license=('BSD')
options=('!libtool')
source=(https://github.com/downloads/${pkgname}/${pkgname}/${pkgname}-$pkgver-stable.tar.gz{,.asc})
md5sums=('91111579769f46055b0a438f5cc59572'
         'a211997b20913954f744a665f31f77d2')


build() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"
  sed -i 's#python#python2#' event_rpcgen.py
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"
  make check || true
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"
  make DESTDIR="${pkgdir}" install
}
