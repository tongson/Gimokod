# Based upon the PKGBUILD from Arch Linux
pkgname=mythes
pkgver=1.2.1
pkgrel=1
pkgdesc="a simple thesaurus"
arch=('i686' 'x86_64')
url="http://hunspell.sourceforge.net/ "
license=('custom')
depends=('hunspell')
options=('!libtool')
source=(http://downloads.sourceforge.net/hunspell/${pkgname/lib/}-${pkgver}.tar.gz)
md5sums=('54b310488dda6929cf31ae859928c945')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --disable-static
  make
  # run the example program:
  ./example th_en_US_new.idx th_en_US_new.dat checkme.lst
  # run the example program with stemming and morphological generation:
  # e.g. to check mouse, mice, rodents, eats, eaten, ate, eating etc. words
  ./example morph.idx morph.dat morph.lst morph.aff morph.dic
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # license
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
