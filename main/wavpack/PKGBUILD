# Based upon the PKGBUILD from Arch Linux
pkgname=wavpack
pkgver=4.60.1
pkgrel=1
pkgdesc="Audio compression format with lossless, lossy, and hybrid compression modes"
arch=('x86_64')
url="http://www.wavpack.com/"
license=('custom')
options=('!libtool')
source=("http://www.wavpack.com/${pkgname}-${pkgver}.tar.bz2")
md5sums=('7bb1528f910e4d0003426c02db856063')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --enable-mmx \
              --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 license.txt \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
