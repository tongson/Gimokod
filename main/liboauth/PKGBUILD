# Based upon the PKGBUILD from Arch Linux
pkgname=liboauth
pkgver=0.9.6
pkgrel=1
pkgdesc="C library implementing OAuth Core RFC 5849"
arch=('x86_64')
url='http://liboauth.sourceforge.net'
license=('MIT')
depends=('curl' 'openssl')
makedepends=('pkg-config')
options=('!libtool')
source=("$url/pool/"${pkgname}"-"${pkgver}".tar.gz")
sha256sums=('9fa0f9d86ac4fbdd8c17d4c242ceef33096e8cf3b1834d9198ccc0cbf8b0f207')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --disable-static \
              --disable-dependency-tracking \
              --disable-nss \
              --enable-fast-install \
              --enable-libcurl
  make

}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING.MIT "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
