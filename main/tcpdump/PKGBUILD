# Based upon the PKGBUILD from Arch Linux
pkgname=tcpdump
pkgver=4.1.1
pkgrel=1
pkgdesc="A tool for network monitoring and data acquisition"
arch=('i686' 'x86_64')
url="http://www.tcpdump.org"
license=('BSD')
depends=('libpcap' 'openssl')
source=("http://www.tcpdump.org/release/${pkgname}-${pkgver}.tar.gz"
        tcpdump_duplicate_executable.patch)
md5sums=('d0dd58bbd6cd36795e05c6f1f74420b0'
         'd4c374b25d10e41b77d2c72bb9ec0d79')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # do not install tcpdump-4.1.1 executable (FS#23346)
  patch -Np1 -i "${srcdir}/tcpdump_duplicate_executable.patch"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --enable-ipv6 \
              --with-crypto \
              --disable-smb \
              --without-smi
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  #install the license
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
