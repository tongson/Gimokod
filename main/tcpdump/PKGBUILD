# Based upon the PKGBUILD from Arch Linux
pkgname=tcpdump
pkgver=4.2.0
pkgrel=1
pkgdesc="A tool for network monitoring and data acquisition"
arch=('i686' 'x86_64')
url="http://www.tcpdump.org"
license=('BSD')
depends=('libpcap' 'openssl')
options=('emptydirs')
install=tcpdump.install
source=("http://www.tcpdump.org/release/${pkgname}-${pkgver}.tar.gz"
        'tcpdump_duplicate_executable.patch'
        'ppi.h')
md5sums=('0296d4c487c3a156879783643704bd39'
         'b45c978dff8193ff5529d3566e7dbfa3'
         'd898c868bb730e14dbd2afd1ad883b87')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Add missing header file
  cp ${srcdir}/ppi.h .
  sed -i 's:uudecode.*::' Makefile.in
  # do not install tcpdump-4.1.1 executable (FS#23346)
  patch -Np1 -i "${srcdir}/tcpdump_duplicate_executable.patch"
  ./configure --prefix=/usr \
              --sbindir=/usr/bin \
              --build=${CBUILD} \
              --host=${CHOST} \
              --enable-ipv6 \
              --with-crypto \
              --disable-smb \
              --with-user=tcpdump \
              --with-chroot=/var/lib/tcpdump \
              --without-smi
  export CFLAGS=${CFLAGS/-Os/-O2}
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed '/^\(espudp1\|eapon1\)/d;' -i tests/TESTLIST
  make check
}


package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -d -m700 -o 801 -g 801 "${pkgdir}/var/lib/tcpdump"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
