# Based upon the PKGBUILD from Arch Linux
pkgname=faad2
pkgver=2.7
pkgrel=1
pkgdesc="ISO AAC audio decoder"
arch=('i686' 'x86_64')
url="http://www.audiocoding.com/"
license=('custom:GPL')
depends=
options=('!libtool' '!makeflags')
source=(http://downloads.sourceforge.net/sourceforge/faac/${pkgname}-${pkgver}.tar.bz2 LICENSE)
md5sums=('4c332fa23febc0e4648064685a3d4332'
         'ec95f38142e14fb46c849503897a37a0')
sha1sums=('b0e80481d27ae9faf9e46c8c8dfb617a0adb91b5'
          '00edd9b30d8d5098b5e91c03c595d96a3bbe56fd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --without-xmms --disable-dependency-tracking
  make CFLAGS="${CFLAGS} -fPIC"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" manmdir=/usr/share/man/man1 install
  mv "${pkgdir}/usr/share/man/man1/faad.man" "${pkgdir}/usr/share/man/man1/faad.1"
  install -D -m644 common/mp4ff/mp4ff_int_types.h "${pkgdir}/usr/include/mp4ff_int_types.h"
  install -D -m644 "${srcdir}/LICENSE"  "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
