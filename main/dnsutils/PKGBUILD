# Based upon the PKGBUILD from Arch Linux
pkgname=dnsutils
pkgver=9.9.0
_pkgver=9.9.0
pkgrel=1
pkgdesc='DNS utilities: dig host nslookup'
url='http://www.isc.org/software/bind/'
license=('custom:ISC')
arch=('x86_64')
options=('!makeflags')
source=("http://ftp.isc.org/isc/bind9/${_pkgver}/bind-${_pkgver}.tar.gz"
        'remove-bind.patch'
        'trusted-key.key')
sha1sums=('6be77c75c8649088b0ae7124d819b5f412bb0094'
          'b465ef6160b004838f04de9978fe1be8422af777'
          'ca93da30c89de4365b846325d6ec4674a3e4fc02')
build() {
  cd "${srcdir}/bind-${_pkgver}"

  patch -p1 -i ../remove-bind.patch
  export STD_CDEFINES='-DDIG_SIGCHASE'

  # hack to remove unused bloat from the binaries
  CFLAGS+=' -fdata-sections -ffunction-sections'
  LDFLAGS+=' -Wl,--gc-sections'

  ./configure \
    --prefix=/usr \
    --build=${CBUILD} \
    --host=${CHOST} \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --disable-linux-caps \
    --without-openssl \
    --without-idn \
    --without-libxml2
  make
}

package() {
  cd "${srcdir}/bind-${_pkgver}"
  install -Dm644 ../trusted-key.key "${pkgdir}"/etc/trusted-key.key
  install -Dm644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  cd bin; make DESTDIR="${pkgdir}" install
}
