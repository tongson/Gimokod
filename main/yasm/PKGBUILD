# Based upon the PKGBUILD from Arch Linux
pkgname=yasm
pkgver=1.2.0
pkgrel=2
pkgdesc="A rewrite of NASM to allow for multiple syntax supported (NASM, TASM, GAS, etc.)"
arch=('x86_64')
license=('custom')
url="http://www.tortall.net/projects/yasm/"
source=(http://www.tortall.net/projects/yasm/releases/${pkgname}-${pkgver}.tar.gz)
options=('!libtool')
md5sums=('4cfc0686cf5350dd1305c4d905eb55a6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --disable-python-bindings \
              --disable-python \
              --disable-nls \
              --disable-dependency-tracking

  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
