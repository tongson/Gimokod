# Based upon the PKGBUILD from Arch Linux
pkgname=sudo
_ver=1.8.2
pkgver=${_ver/[a-z]/.${_ver//[0-9.]/}}
pkgrel=1
pkgdesc="Give certain users the ability to run some commands as root"
arch=('i686' 'x86_64')
url="http://www.sudo.ws/sudo/"
license=('custom')
depends=('zlib' 'pam')
backup=('etc/sudoers' 'etc/pam.d/sudo')
options=('!libtool' '!makeflags')
source=(ftp://ftp.sudo.ws/pub/sudo/${pkgname}-$_ver.tar.gz
        sudo.pam)
md5sums=('000f458e7391be9fdf459a9ad6a4912a'
         '4e7ad4ec8f2fe6a40e12bcb2c0b256e3')

build() {
  cd "${srcdir}/${pkgname}-$_ver"

  ./configure --prefix=/usr \
              --build=${CHOST} \
              --host=${CHOST} \
              --enable-zlib=system \
              --with-pam \
              --libexecdir=/usr/lib \
              --with-env-editor \
              --without-linux-audit \
              --with-logfac=auth
  make
}

package() {
  cd "${srcdir}/${pkgname}-$_ver"
  install -dm755 "${pkgdir}/var/lib"

  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/sudo.pam" "${pkgdir}/etc/pam.d/sudo"

  install -Dm644 doc/LICENSE "${pkgdir}/usr/share/licenses/sudo/LICENSE"
}
