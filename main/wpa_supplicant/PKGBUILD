# Based upon the PKGBUILD from Arch Linux
pkgname=wpa_supplicant
pkgver=0.7.3
pkgrel=3
pkgdesc="A utility providing key negotiation for WPA wireless networks"
url="http://hostap.epitest.fi/wpa_supplicant"
arch=('x86_64')
depends=('libnl' 'openssl' 'zlib')
license=('GPL')
source=(http://hostap.epitest.fi/releases/wpa_supplicant-"${pkgver}".tar.gz)
sha256sums=('d0cd50caa85346ccc376dcda5ed3c258eef19a93b3cade39d25760118ad59443')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  Patch hostap_allow-linking-with-libnl-3.2.patch
  cd "${pkgname}"
  cp "${srcdir}/config" ./.config
  sed -i 's@/usr/local/sbin@$(PREFIX)/bin@g' Makefile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/${pkgname}"
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/wpa_supplicant.service" "${pkgdir}/usr/lib/systemd/system/wpa_supplicant.service"
  install -D -m644 "${srcdir}/wpa_supplicant" "${pkgdir}/etc/conf.d/wpa_supplicant"
  install -d -m755 "${pkgdir}/usr/share/man/man"{5,8}
  install -m644 doc/docbook/*.5 "${pkgdir}/usr/share/man/man5/"
  install -m644 doc/docbook/*.8 "${pkgdir}/usr/share/man/man8/"
  rm -f "${pkgdir}/usr/share/man/man8/wpa_"{priv,gui}.8
}

source=(${source[@]-}
        config
        hostap_allow-linking-with-libnl-3.2.patch
        wpa_supplicant
        wpa_supplicant.service)

sha256sums=(${sha256sums[@]-}
            '07b53ed9fff7a3e3a72174235c83d3905f5f40408c28b12bbbad3fec538c39d5'
            'ac805bf6e5aaec733dfc2c333417e519239cd58663a6e1cb34a54fd0f2bcc3c5'
            '0a7ba43bac33f1e6f10d6cdd6fa0397cf909a3eadbcb73a0ff4b74f83359d894'
            '2fd39db93d7bc00849a60a3bfb52b027f2c2013cb1700306d5722655a95d6c3b')
