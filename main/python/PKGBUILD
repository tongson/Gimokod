# Based upon the PKGBUILD from Arch Linux
pkgname=python
pkgver=2.7.2
pkgrel=3
_pybasever=2.7
pkgdesc="A high-level scripting language"
arch=('i686' 'x86_64')
license=('PSF')
url="http://www.python.org/"
depends=('bzip2' 'gdbm' 'openssl' 'zlib' 'expat' 'sqlite' 'libffi')
options=('!makeflags')
source=(http://www.python.org/ftp/python/${pkgver%rc?}/Python-${pkgver}.tar.xz
        13156-revert-tls-changeset-subinterpreter.patch
        linux2.patch
        gdbm-magic-values.patch)
sha1sums=('56700044141402dc35e7a0a24aa7ffda1a8c1a53'
          'd61fa2b8efd1d08a2ee8a9aed434dc94b8297d6b'
          '937c06190899a491d662c7918658313e7640070e'
          'ede2c991b1a91eec5a01cc2d3a11231f17d750ae')

build() {
  cd "${srcdir}/Python-${pkgver}"

  # Temporary workaround for FS#22322
  # See http://bugs.python.org/issue10835 for upstream report
  sed -i "/progname =/s/python/python${_pybasever}/" Python/pythonrun.c

  # Enable built-in SQLite3 module to load extensions (fix FS#22122)
  sed -i "/SQLITE_OMIT_LOAD_EXTENSION/d" setup.py

  # FS#23997
  sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python|" Lib/cgi.py

  # Make sure sys.platform is set to linux2, even on kernel 3.X
  # cf http://bugs.python.org/issue12325
  patch -Np1 -i ../linux2.patch

  # http://bugs.python.org/issue13156
  Patch 13156-revert-tls-changeset-subinterpreter.patch

  # gdbm has new magic that whichdb does not recognize
  # http://bugs.python.org/issue13007
  patch -Np1 -i ../gdbm-magic-values.patch

  # Ensure that we are using the system copy of various libraries (expat, zlib and libffi),
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*

  export OPT="${CFLAGS}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --with-dbmliborder=gdbm:ndbm \
              --enable-shared \
              --with-threads \
              --enable-ipv6 \
              --enable-unicode=ucs4 \
              --with-system-expat \
              --with-system-ffi \
              --with-fpectl
  make MACHDEP=linux2
}

package() {
  cd "${srcdir}/Python-${pkgver}"
  make DESTDIR="${pkgdir}" altinstall maninstall

  ln -sf python${_pybasever}        "${pkgdir}/usr/bin/python"
  ln -sf python${_pybasever}-config "${pkgdir}/usr/bin/python-config"
  ln -sf python${_pybasever}.1      "${pkgdir}/usr/share/man/man1/python.1"

  ln -sf ../../libpython${_pybasever}.so \
    "${pkgdir}/usr/lib/python${_pybasever}/config/libpython${_pybasever}.so"

  mv "${pkgdir}/usr/bin/smtpd.py" "${pkgdir}/usr/lib/python${_pybasever}/"

  # clean-up reference to build directory
  sed -i "s#${srcdir}/Python-${pkgver}:##" \
    "${pkgdir}/usr/lib/python${_pybasever}/config/Makefile"

  # license
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
