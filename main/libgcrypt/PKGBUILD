# Based upon the PKGBUILD from Arch Linux
pkgname=libgcrypt
pkgver=1.5.0
pkgrel=1
pkgdesc="a general purpose crypto library based on the code used"
arch=(i686 x86_64)
url="http://www.gnupg.org"
license=('LGPL')
depends=('libgpg-error')
options=('!libtool' '!emptydirs')
source=(ftp://ftp.gnupg.org/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('3e776d44375dc1a710560b98ae8437d5da6e32cf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  [ "$CARCH" = "i686" ] && EXTRAFLAGS="--disable-aesni-support"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST}  \
              --disable-static \
              --disable-dependency-tracking \
              --enable-noexecstack \
              --disable-O-flag-munging \
              --disable-padlock-support $EXTRAFLAGS
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -k check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Move dynamic libraries to /lib
  install -d -m755 "${pkgdir}"/lib/
  mv "${pkgdir}"/usr/lib/libgcrypt.so* "${pkgdir}"/lib/
  ln -sf /lib/libgcrypt.so "${pkgdir}"/usr/lib/libgcrypt.so
}
