# Based upon the PKGBUILD from Arch Linux
pkgname=man-pages
pkgver=3.39
_posixver=2003-a
pkgrel=1
pkgdesc="Linux man pages"
arch=('any')
license=('GPL')
url="http://man7.org/linux/man-pages/index.html"
source=(http://www.kernel.org/pub/linux/docs/man-pages/$pkgname-$pkgver.tar.xz
        http://www.kernel.org/pub/linux/docs/man-pages/man-pages-posix/"${pkgname}"-posix-${_posixver}.tar.bz2)
md5sums=('7d660682e2cf56c9d2195529eb2418d3'
         '7c78aff03c0a6767ba483d34f19e4b09')

build() {
  cd "${srcdir}"/"${pkgname}"-"${pkgver}"

  # move the posix pages
  mkdir -p "${srcdir}"/"${pkgname}"-"${pkgver}"/man0
  for sect in 0 1 3; do
    sed -i "/^\.so /s/man${sect}p/man$sect/" "${srcdir}/"${pkgname}"-posix-${_posixver}/man${sect}p"/*
    mv -iv "${srcdir}/"${pkgname}"-posix-${_posixver}/man${sect}p"/* "${srcdir}/"${pkgname}"-"${pkgver}"/man$sect/"
  done
}

package() {
  cd "${srcdir}"/"${pkgname}"-"${pkgver}"

  make prefix="${pkgdir}"/usr install

  cd "${pkgdir}"/usr/share/man
  # these are included in coreutils || return 1
  rm -f man1/{chgrp,chmod,chown,cp,dir,dd}.1
  rm -f man1/{df,dircolors,du,install,ln,ls}.1
  rm -f man1/{mkdir,mkfifo,mknod,mv,rm,rmdir}.1
  rm -f man1/{touch,vdir}.1
  # this is included in shadow
  rm -f man5/passwd.5
  rm -f man3/getspnam.3
  # this is included in xf86-input-mouse
  rm -f man4/mouse.4

  rm -f man7/man.7
  rm -f man7/mdoc.7
}
