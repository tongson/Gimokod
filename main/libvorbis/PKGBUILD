# Based upon the PKGBUILD from Arch Linux
pkgname=libvorbis
pkgver=1.3.2
pkgrel=1
pkgdesc="Vorbis codec library"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.xiph.org/ogg/vorbis/"
depends=('libogg')
options=('!libtool')
source=(http://downloads.xiph.org/releases/vorbis/${pkgname}-${pkgver}.tar.gz)
md5sums=('c870b9bd5858a0ecb5275c14486d9554')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
