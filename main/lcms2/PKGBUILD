# Based upon the PKGBUILD from Arch Linux
pkgname=lcms2
pkgver=2.2
pkgrel=1
pkgdesc="Small-footprint color management engine, version 2"
arch=('i686' 'x86_64')
license=('MIT')
depends=('libtiff' 'libjpeg' 'zlib')
url="http://www.littlecms.com"
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/lcms/${pkgname}-${pkgver}.tar.gz)
md5sums=('aaf33c7c25675e6163189ba488ae20f5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --with-jpg \
              --with-zlib \
              --with-tiff
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING \
    "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
