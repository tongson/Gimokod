# Based upon the PKGBUILD from Arch Linux
pkgname=wxgtk
pkgver=2.8.12.1
pkgrel=1
pkgdesc="GTK+ implementation of wxWidgets API for GUI"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'libgl' 'libxxf86vm' 'libsm')
makedepends=('mesa')
source=(http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2)
md5sums=('8c06c5941477beee213b4f2fa78be620')
sha1sums=('05688dc03d61631750f5904273122bb40a2115f5')

build() {
  cd "${srcdir}/wxPython-src-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx --without-gnomeprint --disable-optimize --disable-mediactrl \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys --without-gnomevfs
  make
  make -C locale allmo
  cd contrib/src
  make
}

package() {
  cd "${srcdir}/wxPython-src-${pkgver}"
  make DESTDIR="${pkgdir}" install
  cd contrib/src
  make DESTDIR="${pkgdir}" install
  install -D -m644 ../../docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
