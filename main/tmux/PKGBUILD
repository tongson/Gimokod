# Based upon the PKGBUILD from Arch Linux
pkgname=tmux
pkgver=1.6
pkgrel=1
pkgdesc="A terminal multiplexer"
url="http://tmux.sourceforge.net/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('ncurses' 'libevent')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz
        LICENSE)
md5sums=('3e37db24aa596bf108a0442a81c845b3'
         '71601bc37fa44e4395580b321963018e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e 's:-I/usr/local/include::' Makefile.in
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST}
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
  install -Dm644 examples/tmux.vim "${pkgdir}/usr/share/vim/vimfiles/syntax/tmux.vim"
  install -Dm644 ../LICENSE "${pkgdir}/usr/share/licenses/tmux/LICENSE"
  install -dm755 "${pkgdir}/usr/share/tmux/"
  install -m644 examples/* "${pkgdir}/usr/share/tmux/"
  mkdir -p "${pkgdir}/etc/bash_completion.d/"
  mv "${pkgdir}/usr/share/tmux/bash_completion_tmux.sh" "${pkgdir}/etc/bash_completion.d/tmux"
}
