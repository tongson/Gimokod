# Based upon the PKGBUILD from Arch Linux
pkgname=openjpeg
pkgver=1.5.0
pkgrel=1
pkgdesc="An open source JPEG 2000 codec"
arch=(i686 x86_64)
license=('BSD')
url="http://www.openjpeg.org"
depends=('zlib' 'libpng' 'libtiff' 'lcms2')
source=(http://openjpeg.googlecode.com/files/openjpeg-${pkgver}.tar.gz)
sha256sums=('c9908fab44432a2ed2550c457cb7e736da6d7c326f1203d23d1082d992364ace')
options=('!makeflags' '!libtool')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # make sure we use system libs
  rm -rf libs
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/openjpeg"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/openjpeg/LICENSE"
}
