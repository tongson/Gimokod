# Based upon the PKGBUILD from Arch Linux
pkgname=openjpeg
pkgver=1.4
_pkgver=v1_4
_revision=r697
pkgrel=1
pkgdesc="An open source JPEG 2000 codec"
arch=(i686 x86_64)
license=('BSD')
url="http://www.openjpeg.org"
depends=('zlib' 'libpng' 'libtiff' 'lcms2')
source=(http://openjpeg.googlecode.com/files/openjpeg_${_pkgver}_sources_${_revision}.tgz
  openjpeg-1.4-autoconf.patch)
md5sums=('7870bb84e810dec63fcf3b712ebb93db'
         '46d1d26b59ce546444f3fe8d7c685775')

options=('!makeflags' '!libtool')

build() {
  cd "${srcdir}/${pkgname}_${_pkgver}_sources_${_revision}"
  patch -Np1 -i "${srcdir}"/openjpeg-1.4-autoconf.patch
  # make sure we use system libs
  rm -rf libs
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}_${_pkgver}_sources_${_revision}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/openjpeg"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/openjpeg/LICENSE"
}
