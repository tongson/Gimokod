# Based upon the PKGBUILD from Arch Linux
pkgname=lame
pkgver=3.99.1
pkgrel=1
pkgdesc="A high-quality MP3 encoder"
arch=('i686' 'x86_64')
url="http://lame.sourceforge.net/"
depends=('ncurses')
makedepends=('nasm')
license=('LGPL')
options=('!libtool')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('2576a7368b5c90a87adc9de6b9f0628c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --enable-nasm \
              --enable-shared \
              --disable-mp3x
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
