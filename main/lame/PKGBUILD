# Based upon the PKGBUILD from Arch Linux
pkgname=lame
pkgver=3.98.4
pkgrel=1
pkgdesc="An MP3 encoder and graphical frame analyzer"
arch=('i686' 'x86_64')
url="http://lame.sourceforge.net/"
depends=('ncurses')
makedepends=('nasm')
license=('LGPL')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('8e9866ad6b570c6c95c8cba48060473f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --enable-nasm \
              --enable-shared \
              --disable-mp3x
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
