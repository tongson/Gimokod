# Based upon the PKGBUILD from Arch Linux
pkgname=nasm
pkgver=2.10
pkgrel=1
pkgdesc="An 80x86 assembler designed for portability and modularity"
arch=('x86_64')
url="http://www.nasm.us"
license=('BSD')
source=(http://www.nasm.us/pub/nasm/releasebuilds/${pkgver}/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('acdcae5366230e2e19b2ad36f952df0232c80950')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export CFLAGS=${CFLAGS/-Os/-O2}
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST}
  make all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make INSTALLROOT="${pkgdir}" install install_rdf
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/nasm/LICENSE"
}

