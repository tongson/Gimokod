# Based upon the PKGBUILD from Arch Linux
pkgbase=boost
pkgname=('boost' 'boost-libs')
pkgver=1.47.0
_boostver=${pkgver//./_}
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.boost.org/"
source=(http://downloads.sourceforge.net/${pkgbase}/${pkgbase}_${_boostver}.tar.gz
        random-Jamfile-1.47.0
        exceptions.patch)
license=('custom')
md5sums=('ff180a5276bec773a7625cac7e2288e8'
         '6db01e5cde8739ce35e7f0a2abb0de14'
         '623b2d3b64fbb03b0d23ae9d9830a966')

_stagedir="${srcdir}/stagedir"

build() {
  cd "${srcdir}/${pkgbase}_${_boostver}/tools"

  # http://web.archiveorange.com/archive/v/Q0J4VE5bwsy3zxRXqUgd
  cd "${srcdir}/${pkgbase}_${_boostver}"
  Patch exceptions.patch

  # build bjam
  cd "${srcdir}/${pkgbase}_${_boostver}/tools/build/v2/engine"
  ./build.sh cc

  _bindir="bin.linuxx86"
  [[ "${CARCH}" = "x86_64" ]] && _bindir="bin.linuxx86_64"

  install -d "${_stagedir}/usr/bin"
  install "${_bindir}/bjam" "${_stagedir}/usr/bin/bjam"

  # build tools
  cd "${srcdir}/${pkgbase}_${_boostver}/tools/"
  "${_stagedir}/usr/bin/bjam" --toolset=gcc

  # copy the tools
  cd "${srcdir}/${pkgbase}_${_boostver}/dist/bin"
  for i in *;do
    install -m755 "${i}" "${_stagedir}/usr/bin/${i}"
  done

  #boostbook needed by quickbook
  cd "${srcdir}/${pkgbase}_${_boostver}/dist/"
  cp -r share "${_stagedir}"

  # build libs
  cd "${srcdir}/${pkgbase}_${_boostver}"

  # /dev/urandom support
  mkdir -p libs/random/build
  cp "${srcdir}/random-Jamfile-${pkgver}" libs/random/build/Jamfile.v2

  # default "minimal" install: "release link=shared,static
  # runtime-link=shared threading=single,multi"
  # --layout=tagged will add the "-mt" suffix for multithreaded libraries
  # and installs includes in /usr/include/boost.
  # --layout=system no longer adds the -mt suffix for multi-threaded libs.
  # install to ${_stagedir} in preparation for split packaging

  "${_stagedir}"/usr/bin/bjam \
    release debug-symbols=off threading=multi \
    runtime-link=shared link=shared,static \
    cflags=-fno-strict-aliasing \
    pch=off \
    toolset=gcc \
    --prefix="${_stagedir}" \
     -sTOOLS=gcc \
    --layout=system \
     -sICU_PATH=/usr \
    --disable-long-double \
    ${MAKEFLAGS} \
    install
}

package_boost() {
  pkgdesc="Free peer-reviewed portable C++ source libraries - Development"
  depends=("boost-libs=${pkgver}")

  install -d "${pkgdir}"/usr/{include,lib,share}
  # headers/source files
  cp -r "${_stagedir}"/include/ "${pkgdir}"/usr/

  # static libs
  cp -r "${_stagedir}"/lib/*.a "${pkgdir}"/usr/lib/

  # utilities (bjam, bcp, pyste)
  cp -r "${_stagedir}"/usr/* "${pkgdir}"/usr/

  #boostbook
  cp -r "${_stagedir}"/share/* "${pkgdir}"/usr/share

  # license
  install -D -m644 "${srcdir}/${pkgbase}_${_boostver}/LICENSE_1_0.txt" \
    "${pkgdir}"/usr/share/licenses/boost/LICENSE_1_0.txt
}

package_boost-libs() {
  pkgdesc="Free peer-reviewed portable C++ source libraries - Runtime"
  depends=('gcc-libs' 'zlib' 'icu')

  install -d "${pkgdir}/usr/lib"
  #shared libs
  cp -r "${_stagedir}"/lib/*.so{,.*} "${pkgdir}/usr/lib/"

  # license
  install -D -m644 "${srcdir}/${pkgbase}_${_boostver}/LICENSE_1_0.txt" \
    "${pkgdir}"/usr/share/licenses/boost-libs/LICENSE_1_0.txt
}
