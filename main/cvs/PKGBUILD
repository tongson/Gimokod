# Based upon the PKGBUILD from Arch Linux
pkgname=cvs
pkgver=1.11.23
pkgrel=1
pkgdesc="Concurrent Versions System - a source control system"
arch=('i686' 'x86_64')
url="http://cvs.nongnu.org/"
license=('GPL')
depends=('zlib')
source=(ftp://ftp.gnu.org/non-gnu/cvs/source/stable/${pkgver}/${pkgname}-${pkgver}.tar.bz2
        cvs-1.11.19-extzlib.patch
        cvs-1.11.23-getline64.patch
        cvs-1.11.23-cve-2010-3846.patch)
sha256sums=('400f51b59d85116e79b844f2d5dbbad4759442a789b401a94aa5052c3d7a4aa9'
            '8dbee65419189de26fb7901a6409932100d0000c581624ba41c4a717edef7f16'
            '9126d7992ace943980ad8a10d5a09aeb6f1eeeb9b921fc796fe31de7b1c220cf'
            'c6506d0a5efc7b0cab6415f26e070ec214fb9781fac8d295506f4d0825431a8f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  unset EDITOR VISUAL

  find "${srcdir}" -type f -name getdate -exec rm \{\} \;

  patch -Np1 < ../cvs-1.11.19-extzlib.patch
  patch -Np1 < ../cvs-1.11.23-getline64.patch
  # CVE-2010-3864, https://www.redhat.com/security/data/cve/CVE-2010-3846.html
  patch -Np1 < ../cvs-1.11.23-cve-2010-3846.patch

  autoreconf
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --without-gssapi
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
