# Based upon the PKGBUILD from Arch Linux
pkgname=libogg
pkgver=1.3.0
pkgrel=1
pkgdesc="Ogg bitstream and framing library"
arch=('i686' 'x86_64')
url="http://www.xiph.org/ogg/"
license=('BSD')
options=('!libtool')
source=(http://downloads.xiph.org/releases/ogg/${pkgname}-${pkgver}.tar.xz
        config_types.h)
md5sums=('84a35715170f2cd4c77a4448772b95d5'
         'a4d786b1b1955cb85b64fd16cbbee3f9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST}
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  if [[ $CARCH == "x86_64" ]]; then
    mv "${pkgdir}"/usr/include/ogg/config_types{,-64}.h
    install -m 644 "${srcdir}/config_types.h" \
      "${pkgdir}/usr/include/ogg/config_types.h"
  fi
}
