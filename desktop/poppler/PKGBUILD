# Based upon the PKGBUILD from Arch Linux
pkgname=('poppler' 'poppler-glib')
pkgbase='poppler'
pkgver=0.18.0
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
makedepends=('glib' 'cairo' 'libjpeg' 'libpng' 'gcc-libs' 'fontconfig' 'openjpeg' 'pkg-config' 'lcms')
options=('!libtool')
url="http://poppler.freedesktop.org/"
source=(http://poppler.freedesktop.org/${pkgbase}-${pkgver}.tar.gz
        poppler-0.18.0-poppler_page_get_image-crash.patch)
md5sums=('4cd3bf2a0a13fa8eaf00d31368915f77'
         '969ace9d6e7378c3de8366e61dccecaf')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
#  sed -i -e '/AC_PATH_XTRA/d' configure.ac
#  autoreconf
  patch -Np1 -i ../poppler-0.18.0-poppler_page_get_image-crash.patch
  ./configure \
              --enable-libpng \
              --enable-introspection=no \
              --disable-gtk-test \
              --disable-gtk-doc-html \
              --disable-libcurl \
              --prefix=/usr \
              --host=${CHOST} \
              --build=${CBUILD} \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --disable-static \
              --enable-cairo-output \
              --enable-xpdf-headers \
              --enable-libjpeg \
              --enable-zlib \
              --disable-poppler-qt4 \
              --enable-poppler-glib
  make
}

package_poppler() {
  pkgdesc="PDF rendering library based on xpdf 3.0"
  depends=('libjpeg' 'libpng' 'gcc-libs' 'fontconfig' 'openjpeg' 'lcms' 'poppler-data' 'zlib')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -e 's/^glib_subdir =.*/glib_subdir =/' \
      -e 's/^qt4_subdir =.*/qt4_subdir =/' -i Makefile
  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}"/usr/lib/pkgconfig/poppler-{glib,qt4}.pc
}

package_poppler-glib() {
  pkgdesc="Poppler glib bindings"
  depends=("poppler=${pkgver}" 'glib' 'cairo')

  cd "${srcdir}/${pkgbase}-${pkgver}/poppler"
  make DESTDIR="${pkgdir}" install-libLTLIBRARIES
  cd "${srcdir}/${pkgbase}-${pkgver}/glib"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/lib/pkgconfig"
  install -m644 ../poppler-glib.pc "${pkgdir}/usr/lib/pkgconfig/"
  rm -f "${pkgdir}"/usr/lib/libpoppler.*
  rm -f "${pkgdir}/usr/bin/poppler-glib-demo"
}

