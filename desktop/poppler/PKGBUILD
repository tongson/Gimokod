# Based upon the PKGBUILD from Arch Linux
pkgbase=poppler
pkgname=('poppler' 'poppler-glib')
pkgver=0.18.4
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
makedepends=('glib' 'cairo' 'libjpeg' 'libpng' 'gcc-libs' 'fontconfig' 'openjpeg' 'pkg-config' 'lcms')
options=('!libtool')
url="http://poppler.freedesktop.org/"
_testtag=0d2bfd4af4c76a3bac27ccaff793d9129df7b57a
source=(http://poppler.freedesktop.org/${pkgbase}-${pkgver}.tar.gz
        http://cgit.freedesktop.org/poppler/test/snapshot/test-${_testtag}.tar.bz2)
md5sums=('12658f3308597e57f3faff538cc73baf'
         '9dc64c254a31e570507bdd4ad4ba629a')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -i -e '/AC_PATH_XTRA/d' configure.ac
  #patch -p1 -i "${srcdir}/git-fixes.patch"
  autoreconf -fi
  ./configure \
              --enable-libpng \
              --enable-introspection=no \
              --disable-gtk-test \
              --disable-gtk-doc-html \
              --disable-libcurl \
              --prefix=/usr \
              --host=${CHOST} \
              --build=${CBUILD} \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --disable-static \
              --enable-cairo-output \
              --enable-xpdf-headers \
              --enable-libjpeg \
              --enable-zlib \
              --disable-poppler-qt4 \
              --enable-poppler-glib
  make
}

package_poppler() {
  pkgdesc="PDF rendering library based on xpdf 3.0"
  depends=('libjpeg' 'libpng' 'gcc-libs' 'fontconfig' 'openjpeg' 'lcms' 'poppler-data' 'zlib')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -e 's/^glib_subdir =.*/glib_subdir =/' \
      -e 's/^qt4_subdir =.*/qt4_subdir =/' -i Makefile
  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}"/usr/lib/pkgconfig/poppler-{glib,qt4}.pc
}

package_poppler-glib() {
  pkgdesc="Poppler glib bindings"
  depends=("poppler=${pkgver}" 'glib' 'cairo')

  cd "${srcdir}/${pkgbase}-${pkgver}/poppler"
  make DESTDIR="${pkgdir}" install-libLTLIBRARIES
  cd "${srcdir}/${pkgbase}-${pkgver}/glib"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/lib/pkgconfig"
  install -m644 ../poppler-glib.pc "${pkgdir}/usr/lib/pkgconfig/"
  rm -f "${pkgdir}"/usr/lib/libpoppler.*
  rm -f "${pkgdir}/usr/bin/poppler-glib-demo"
}

