# Based upon the PKGBUILD from Arch Linux
pkgname=xorg-fonts-encodings
pkgver=1.0.4
pkgrel=2
pkgdesc="X.org font encoding files"
arch=(any)
url="http://xorg.freedesktop.org/"
license=('custom')
makedepends=('xorg-mkfontscale' 'xorg-util-macros' 'xorg-font-util')
source=(${url}/releases/individual/font/encodings-${pkgver}.tar.bz2)
options=(!makeflags)
sha1sums=('24ace2470403f85a0d393769204029bd8247992a')

build() {
  cd "${srcdir}/encodings-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST}
  make
}
package() {
  cd "${srcdir}/encodings-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
  # regenerate encodings file not to include non-compressed versions
  cd $pkgdir/usr/share/fonts/encodings/large
  mkfontscale -b -s -l -n -r -p /usr/share/fonts/encodings/large -e . .
  cd $pkgdir/usr/share/fonts/encodings/
  mkfontscale -b -s -l -n -r -p /usr/share/fonts/encodings -e . -e large .
}
