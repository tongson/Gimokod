# Based upon the PKGBUILD from Arch Linux
pkgname=libxext
pkgver=1.3.1
pkgrel=1
pkgdesc="X11 miscellaneous extensions library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'xextproto')
makedepends=('xorg-util-macros')
options=('!libtool')
source=(${url}/releases/individual/lib/libXext-${pkgver}.tar.bz2)
sha1sums=('764ac472ae19a0faade193717a9e0938d3430aaa')

build() {
  cd "${srcdir}/libXext-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --disable-static \
              --without-fop \
              --without-xmlto \
              --disable-specs
  make
}

package() {
  cd "${srcdir}/libXext-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
