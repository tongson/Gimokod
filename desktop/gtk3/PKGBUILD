# Based upon the PKGBUILD from Arch Linux
pkgname=gtk3
pkgver=3.2.2
pkgrel=2
pkgdesc="The GTK+ Toolkit (v3)"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
install=gtk3.install
depends=('glib' 'atk' 'cairo' 'gtk-update-icon-cache' 'libxfixes' 'libxcursor' 'libxinerama' 'libxrandr'
         'fontconfig' 'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info')
options=('!libtool' '!docs')
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/3.2/gtk+-${pkgver}.tar.xz
        fix_crash_treeview.patch
        settings.ini)
sha256sums=('f7ec82de393cd7ae2aa45022576400941704709d1f0f35fb0b17f3be1f2e7d84'
            'eb187ef05f3735b24b58cd4c533dc3d4f616103cafba399c009ea3eb519be0b9'
            'c214d3dcdcadda3d642112287524ab3e526ad592b70895c9f3e3733c23701621')

_strip_builddir() {
  local rule=$1
  shift
  local directory=$1
  shift
  sed -e "s/^\(${rule} =.*\)${directory}\(.*\)$/\1\2/" -i $@
}

build() {
  cd "${srcdir}/gtk+-${pkgver}"
  patch -Np1 -R -i "$srcdir/fix_crash_treeview.patch"
  _strip_builddir SRC_SUBDIRS tests Makefile.am
  _strip_builddir SRC_SUBDIRS tests Makefile.in
  _strip_builddir SRC_SUBDIRS demos Makefile.am
  _strip_builddir SRC_SUBDIRS demos Makefile.in

  CXX=/bin/false ./configure --prefix=/usr \
    --build=${CBUILD} \
    --host=${CHOST} \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-gtk2-dependency \
    --disable-schemas-compile \
    --enable-xinput \
    --enable-xrandr \
    --enable-xfixes \
    --enable-xcomposite \
    --enable-xdamage \
    --enable-xinerama \
    --disable-packagekit \
    --disable-papi \
    --disable-cups \
    --enable-gtk-doc-html=no \
    --enable-introspection=no
    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package() {
  cd "${srcdir}/gtk+-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/settings.ini" "${pkgdir}/etc/gtk-3.0/settings.ini"
}
