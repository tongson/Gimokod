# Based upon the PKGBUILD from Arch Linux
pkgname=gtk3
pkgver=3.4.2
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit (v3)"
arch=('x86_64')
url="http://www.gtk.org/"
depends=('glib' 'atk' 'cairo' 'gtk-update-icon-cache' 'libxfixes' 'libxcursor' 
         'libxinerama' 'libxrandr' 'fontconfig' 'libxi' 'libxcomposite' 'libxdamage' 
         'pango' 'shared-mime-info')
options=('!libtool' '!docs')
license=('LGPL')
install=gtk3.install
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz
        settings.ini)
sha256sums=('b1ab72bc61434418160ff0c7edfd93c74758ed8a35e2ed3d07e3e218c2705480'
            'c214d3dcdcadda3d642112287524ab3e526ad592b70895c9f3e3733c23701621')

_strip_builddir() {
  local rule=$1
  shift
  local directory=$1
  shift
  sed -e "s/^\(${rule} =.*\)${directory}\(.*\)$/\1\2/" -i $@
}

build() {
  cd "gtk+-${pkgver}"
  _strip_builddir SRC_SUBDIRS tests Makefile.am
  _strip_builddir SRC_SUBDIRS tests Makefile.in
  _strip_builddir SRC_SUBDIRS demos Makefile.am
  _strip_builddir SRC_SUBDIRS demos Makefile.in

  CXX=/bin/false ./configure --prefix=/usr \
    --build=${CBUILD} \
    --host=${CHOST} \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-gtk2-dependency \
    --disable-schemas-compile \
    --enable-xinput \
    --enable-xrandr \
    --enable-xfixes \
    --enable-xcomposite \
    --enable-xdamage \
    --enable-xinerama \
    --disable-packagekit \
    --disable-papi \
    --disable-cups \
    --enable-gtk-doc-html=no \
    --enable-introspection=no
    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package() {
  cd "gtk+-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/settings.ini" "${pkgdir}/etc/gtk-3.0/settings.ini"
}
