# Based upon the PKGBUILD from Arch Linux
pkgname=xorg-xbacklight
pkgver=1.1.2
pkgrel=1
pkgdesc="RandR-based backlight control application"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxrandr' 'libx11')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(http://xorg.freedesktop.org/archive/individual/app/xbacklight-${pkgver}.tar.bz2)
sha1sums=('e297b359ab83f3844ca5ea1c23280b2f8bb71cfd')

build() {
  cd "${srcdir}/xbacklight-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST}
  make
}

package() {
  cd "${srcdir}/xbacklight-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
