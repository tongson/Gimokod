# Based upon the PKGBUILD from Arch Linux
pkgname=xchm
pkgver=1.20
pkgrel=1
pkgdesc="A cross-platform GUI front-end to chmlib"
arch=('i686' 'x86_64')
url="http://xchm.sourceforge.net"
license=('GPL')
depends=('chmlib' 'wxgtk' 'gcc-libs')
install=xchm.install
source=(http://downloads.sourceforge.net/sourceforge/xchm/${pkgname}-${pkgver}.tar.gz
        xchm.xml)
md5sums=('85943645d20af536704a20b58ebd5849'
         '4102b0a393f5c60236143cc034664dd0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing"
  sed -i '86s:wxLocale _loc;::' src/chmapp.h
  sed -i '160s:_loc.AddCatalog(wxT("xchm"));::' src/chmapp.cpp
  sed -i '159s:_loc.Init();::' src/chmapp.cpp
  ./configure --prefix=/usr \
              --host=${CHOST} \
              --build=${CBUILD} \
              --disable-nls
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}"/xchm.xml "${pkgdir}/usr/share/mime/packages/xchm.xml"
}
