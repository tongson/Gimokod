# Based upon the PKGBUILD from Arch Linux
pkgbase=ntfs-3g_ntfsprogs
pkgname=('ntfs-3g' 'ntfsprogs')
pkgver=2011.4.12
pkgrel=1
license=('GPL2')
depends=('fuse')
arch=('i686' 'x86_64')
url="http://www.tuxera.com/"
makedepends=('pkg-config')
options=('!libtool')
source=(http://tuxera.com/opensource/${pkgbase}-${pkgver}.tgz)
sha1sums=('a4340ae611a3f26089f7d5601a86a5847517bff7')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ac_cv_path_LDCONFIG=/usr/bin/true ./configure \
              --prefix=/usr \
              --sbindir=/usr/bin \
              --bindir=/usr/bin \
              --libdir=/usr/lib \
              --build=${CBUILD} \
              --host=${CHOST} \
              --with-fuse=external \
              --disable-static \
              --disable-debug \
              --enable-ntfsprogs \
              --disable-crypto \
              --disable-posix-acls \
              --disable-xattr-mappings \
              --disable-gnome-vfs \
              --enable-ldscript \
              --disable-ldconfig \
              --mandir=/usr/share/man
  make
}

package_ntfs-3g() {
  pkgdesc="Stable read and write NTFS driver"

  for DIR in src libntfs-3g include; do
    cd "${srcdir}/${pkgbase}-${pkgver}/${DIR}"
    make DESTDIR="${pkgdir}" install
  done
  mv "${pkgdir}"/sbin/* "${pkgdir}"/usr/bin
  mv "${pkgdir}"/bin/* "${pkgdir}"/usr/bin
  rmdir "${pkgdir}/sbin"
  rmdir "${pkgdir}/bin"
  rmdir "${pkgdir}/lib"

}

package_ntfsprogs() {
  pkgdesc="NTFS filesystem utilities"
  depends=('util-linux' 'ntfs-3g')

  cd "${srcdir}/${pkgbase}-${pkgver}/ntfsprogs"
  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/sbin/* "${pkgdir}"/usr/bin
  rmdir "${pkgdir}/sbin"
 
}
