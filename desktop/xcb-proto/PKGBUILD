# Based upon the PKGBUILD from Arch Linux
pkgname=xcb-proto
pkgver=1.7.1
pkgrel=1
pkgdesc="XML-XCB protocol descriptions"
arch=('any')
url="http://xcb.freedesktop.org/"
license=('custom')
makedepends=('python' 'libxml2')
source=($url/dist/$pkgname-$pkgver.tar.bz2)
sha1sums=('82a568559235fc6e26d0a38911c5ea18f8e8455c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST}
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
