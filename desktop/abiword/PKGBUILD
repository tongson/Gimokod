# Based upon the PKGBUILD from Arch Linux
pkgname=abiword
pkgver=2.8.6
pkgrel=5
arch=('x86_64')
license=('GPL')
url="http://www.abisource.com"
depends=('fribidi' 'wv' 'libxslt' 'glib' 'freetype' 'zlib'
         'libpng' 'libjpeg' 'cairo' 'gtk2' 'libxml2' 'enchant')
makedepends=('boost' 'pkg-config')
options=('!makeflags' '!libtool')
source=("http://www.abisource.com/downloads/${pkgname}/${pkgver}/source/${pkgname}-${pkgver}.tar.gz")
sha256sums=('d99089a63a6cfc1a6a4a026be9278028d47d224088d24b1853acb67e95683a15')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # fix a gcc 4.6.x build issue, patch from Fedora
  Patch abiword-2.8.6-no-undefined.patch
  Patch abiword-2.8.6-libpng15.patch
  Patch abiword-2.8.6-glib-2.31.patch
  libtoolize --force
  autoreconf

  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --disable-clipart \
              --disable-templates \
              --enable-statusbar \
              --enable-shave \
              --disable-maintainer-mode \
              --disable-default-plugins \
              --disable-builtin-plugins \
              --disable-collab-backend-telepathy \
              --disable-collab-backend-sugar \
              --enable-plugins="openxml latex" \
              --enable-spell \
              --without-gnomevfs \
              --with-gio \
              --without-goffice \
              --disable-static \
              --with-psiconv-config=/nothere
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i plugins/Makefile \
      -e 's/ collab / /' \
      -e 's/ wpg / /' \
      -e 's/ wmf / /' \
      -e 's/ grammar / /' \
      -e 's/ mathview / /' \
      -e 's/ aiksaurus / /' \
      -e 's/ ots / /' \
      -e 's/ wordperfect / /'
  make DESTDIR="${pkgdir}" install
}


source=(${source[@]-}
        abiword-2.8.6-glib-2.31.patch
        abiword-2.8.6-libpng15.patch
        abiword-2.8.6-no-undefined.patch)

sha256sums=(${sha256sums[@]-}
            'e16a496a552f43f21041e0be6077f179a66270b7534dfbd9fbb8f4e0554f5d35'
            'a5ea99ba7aa00ebc3a669d6cbd019b172e576df6b2eb8349505ed18576e10e90'
            '78e3b3be4340022f57246e88948d947bfd031819664edce965a40d7a518862a3')
