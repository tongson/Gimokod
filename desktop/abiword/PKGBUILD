# Based upon the PKGBUILD from Arch Linux
pkgname=abiword
pkgver=2.8.6
pkgrel=2
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.abisource.com"
makedepends=('pkg-config')
depends=('boost' 'fribidi' 'wv' 'libxslt' 'glib' 'freetype' 'zlib'
         'libpng' 'libjpeg' 'cairo' 'gtk2' 'libxml2' 'gcc-libs')
options=('!makeflags' '!libtool')
source=("http://www.abisource.com/downloads/${pkgname}/${pkgver}/source/${pkgname}-${pkgver}.tar.gz"
        'abiword-2.8.6-no-undefined.patch'
        'abiword-2.8.6-libpng15.patch')
md5sums=('f883b0a7f26229a9c66fd6a1a94381aa'
         '42e2614b6e0aad457d91f6ce20b6f225'
         '40e370e7e88c5437140ef18037abfbd3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # fix a gcc 4.6.x build issue, patch from Fedora
  Patch abiword-2.8.6-no-undefined.patch
  Patch abiword-2.8.6-libpng15.patch
  libtoolize --force
  autoreconf

  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --enable-clipart \
              --enable-templates \
              --enable-statusbar \
              --enable-shave \
              --disable-maintainer-mode \
              --disable-default-plugins \
              --disable-builtin-plugins \
              --disable-collab-backend-telepathy \
              --disable-collab-backend-sugar \
              --enable-plugins="openxml latex" \
              --disable-spell \
              --without-gnomevfs \
              --with-gio \
              --without-goffice \
              --disable-static \
              --with-psiconv-config=/nothere
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i plugins/Makefile \
      -e 's/ collab / /' \
      -e 's/ wpg / /' \
      -e 's/ wmf / /' \
      -e 's/ grammar / /' \
      -e 's/ mathview / /' \
      -e 's/ aiksaurus / /' \
      -e 's/ ots / /' \
      -e 's/ wordperfect / /'
  make DESTDIR="${pkgdir}" install
}

