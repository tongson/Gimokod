# Based upon the PKGBUILD from Arch Linux
pkgname=gdk-pixbuf
pkgver=2.24.1
pkgrel=1
pkgdesc="An image loading library"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('glib' 'libpng' 'libtiff' 'libjpeg' 'libx11')
options=('!libtool')
install=gdk-pixbuf2.install
source=(http://download.gnome.org/sources/gdk-pixbuf/2.24/gdk-pixbuf-${pkgver}.tar.xz)
sha256sums=('da7a3f00db360913716368e19e336402755cafa93769f3cfa28a969303e4bee1')

build() {
  cd "${srcdir}/gdk-pixbuf-${pkgver}"
  sed -i 's:po docs tests::' Makefile.{in,am}
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --with-x11 \
              --without-libjasper \
              --with-included-loaders=png \
              --disable-gtk-doc \
              --disable-introspection
  make
}

package() {
  cd "${srcdir}/gdk-pixbuf-${pkgver}"

  make DESTDIR="${pkgdir}" install
}

