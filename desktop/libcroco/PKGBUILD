# Based upon the PKGBUILD from Arch Linux
pkgname=libcroco
pkgver=0.6.4
pkgrel=1
pkgdesc="GNOME CSS2 parsing and manipulation toolkit"
arch=('x86_64' 'i686')
depends=('glib' 'libxml2')
makedepends=('pkg-config')
license=('LGPL')
options=('!libtool')
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/0.6/${pkgname}-${pkgver}.tar.xz")
url="http://www.gnome.org"
sha256sums=('c816bad3406c52a98d84a2222c0e4a7b70ee0640b49cde4a236deaa02c4232ea')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed 's/^\(SUBDIRS .*\=.*\)tests\(.*\)$/\1\2/' -i Makefile.am Makefile.in
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
