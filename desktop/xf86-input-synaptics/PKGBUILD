# Based upon the PKGBUILD from Arch Linux
pkgname=xf86-input-synaptics
pkgver=1.5.99.901
pkgrel=1
pkgdesc="Synaptics driver for notebook touchpads"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxtst' 'mtdev')
makedepends=('xorg-server-devel' 'libxi' 'libx11')
options=('!libtool')
source=(http://xorg.freedesktop.org/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
        synaptics-Fix-inverted-circular-scrolling-direction.patch)
sha1sums=('8836b1c2bf443b2c11f38457e203c5309698d9d0'
          '2e35cde5db6c435f612a5c6802f33aca8ba1b6bb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ${srcdir}/synaptics-Fix-inverted-circular-scrolling-direction.patch
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST}
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
  rm -rf "${pkgdir}/usr/share/X11"
}
