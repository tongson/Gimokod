# Based upon the PKGBUILD from Arch Linux
pkgname=libxfont
pkgver=1.4.5
pkgrel=1
pkgdesc="X11 font rasterisation library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('freetype' 'bzip2' 'libfontenc' 'xproto' 'fontsproto')
makedepends=('xorg-util-macros' 'xtrans')
options=('!libtool')
source=(${url}/archive/individual/lib/libXfont-${pkgver}.tar.bz2)
sha1sums=('0bf3cb8aa192ab96bbaf384d1a4a00d15a40171e')

build() {
  cd "${srcdir}/libXfont-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --disable-devel-docs \
              --enable-ipv6 \
              --without-xmlto \
              --with-bzip2 \
              --without-fop \
              --sysconfdir=/etc \
              --disable-static
  make
}

package() {
  cd "${srcdir}/libXfont-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
