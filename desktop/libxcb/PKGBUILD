# Based upon the PKGBUILD from Arch Linux
pkgname=libxcb
pkgver=1.7
pkgrel=1
pkgdesc="X11 client-side library"
arch=(i686 x86_64)
url="http://xcb.freedesktop.org/"
depends=('xcb-proto' 'libxdmcp' 'libxau')
makedepends=('pkg-config' 'libxslt' 'python')
conflicts=('libx11<1.1.99.2')
options=('!libtool')
license=('custom')
source=(${url}/dist/${pkgname}-${pkgver}.tar.bz2
        xcb_auth-fix-memory-leak.patch
        libxcb-1.1-no-pthread-stubs.patch)
sha1sums=('7540f0587907bce421914f0ddb813810cb2f36f8'
          '0c9246fd2ea270bfa06cb8ef93df2f8227d83c2a'
          '3455e84642283bc91c8313af319002a20bbcbdf4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/libxcb-1.1-no-pthread-stubs.patch"
  patch -Np1 -i "${srcdir}/xcb_auth-fix-memory-leak.patch"
  libtoolize --force --copy
  aclocal
  autoconf
  automake --add-missing
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --enable-xinput \
              --disable-build-docs \
              --disable-selinux
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
