# Based upon the PKGBUILD from Arch Linux
pkgname=gimp
pkgver=2.6.11
pkgrel=1
pkgdesc="GNU Image Manipulation Program"
arch=('i686' 'x86_64')
url="http://www.gimp.org/"
license=('GPL' 'LGPL')
depends=('aalib' 'poppler-glib' 'lcms' 'libxpm' 'libxmu' 'librsvg' 'dbus-glib'
         'libexif' 'gegl' 'hicolor-icon-theme')
makedepends=('intltool' 'gettext')
options=('!libtool' '!makeflags')
install=gimp.install
source=(ftp://ftp.gimp.org/pub/gimp/v${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2
        gimp-poppler-0.18.patch
        linux.gpl)
md5sums=('bb2939fe13e54fc7255cef5d097bb5dd'
         'bfc73f80e911ed7a7d500d80973469f1'
         'bb27bc214261d36484093e857f015f38')
sha1sums=('2f9d596e727bdbf304fa78257c1731d9faf3934c'
          '1479a6d80be7adf74f66a3b88057a5029fe892e8'
          '110ce9798173b19a662d086ed7b882b4729f06cf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 < ../gimp-poppler-0.18.patch
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST}  \
              --sysconfdir=/etc \
              --enable-mp \
              --enable-gimp-console \
              --enable-gimp-remote \
              --disable-python \
              --disable-nls \
              --enable-gtk-doc-html=no \
              --with-gif-compression=lzw \
              --without-libcurl \
              --with-aa \
              --without-hal \
              --without-gvfs \
              --without-gnomevfs \
              --without-alsa \
              --without-wmf \
              --without-webkit \
              --without-libmng \
              --with-poppler \
              --with-libpng \
              --with-librsvg \
              --with-libjpeg \
              --with-libtiff \
              --enable-mmx \
              --enable-sse
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/linux.gpl" "${pkgdir}/usr/share/gimp/2.0/palettes/Linux.gpl"

  rm "${pkgdir}/usr/share/man/man1/gimp-console.1"
  ln -s gimp-console-${pkgver%.*}.1.gz "${pkgdir}/usr/share/man/man1/gimp-console.1.gz"
  ln -s gimptool-2.0 "${pkgdir}/usr/bin/gimptool"
  ln -sf gimptool-2.0.1.gz "${pkgdir}/usr/share/man/man1/gimptool.1.gz"
}
