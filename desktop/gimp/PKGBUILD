# Based upon the PKGBUILD from Arch Linux
pkgname=gimp
pkgver=2.6.12
pkgrel=1
pkgdesc="GNU Image Manipulation Program"
arch=('i686' 'x86_64')
url="http://www.gimp.org/"
license=('GPL' 'LGPL')
depends=('aalib' 'poppler-glib' 'lcms' 'libxpm' 'libxmu' 'librsvg' 'dbus-glib'
         'libexif' 'gegl' 'hicolor-icon-theme')
makedepends=('intltool' 'gettext')
options=('!libtool' '!makeflags')
install=gimp.install
source=(ftp://ftp.gimp.org/pub/gimp/v${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2
        linux.gpl)
sha1sums=('82964e3d4eb003239f3443a1bccac53f5d780e15'
          '110ce9798173b19a662d086ed7b882b4729f06cf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST}  \
              --sysconfdir=/etc \
              --enable-mp \
              --enable-gimp-console \
              --enable-gimp-remote \
              --disable-python \
              --disable-nls \
              --enable-gtk-doc-html=no \
              --with-gif-compression=lzw \
              --without-libcurl \
              --with-aa \
              --without-hal \
              --without-gvfs \
              --without-gnomevfs \
              --without-alsa \
              --without-wmf \
              --without-webkit \
              --without-libmng \
              --with-poppler \
              --with-libpng \
              --with-librsvg \
              --with-libjpeg \
              --with-libtiff \
              --enable-mmx \
              --enable-sse
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/linux.gpl" "${pkgdir}/usr/share/gimp/2.0/palettes/Linux.gpl"

  rm "${pkgdir}/usr/share/man/man1/gimp-console.1"
  ln -s gimp-console-${pkgver%.*}.1.gz "${pkgdir}/usr/share/man/man1/gimp-console.1.gz"
  ln -s gimptool-2.0 "${pkgdir}/usr/bin/gimptool"
  ln -sf gimptool-2.0.1.gz "${pkgdir}/usr/share/man/man1/gimptool.1.gz"
}
