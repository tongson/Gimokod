# Based upon the PKGBUILD from Arch Linux
pkgname=gimp
pkgver=2.6.11
pkgrel=6
pkgdesc="GNU Image Manipulation Program"
arch=('i686' 'x86_64')
url="http://www.gimp.org/"
license=('GPL' 'LGPL')
depends=('lcms' 'libxpm' 'libxmu' 'librsvg' 'dbus-glib' \
         'libexif' 'gegl' 'desktop-file-utils' 'hicolor-icon-theme')
options=('!libtool' '!makeflags')
conflicts=('gimp-devel')
install=gimp.install
source=(ftp://ftp.gimp.org/pub/gimp/v${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2 linux.gpl 
        uri-backend-libcurl.patch)
md5sums=('bb2939fe13e54fc7255cef5d097bb5dd'
         'bb27bc214261d36484093e857f015f38'
         'e894f4b2ffa92c71448fdd350e9b78c6')
sha1sums=('2f9d596e727bdbf304fa78257c1731d9faf3934c'
          '110ce9798173b19a662d086ed7b882b4729f06cf'
          'a65b0ee6cd1b4345065b7b98c07f2fed15f844f4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../uri-backend-libcurl.patch
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-mp \
              --enable-gimp-console \
              --enable-gimp-remote \
              --disable-python \
              --with-gif-compression=lzw \
              --without-libcurl \
              --without-aa \
              --without-hal \
              --without-gvfs \
              --without-gnomevfs \
              --without-alsa \
              --without-wmf \
              --without-webkit \
              --without-libjasper \
              --without-libmng \
              --without-poppler \
              --with-libxpm \
              --without-xvfb-run \
              --with-libpng \
              --with-librsvg \
              --with-libjpeg \
              --with-libtiff \
              --enable-mmx \
              --enable-sse
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/linux.gpl" "${pkgdir}/usr/share/gimp/2.0/palettes/Linux.gpl"

  rm "${pkgdir}/usr/share/man/man1/gimp-console.1"
  ln -s gimp-console-${pkgver%.*}.1.gz "${pkgdir}/usr/share/man/man1/gimp-console.1.gz"
  ln -s gimptool-2.0 "${pkgdir}/usr/bin/gimptool"
  ln -sf gimptool-2.0.1.gz "${pkgdir}/usr/share/man/man1/gimptool.1.gz"
}
