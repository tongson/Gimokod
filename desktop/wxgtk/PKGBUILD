# Based upon the PKGBUILD from Arch Linux
pkgname=wxgtk
pkgver=2.8.12.1
pkgrel=2
pkgdesc="GTK+ implementation of wxWidgets API for GUI"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('expat' 'zlib' 'libjpeg' 'libpng' 'libtiff' 'gtk2' 'libgl' 'libxxf86vm' 'libsm')
makedepends=('mesa')
source=(http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2)
md5sums=('8c06c5941477beee213b4f2fa78be620')
sha1sums=('05688dc03d61631750f5904273122bb40a2115f5')

build() {
  cd "${srcdir}/wxPython-src-${pkgver}"
  sed -i '11697s:locale_install::' Makefile.in
  sed -i '11697s:locale_msw_install::' Makefile.in
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --libdir=/usr/lib \
              --with-gtk=2 \
              --with-opengl \
              --enable-intl=no \
              --disable-gtktest \
              --enable-opengl \
              --enable-unicode \
              --with-regex=builtin \
              --with-zlib=sys \
              --with-expat=sys \
              --enable-graphics_ctx \
              --without-gnomeprint \
              --disable-mediactrl \
              --with-libpng=sys \
              --with-libxpm=sys \
              --with-libjpeg=sys \
              --with-libtiff=sys \
              --without-gnomevfs
  make
  cd contrib/src
  make
}

package() {
  cd "${srcdir}/wxPython-src-${pkgver}"
  make DESTDIR="${pkgdir}" install
  cd contrib/src
  make DESTDIR="${pkgdir}" install
  install -D -m644 ../../docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
