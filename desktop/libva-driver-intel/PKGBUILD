# Based upon the PKGBUILD from Arch Linux
pkgname=libva-driver-intel
pkgver=1.0.15
pkgrel=1
pkgdesc="VA-API implementation for Intel G45 chipsets and Intel HD Graphics"
arch=('i686' 'x86_64')
url="http://freedesktop.org/wiki/Software/vaapi"
license=('MIT')
depends=('libva')
options=('!libtool')
source=(http://cgit.freedesktop.org/vaapi/intel-driver/snapshot/intel-driver-${pkgver}.tar.bz2)
md5sums=('9dbd642f18993335146480a3a2987874')

build() {
  cd "${srcdir}"/intel-driver-${pkgver}
  ./autogen.sh
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST}
  make
}

package() {
  cd "${srcdir}"/intel-driver-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m644 -D COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}

