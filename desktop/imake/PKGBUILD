# Based upon the PKGBUILD from Arch Linux
pkgname=imake
pkgver=1.0.4
pkgrel=1
pkgdesc="X.Org imake program and related utilities"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
makedepends=('pkg-config' 'xproto')
source=("http://xorg.freedesktop.org/releases/individual/util/imake-${pkgver}.tar.bz2"
        'http://xorg.freedesktop.org/releases/individual/util/gccmakedep-1.0.2.tar.bz2'
        'http://xorg.freedesktop.org/releases/individual/util/lndir-1.0.2.tar.bz2'
        'http://xorg.freedesktop.org/releases/individual/util/makedepend-1.0.3.tar.bz2'
        'http://xorg.freedesktop.org/releases/individual/util/xorg-cf-files-1.0.4.tar.bz2'
        'linuxconfig.patch'
        'LICENSE')
md5sums=('48133c75bd77c127c7eff122e08ebbf6'
         'b533c0771dbbaf9b041ff35bb941d3a2'
         '619acbb2ed766d7762f02328513b1f72'
         '4e6cb97bbecfbb34f3f644a75e513093'
         '700c6d040d36a569e657a3ba5e1d8b24'
         'b3385d8efbbe7c7fc50ed3a96ce37a16'
         'f5767133ef7a98a0bfa15139e0e3d32e')

build() {
  cd "${srcdir}"/xorg-cf-files-1.0.4*
  patch -Np1 -i "${srcdir}/linuxconfig.patch"
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --mandir=/usr/share/man
      make
      popd
    fi
  done
}

package() {
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      make DESTDIR="${pkgdir}" install
      popd
    fi
  done
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
