# Based upon the PKGBUILD from Arch Linux
pkgname=xextproto
pkgver=7.2.0
pkgrel=1
pkgdesc="X11 various extension wire protocol"
arch=('any')
license=('custom')
makedepends=('xorg-util-macros')
url="http://xorg.freedesktop.org/"
source=(${url}/releases/individual/proto/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('a117fb9d7fdebee7af3f9e79efe9812e39e650a5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --without-fop \
              --without-xmlto
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
