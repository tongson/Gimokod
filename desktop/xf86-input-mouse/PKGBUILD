# Based upon the PKGBUILD from Arch Linux
pkgname=xf86-input-mouse
pkgver=1.7.2
pkgrel=1
pkgdesc="X.org mouse input driver"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
makedepends=('xorg-server-devel')
options=('!libtool')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('0da6390c6f3d7d636110a52f08193739929ae4ae')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST}
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
