# Based upon the PKGBUILD from Arch Linux
pkgname=openbox
pkgver=3.5.0
pkgrel=5
pkgdesc="A window manager for the X11 windowing system"
arch=('x86_64')
url="http://openbox.org"
license=('GPL')
depends=('startup-notification' 'libxml2' 'libxinerama' 'libxrandr' 'libice' 'libx11' 'libxcb' 'icu'
         'libxcursor' 'pango' 'glib' 'fontconfig' 'libxft' 'libxt' 'libsm' 'gcc-libs' 'zlib')
makedepends=('pkg-config' 'xextproto' 'xf86vidmodeproto' 'xineramaproto')
options=('!libtool')
source=("http://www.icculus.org/openbox/releases/${pkgname}-${pkgver}.tar.gz"
        title-matching.patch
        movesize.patch)
md5sums=('00441b53cf14c03566c8e82643544ff9'
         '6f99711eee0b1c413dca9fa691917b71'
         'e09d77842aac9a0cd7eebe112df88383')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  Patch movesize.patch
  Patch title-matching.patch
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --with-x \
              --disable-nls \
              --disable-imlib2 \
              --enable-session-management \
              --enable-startup-notification \
              --sysconfdir=/etc \
              --libexecdir=/usr/lib/openbox
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  mkdir -p "${pkgdir}/etc/X11/Sessions"
  echo "/usr/bin/openbox-session" > "${pkgdir}/etc/X11/Sessions/${pkgname}"
  chmod a+x "${pkgdir}/etc/X11/Sessions/${pkgname}"
  make DESTDIR="${pkgdir}" install
}
