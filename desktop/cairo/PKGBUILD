# Based upon the PKGBUILD from Arch Linux
pkgname=cairo
pkgver=1.12.2
pkgrel=2
pkgdesc="Cairo vector graphics library"
arch=('x86_64')
license=('LGPL' 'MPL')
url="http://cairographics.org/"
depends=('libpng' 'libxrender' 'fontconfig' 'pixman' 'glib' 'libgl' 'libegl')
makedepends=('pkg-config' 'mesa')
options=('!libtool')
source=(http://cairographics.org/releases/$pkgname-$pkgver.tar.xz
        cairo-1.12.2-reduce-broken-stopped-edge-continuation.patch
        cairo-1.10.0-buggy_gradients.patch)
md5sums=('87649eb75789739d517c743e94879e51'
         '75ec73746cfaefcbed0e9b2a9f76bf00'
         '9b323790dab003e228c6955633cb888e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  Patch cairo-1.10.0-buggy_gradients.patch
  Patch cairo-1.12.2-reduce-broken-stopped-edge-continuation.patch
  autoreconf -vfi
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --disable-static \
              --disable-xlib-xcb \
              --enable-gl \
              --enable-tee
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
