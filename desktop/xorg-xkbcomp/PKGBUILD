# Based upon the PKGBUILD from Arch Linux
pkgname=xorg-xkbcomp
pkgver=1.2.3
pkgrel=1
pkgdesc="X Keyboard description compiler"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxkbfile')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(${url}/releases/individual/app/xkbcomp-${pkgver}.tar.bz2)
sha1sums=('c20d1b5b8e25634cc9a79c5e4c3397a5ffbee1e0')

build() {
  cd "${srcdir}/xkbcomp-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST}
  make
}

package() {
  cd "${srcdir}/xkbcomp-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
