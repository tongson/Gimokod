# Based upon the PKGBUILD from Arch Linux
pkgname=imlib2
pkgver=1.4.5
pkgrel=1
pkgdesc='Library that does image file loading and saving as well as rendering, manipulation, arbitrary polygon support'
url="http://sourceforge.net/projects/enlightenment/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('libtiff' 'giflib' 'bzip2' 'freetype2' 'libxext' 'libpng' 'libjpeg')
options=('!libtool')
source=("http://downloads.sourceforge.net/enlightenment/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('af86a2c38f4bc3806db57e64e74dc9814ad474a0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Configure and Build
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc/imlib2 \
              --x-libraries=/usr/lib \
              --without-id3
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Install License
  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}
