# Based upon the PKGBUILD from Arch Linux
pkgname=libdrm
pkgver=2.4.35
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=('x86_64')
license=('custom')
depends=('libpciaccess')
options=('!libtool')
url="http://dri.freedesktop.org/"
source=(http://dri.freedesktop.org/$pkgname/$pkgname-$pkgver.tar.bz2
        no-pthread-stubs.patch
        COPYING)
sha1sums=('a1d8d4945f782371d7855dbd693db885bd7e3d83'
          '2a5410baa3e6e078f9378ce486a88f41d22fd838'
          'ba3dcd636997ee0d30df14b03dae05c24ae5d094')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/no-pthread-stubs.patch"

  # patch -Np1 -i ${srcdir}/git_fixes.diff

  autoreconf --force --install
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --enable-udev \
              --enable-intel \
              --enable-radeon \
              --enable-libkms \
              --enable-vmwgfx-experimental-api \
              --enable-nouveau-experimental-api
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -k check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/"
}
