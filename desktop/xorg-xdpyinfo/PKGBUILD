# Based upon the PKGBUILD from Arch Linux
pkgname=xorg-xdpyinfo
pkgver=1.3.0
pkgrel=1
pkgdesc="Display information utility for X"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxext' 'libxtst' 'libxxf86vm' 'libxrender' 'libxcomposite' 'libxinerama')
makedepends=('xorg-util-macros')
source=(http://xorg.freedesktop.org/archive/individual/app/xdpyinfo-${pkgver}.tar.bz2)
sha256sums=('23ee4944a32b5701b4379cb420729eb7a4dde54de2b5b006d4747855efd6d73f')

build() {
  cd "${srcdir}/xdpyinfo-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --without-dmx \
              --without-dga \
              --without-xf86misc \
              --with-xinerama
  make
}

package() {
  cd "${srcdir}/xdpyinfo-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
