# Based upon the PKGBUILD from Arch Linux
pkgname=chromium
pkgver=19.0.1084.52
pkgrel=1
pkgdesc="The open-source project behind Google Chrome browser"
arch=('x86_64')
url="http://www.chromium.org/"
license=('BSD')
depends=('zlib' 'gtk2' 'dbus-glib' 'nss' 'nss-freebl' 'alsa-lib' 'xdg-utils' 'bzip2' 'libevent'
         'libxss' 'libgcrypt' 'ttf-dejavu' 'desktop-file-utils' 'libxml2'
         'hicolor-icon-theme' 'libpng' 'speex' 'libxslt')
makedepends=('python' 'perl' 'gperf' 'yasm' 'mesa')
source=(http://commondatastorage.googleapis.com/chromium-browser-official/$pkgname-$pkgver.tar.bz2)
sha256sums=('e725d4aa7608c9975b8989188d85d38846956db2f480a1a217bcab06419dab72')

build() {
  cd "${srcdir}/chromium-${pkgver}"

  # NaCL disabled
  cp "${srcdir}/nacl.gypi" chrome/

  patch -i "$srcdir/sqlite-3.7.6.3-fix-out-of-scope-memory-reference.patch" \
    third_party/sqlite/amalgamation/sqlite3.c

  # We need to disable system_ssl until "next protocol negotiation" support is
  # available in our nss package.
  # (See https://bugzilla.mozilla.org/show_bug.cgi?id=547312)

  find third_party -type f \! -iname '*.gyp*' \
    \! -path 'third_party/adobe/*' \
    \! -path 'third_party/WebKit/*' \
    \! -path 'third_party/angle/*' \
    \! -path 'third_party/cacheinvalidation/*' \
    \! -path 'third_party/cld/*' \
    \! -path 'third_party/expat/*' \
    \! -path 'third_party/ffmpeg/*' \
    \! -path 'third_party/flac/*' \
    \! -path 'third_party/gpsd/*' \
    \! -path 'third_party/harfbuzz/*' \
    \! -path 'third_party/hunspell/*' \
    \! -path 'third_party/iccjpeg/*' \
    \! -path 'third_party/icu/*' \
    \! -path 'third_party/jsoncpp/*' \
    \! -path 'third_party/launchpad_translations/*' \
    \! -path 'third_party/leveldb/*' \
    \! -path 'third_party/leveldatabase/*' \
    \! -path 'third_party/libjingle/*' \
    \! -path 'third_party/libphonenumber/*' \
    \! -path 'third_party/libsrtp/*' \
    \! -path 'third_party/libvpx/*' \
    \! -path 'third_party/libwebp/*' \
    \! -path 'third_party/libyuv/*' \
    \! -path 'third_party/khronos/*' \
    \! -path 'third_party/mesa/*' \
    \! -path 'third_party/modp_b64/*' \
    \! -path 'third_party/mongoose/*' \
    \! -path 'third_party/npapi/*' \
    \! -path 'third_party/openmax/*' \
    \! -path 'third_party/ots/*' \
    \! -path 'third_party/ply/*' \
    \! -path 'third_party/protobuf/*' \
    \! -path 'third_party/scons-2.0.1/*' \
    \! -path 'third_party/sfntly/*' \
    \! -path 'third_party/skia/*' \
    \! -path 'third_party/smhasher/*' \
    \! -path 'third_party/speex/speex.h' \
    \! -path 'third_party/sqlite/*' \
    \! -path 'third_party/tcmalloc/*' \
    \! -path 'third_party/tlslite/*' \
    \! -path 'third_party/undoview/*' \
    \! -path 'third_party/v8-i18n/*' \
    \! -path 'third_party/webdriver/*' \
    \! -path 'third_party/webgl_conformance/*' \
    \! -path 'third_party/webrtc/*' \
    \! -path 'third_party/yasm/*' \
    \! -path 'third_party/zlib/contrib/minizip/*' \
    \! -path 'third_party/nss/*' \
    \! -path 'third_party/nspr/*' \
    \! -path 'third_party/openssl/*' \
    \! -path 'third_party/mozilla/*' \
    \! -path 'third_party/mozilla_security_manager/*' \
    -delete
  export CXXFLAGS="${CXXFLAGS/-Os/-O2}"
  export CFLAGS="${CFLAGS/-Os/-O2}"
  export CFLAGS+=' -Wno-c++0x-compat'
  export CXX="/usr/bin/x86_64-pc-linux-gnu-g++"
  export CC="/usr/bin/x86_64-pc-linux-gnu-gcc"
  build/gyp_chromium --depth=. \
    -Dwerror= \
    -Dlinux_sandbox_path=/usr/lib/chromium/chromium-sandbox \
    -Dlinux_sandbox_chrome_path=/usr/lib/chromium/chromium \
    -Dlinux_strip_binary=1 \
    -Drelease_extra_cflags="${CFLAGS}" \
    -Dffmpeg_branding=Chrome \
    -Dproprietary_codecs=0 \
    -Duse_system_bzip2=1 \
    -Duse_system_ffmpeg=0 \
    -Duse_system_libevent=1 \
    -Duse_system_libxml=1 \
    -Duse_system_icu=0 \
    -Duse_system_ssl=0 \
    -Duse_system_yasm=1 \
    -Duse_system_zlib=1 \
    -Duse_system_speex=1 \
    -Duse_system_xdg_utils=1 \
    -Duse_gconf=0 \
    -Ddisable_nacl=1 \
    -Duse_cups=0 \
    -Duse_gconf=0 \
    -Duse_gnome_keyring=0 \
    -Dlinux_link_gnome_keyring=0 \
    -Duse_kerberos=0 \
    -Duse_pulseaudio=0 \
    -Denable_webrtc=1 \
    -Ddisable_remoting=1 \
    -Ddisable_input_speech=1 \
    -Duse_system_libwebp=0 \
    -Duse_system_vpx=0 \
    -Duse_system_libpng=1 \
    -Duse_system_libjpeg=1 \
    -Ddisable_sse2=1 \
    -Duse_system_flac=0 \
    -Dlinux_use_gold_binary=0 \
    -Dlinux_use_gold_flags=0 \
    -Dtarget_arch=x64

  make chrome chrome_sandbox BUILDTYPE=Release V=1
}

package() {
  cd "${srcdir}/chromium-${pkgver}"

  install -D out/Release/chrome "${pkgdir}"/usr/lib/chromium/chromium

  install -Dm4755 -o root -g root out/Release/chrome_sandbox \
    "${pkgdir}/usr/lib/chromium/chromium-sandbox"

  # Add out/Release/{libppGoogleNaClPluginChrome.so,nacl_irt_x86_*.nexe} again
  # when NaCl build is fixed
  cp out/Release/{{chrome,resources}.pak,libffmpegsumo.so} \
    "${pkgdir}/usr/lib/chromium/"

  # These links are only needed when building with system ffmpeg
  #ln -s /usr/lib/libavcodec.so.52 "${pkgdir}"/usr/lib/chromium/
  #ln -s /usr/lib/libavformat.so.52 "${pkgdir}"/usr/lib/chromium/
  #ln -s /usr/lib/libavutil.so.50 "${pkgdir}"/usr/lib/chromium/

  cp -a out/Release/locales out/Release/resources "${pkgdir}/usr/lib/chromium/"

  find "${pkgdir}/usr/lib/chromium/" -name '*.d' -type f -delete

  install -Dm644 out/Release/chrome.1 "${pkgdir}/usr/share/man/man1/chromium.1"

  for size in 16 22 24 32 48 64 128 256; do
    install -Dm644 "chrome/app/theme/chromium/product_logo_$size.png" \
      "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/chromium.png"
  done

  install -D "${srcdir}/chromium.sh" "${pkgdir}/usr/bin/chromium"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/chromium/LICENSE"
}

source=(${source[@]-}
        chromium.sh
        nacl.gypi
        sqlite-3.7.6.3-fix-out-of-scope-memory-reference.patch)

sha256sums=(${sha256sums[@]-}
            '10edbd6d0d961f12fde37aec9b2e1508c44309080409f0a0708d493ef941e25e'
            '25bddd2eee7821e18ddda06f6e69552c359e1c04a10971ac3985556c3d8ef059'
            'a700aa054800d1b21d84eaba27c38a703dfa023e9226d11a942690c2a0630aff')
