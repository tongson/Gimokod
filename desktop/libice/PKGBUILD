# Based upon the PKGBUILD from Arch Linux
pkgname=libice
pkgver=1.0.8
pkgrel=1
pkgdesc="X11 Inter-Client Exchange library"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
depends=('xproto')
makedepends=('pkg-config' 'xtrans')
options=('!libtool')
source=(${url}/releases/individual/lib/libICE-${pkgver}.tar.bz2)
license=('custom')
sha1sums=('ddb14df8bbc43df7322978f5f9f802936e2a7324')

build() {
  cd "${srcdir}/libICE-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --enable-ipv6 \
              --disable-docs \
              --disable-specs \
              --without-xmlto \
              --without-fop
  make
}

package() {
  cd "${srcdir}/libICE-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
