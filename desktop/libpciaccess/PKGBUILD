# Based upon the PKGBUILD from Arch Linux
pkgname=libpciaccess
pkgver=0.12.1
pkgrel=1
pkgdesc="X11 PCI access library"
arch=(i686 x86_64)
license=('custom')
url="http://xorg.freedesktop.org/"
makedepends=('pkg-config' 'xorg-util-macros')
options=('!libtool')
source=(${url}/releases/individual/lib/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('4933bda545df37395e57ff6b4bd61e17a5431770')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
