# Based upon the PKGBUILD from Arch Linux
pkgname=libxi
pkgver=1.4.3
pkgrel=1
pkgdesc="X11 Input extension library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org"
depends=('libxext' 'inputproto')
makedepends=('pkg-config' 'xorg-util-macros')
options=(!libtool)
license=('custom')
source=(${url}/releases/individual/lib/libXi-${pkgver}.tar.bz2)
sha1sums=('c66cfdee74e8d169a7992b5f257395e653ca761b')

build() {
  cd "${srcdir}/libXi-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --disable-static \
              --disable-specs \
              --without-fop \
              --without-xmlto \
              --without-asciidoc
  make
}

package() {
  cd "${srcdir}/libXi-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
