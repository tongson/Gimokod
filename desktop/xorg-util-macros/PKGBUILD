# Based upon the PKGBUILD from Arch Linux
pkgname=xorg-util-macros
pkgver=1.16.0
pkgrel=1
pkgdesc="X.Org Autotools macros"
arch=(any)
license=('custom')
url="http://xorg.freedesktop.org/"
source=(${url}/releases/individual/util/util-macros-${pkgver}.tar.bz2)
sha1sums=('1c08e52ec492df85be49a3b157ec39f7d4e52fa4')

build() {
  cd "${srcdir}/util-macros-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST}
  make
}

package() {
  cd "${srcdir}/util-macros-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/lib"
  mv "${pkgdir}/usr/share/pkgconfig" "${pkgdir}/usr/lib/"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
