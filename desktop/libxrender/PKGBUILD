# Based upon the PKGBUILD from Arch Linux
pkgname=libxrender
pkgver=0.9.7
pkgrel=1
pkgdesc="X Rendering Extension client library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'renderproto')
makedepends=('pkg-config')
options=('!libtool')
source=(${url}/releases/individual/lib/libXrender-${pkgver}.tar.bz2)
sha1sums=('6e4d75d8316d5217324258f555eba3a5d383feb2')

build() {
  cd "${srcdir}/libXrender-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --disable-static
  make
}

package() {
  cd "${srcdir}/libXrender-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
