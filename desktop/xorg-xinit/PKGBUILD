# Based upon the PKGBUILD from Arch Linux
pkgname=xorg-xinit
pkgver=1.3.1
pkgrel=2
pkgdesc="X.Org initialisation program "
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libx11' 'xorg-xauth')
makedepends=('xorg-util-macros')
source=(${url}/releases/individual/app/xinit-${pkgver}.tar.bz2
        06_move_serverauthfile_into_tmp.diff
        xinitrc
        xsession
        xserverrc)
options=('!emptydirs')
md5sums=('ee234056d8a3dbf37b61b4bcb35b88e4'
         'abd072ec435ce084cf4e89c58f83d45c'
         'c7b406c3c5c1e8011b3ac7d6b29c1a2c'
         '7873d97db30d6c279ed37e3559e7c59d'
         'c5baef10fdb7239594daee72a94ddab3')

build() {
  cd "${srcdir}/xinit-${pkgver}"
  patch -Np1 -i "${srcdir}/06_move_serverauthfile_into_tmp.diff"
  sed -i -e 's/XSLASHGLOB.sh/XSLASHGLOB/' xinitrc.cpp
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --with-xinitdir=/etc/X11/xinit
  make
}

package() {
  cd "${srcdir}/xinit-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/skel"
  install -m644 "${srcdir}/xinitrc" "${pkgdir}/etc/skel/.xinitrc"
  install -m644 "${srcdir}/xsession" "${pkgdir}/etc/skel/.xsession"
  install -m644 "${srcdir}/xserverrc" "${pkgdir}/etc/X11/xinit/xserverrc"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
