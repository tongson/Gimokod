# Based upon the PKGBUILD from Arch Linux
pkgname=rxvt-unicode
pkgver=9.15
pkgrel=4
pkgdesc="An unicode enabled rxvt-clone terminal emulator (urxvt)"
arch=('i686' 'x86_64')
url="http://software.schmorp.de/pkg/rxvt-unicode.html"
license=('GPL')
depends=('libxft' 'libx11' 'libxrender' 'ncurses' 'perl' 'gcc-libs')
makedepends=('pkg-config' 'xproto')
source=(http://dist.schmorp.de/rxvt-unicode/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('ec1aa2932da844979ed8140bd92223defb12042aa5e877e05ac31139ca81f2b1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  Patch rxvt-unicode-9.14-clear.patch
  Patch rxvt-unicode-9.14-secondary-wheel.patch
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --with-terminfo=/usr/share/terminfo \
              --enable-256-color \
              --enable-font-styles \
              --enable-xim \
              --enable-keepscrolling \
              --disable-startup-notification \
              --enable-selectionscrolling \
              --enable-smart-resize \
              --disable-pixbuf \
              --disable-transparency \
              --enable-xft \
              --enable-utmp \
              --enable-wtmp \
              --enable-perl \
              --disable-unicode3 \
              --enable-lastlog \
              --disable-frills
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -d "${pkgdir}/usr/share/terminfo"
  export TERMINFO="${pkgdir}/usr/share/terminfo"
  make DESTDIR="${pkgdir}" install
}

source=(${source[@]-}
        rxvt-unicode-9.14-clear.patch
        rxvt-unicode-9.14-secondary-wheel.patch)

sha256sums=(${sha256sums[@]-}
            'c1010b15d74f56671351e8314264d76f638230e94d4e334691af29a9353a9540'
            '95965a8c245b5de04af2c1806718f38c3f8d320395991fffeb9a935e9bf37b76')
