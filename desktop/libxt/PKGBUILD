# Based upon the PKGBUILD from Arch Linux
pkgname=libxt
pkgver=1.1.1
pkgrel=1
pkgdesc="X11 toolkit intrinsics library"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libsm' 'libx11')
makedepends=('xorg-util-macros')
options=('!libtool')
source=(${url}/releases/individual/lib/libXt-${pkgver}.tar.bz2)
sha1sums=('a29a97f8521bdc7a95364e163f0ce474de572ae5')

build() {
  cd "${srcdir}/libXt-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --disable-static
  make
}

package() {
  cd "${srcdir}/libXt-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
