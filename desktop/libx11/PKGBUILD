# Based upon the PKGBUILD from Arch Linux
pkgname=libx11
pkgver=1.5.0
pkgrel=1
pkgdesc="X11 client-side library"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
depends=('libxcb' 'xproto' 'kbproto')
makedepends=('xorg-util-macros' 'xextproto' 'xtrans' 'inputproto')
options=('!libtool')
license=('custom')
source=(${url}/releases/individual/lib/libX11-${pkgver}.tar.bz2
        xorg.sh)
sha256sums=('c382efd7e92bfc3cef39a4b7f1ecf2744ba4414a705e3bc1e697f75502bd4d86'
            '1a1b5db0320579958fba24774bae7984a1b1b8c647ac6fbd4d8937178c48ed46')

build() {
  cd "${srcdir}/libX11-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --disable-static \
              --disable-xf86bigfont \
              --without-fop \
              --without-xmlto \
              --disable-specs
  make
}

package() {
  cd "${srcdir}/libX11-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/profile.d"
  install -m755 "${srcdir}/"xorg.sh "${pkgdir}/etc/profile.d/"

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
