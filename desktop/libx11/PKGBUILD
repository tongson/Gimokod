# Based upon the PKGBUILD from Arch Linux
pkgname=libx11
pkgver=1.4.4
pkgrel=1
pkgdesc="X11 client-side library"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
depends=('libxcb' 'xproto' 'kbproto')
makedepends=('xorg-util-macros' 'xextproto' 'xtrans' 'inputproto')
options=('!libtool')
license=('custom')
source=(${url}/releases/individual/lib/libX11-${pkgver}.tar.bz2
        xorg.sh
        xorg.csh)
sha1sums=('2d9d38bfd7003ec60eea0b1999ab72cfd5fa3b90'
          '4b756387426afb2a5bb7a155631ec42348abf5c1'
          '45b0b310776cf5d9a94b4a18a5c63ddad9de6ba8')

build() {
  cd "${srcdir}/libX11-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --disable-static \
              --disable-xf86bigfont \
              --without-fop \
              --without-xmlto \
              --disable-specs
  make
}

package() {
  cd "${srcdir}/libX11-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/profile.d"
  install -m755 "${srcdir}/"xorg.{sh,csh} "${pkgdir}/etc/profile.d/"

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
