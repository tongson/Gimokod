# Based upon the PKGBUILD from Arch Linux
pkgname=libgsf
pkgver=1.14.23
pkgrel=1
pkgdesc="Library for reading and writing structured file formats"
arch=('x86_64')
url="http://www.gnome.org/"
license=('GPL' 'LGPL')
depends=('libxml2' 'glib' 'bzip2')
makedepends=('pkg-config')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/1.14/$pkgname-$pkgver.tar.xz)
sha256sums=('bfc1c6178f5319d5e6d854c380ce26542f9a103a5ff31c9d25a834e0be52fb17')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --mandir=/usr/share/man \
              --with-gio \
              --with-bz2 \
              --without-gnome-vfs \
              --without-bonobo \
              --without-python \
              --without-gdk-pixbuf \
              --without-gconf \
              --disable-static
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install
  rm -f "${pkgdir}/usr/share/man/man1/gsf-office-thumbnailer.1"
  rm -f "${pkgdir}/usr/lib/libgsf-gnome"*
  rm -rf "${pkgdir}/usr/include/libgsf-1/gsf-gnome/"
  rm -rf "${pkgdir}/etc/gconf"
  rm -f "${pkgdir}/usr/lib/pkgconfig/libgsf-gnome-1.pc"
  rm -f "${pkgdir}/usr/bin/gsf-office-thumbnailer"
  rm -f "${pkgdir}"/usr/lib/python*/site-packages/gsf/gnomemodule.so
}
