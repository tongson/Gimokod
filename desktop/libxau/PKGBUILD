# Based upon the PKGBUILD from Arch Linux
pkgname=libxau
pkgver=1.0.6
pkgrel=1
pkgdesc="X11 authorisation library"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
depends=('xproto')
makedepends=('pkg-config')
license=('custom')
options=('!libtool')
source=(${url}/releases/individual/lib/libXau-${pkgver}.tar.bz2)
sha1sums=('0e1ab449f98026e4599f6e0d7491810d36c8fe4d')

build() {
  cd "${srcdir}/libXau-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/libXau-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
