# Based upon the PKGBUILD from Arch Linux
pkgname=libxtst
pkgver=1.2.0
pkgrel=1
pkgdesc="X11 Testing -- Resource extension library"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxext' 'libxi' 'recordproto' 'inputproto')
makedepends=('xorg-util-macros')
options=('!libtool')
source=(${url}/releases/individual/lib/libXtst-${pkgver}.tar.bz2)
sha1sums=('9fb06ed599caf7f9e7115cbbfadf02b47c17aa72')

build() {
  cd "${srcdir}/libXtst-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --without-xmlto \
              --without-fop \
              --disable-static
  make
}

package() {
  cd "${srcdir}/libXtst-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
