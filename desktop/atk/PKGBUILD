# Based upon the PKGBUILD from Arch Linux
pkgname=atk
pkgver=2.4.0
pkgrel=1
pkgdesc="A library providing a set of interfaces for accessibility"
arch=('i686' 'x86_64')
url='http://www.gtk.org/'
license=('LGPL')
depends=('glib')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('091e9ce975a9fbbc7cd8fa64c9c389ffb7fa6cdde58b6d5c01b2c267093d888d')

build() {
  cd "${pkgname}-${pkgver}"
  sed 's/^\(SUBDIRS =.*\)tests\(.*\)$/\1\2/' -i Makefile.am Makefile.in
  sed -e '/-D[A-Z_]*DISABLE_DEPRECATED/d' -i atk/Makefile.am atk/Makefile.in \
                  tests/Makefile.am tests/Makefile.in
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --enable-gtk-doc-html=no \
              --enable-introspection=no
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
