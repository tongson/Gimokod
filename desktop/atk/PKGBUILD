# Based upon the PKGBUILD from Arch Linux
pkgname=atk
pkgver=2.2.0
pkgrel=1
pkgdesc="A library providing a set of interfaces for accessibility"
arch=('i686' 'x86_64')
url='http://www.gtk.org/'
license=('LGPL')
depends=('glib')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('8b22f0e7803dd3734c676ccd68ea999ff1156ca49d99c3de5c1d269ad0c3739d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed 's/^\(SUBDIRS =.*\)tests\(.*\)$/\1\2/' -i Makefile.am Makefile.in
  sed -e '/-D[A-Z_]*DISABLE_DEPRECATED/d' -i atk/Makefile.am atk/Makefile.in \
                  tests/Makefile.am tests/Makefile.in
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --enable-gtk-doc-html=no \
              --enable-introspection=no
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
