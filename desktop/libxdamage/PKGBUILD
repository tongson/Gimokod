# Based upon the PKGBUILD from Arch Linux
pkgname=libxdamage
pkgver=1.1.3
pkgrel=1
pkgdesc="X11 damaged region extension library"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxfixes' 'damageproto')
makedepends=('pkg-config')
options=('!libtool')
source=(${url}/releases/individual/lib/libXdamage-${pkgver}.tar.bz2)
sha1sums=('7d96e8de107fede16951cb47d5e147630fbc7dee')

build() {
  cd "${srcdir}/libXdamage-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --disable-static
  make
}

package() {
  cd "${srcdir}/libXdamage-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
