# Based upon the PKGBUILD from Arch Linux
pkgname=libva
pkgver=1.1.0
pkgrel=1
pkgdesc="Video Acceleration (VA) API for Linux"
arch=('x86_64')
url="http://freedesktop.org/wiki/Software/vaapi"
license=('MIT')
depends=('libegl' 'libgl' 'libdrm' 'libxfixes' 'udev')
makedepends=('mesa')
options=('!libtool')
source=(http://cgit.freedesktop.org/libva/snapshot/${pkgname}-${pkgver}.tar.bz2)
md5sums=('b22179bd44137e91d5c77a43a0cb8063')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./autogen.sh
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST}  \
              --enable-dummy-driver \
              --enable-dummy-backend
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m644 -D COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}

