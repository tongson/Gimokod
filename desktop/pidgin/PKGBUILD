# Based upon the PKGBUILD from Arch Linux
pkgname=('pidgin' 'libpurple' )
pkgver=2.10.0
pkgrel=1
arch=('i686' 'x86_64')
url="http://pidgin.im/"
license=('GPL')
makedepends=('startup-notification' 'libxss' 'nss' 'libsasl' 'libsm'
              'hicolor-icon-theme' 'ca-certificates')
options=('!libtool')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        nm09-more.patch)
md5sums=('e1453c9093c4f32beec19abd14069a3f'
         'a673659d86c7a65aa710f7c8c7feda82')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Further fixes for NetworkManager 0.9
  # http://developer.pidgin.im/ticket/13859
  patch -Np1 -i "${srcdir}/nm09-more.patch"

  # Use Python 2
  sed -i 's/env python$/\02/' */plugins/*.py \
    libpurple/purple-{remote,notifications-example,url-handler}

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --disable-cap \
              --disable-gevolution \
              --disable-gtkspell \
              --disable-perl \
              --disable-debug \
              --disable-tk \
              --disable-tcl \
              --disable-gstreamer \
              --disable-farsight \
              --disable-vv \
              --disable-avahi \
              --disable-mono \
              --disable-dbus \
              --disable-python \
              --disable-schemas-install \
              --disable-meanwhile \
              --enable-gnutls=no \
              --enable-nss=yes \
              --enable-cyrus-sasl \
              --disable-doxygen \
              --disable-nm \
              --with-system-ssl-certs=/etc/ssl/certs
  make
}

package_pidgin(){
  pkgdesc="Multi-protocol instant messaging client"
  depends=("libpurple=${pkgver}-$pkgrel" 'startup-notification'
           'libxss' 'libsm' 'hicolor-icon-theme')
  install=pidgin.install

  cd "${srcdir}/pidgin-${pkgver}"

  # For linking
  make -C libpurple DESTDIR="${pkgdir}" install-libLTLIBRARIES

  make -C pidgin DESTDIR="${pkgdir}" install
  make -C doc DESTDIR="${pkgdir}" install

  # Remove files that are packaged in libpurle
  make -C libpurple DESTDIR="${pkgdir}" uninstall-libLTLIBRARIES

  install -Dm644 pidgin.desktop "${pkgdir}"/usr/share/applications/pidgin.desktop 

  rm "${pkgdir}/usr/share/man/man1/finch.1"
}

package_libpurple(){
  pkgdesc="IM library extracted from Pidgin"
  depends=('libsasl' 'nss')

  cd "${srcdir}/pidgin-${pkgver}"

  for _dir in libpurple share/sounds share/ca-certs m4macros; do
    make -C "$_dir" DESTDIR="${pkgdir}" install
  done
}


