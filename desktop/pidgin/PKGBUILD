# Based upon the PKGBUILD from Arch Linux
pkgbase=pidgin
pkgname=('pidgin' 'libpurple')
pkgver=2.10.2
pkgrel=1
arch=('x86_64')
url="http://pidgin.im/"
license=('GPL')
makedepends=('startup-notification' 'libxss' 'nss' 'libsasl' 'libsm'
              'hicolor-icon-theme' 'ca-certificates')
options=('!libtool')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('e5cdd29056d36677cb81225c3b36b1bcde64605e201492f6e2fdf27ff24d32b8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --disable-consoleui \
              --enable-gtkui \
              --enable-sm \
              --disable-cap \
              --disable-gevolution \
              --disable-gtkspell \
              --disable-perl \
              --disable-debug \
              --disable-tk \
              --disable-tcl \
              --disable-gstreamer \
              --disable-gstreamer-interfaces \
              --disable-idn \
              --disable-farsight \
              --disable-vv \
              --disable-avahi \
              --disable-mono \
              --disable-dbus \
              --disable-schemas-install \
              --disable-meanwhile \
              --enable-gnutls=no \
              --enable-nss=yes \
              --enable-cyrus-sasl \
              --enable-nls \
              --disable-doxygen \
              --disable-nm \
              --with-system-ssl-certs=/etc/ssl/certs
  make
}

package_pidgin(){
  pkgdesc="Multi-protocol instant messaging client"
  depends=("libpurple=${pkgver}-$pkgrel" 'startup-notification'
           'libxss' 'libsm' 'hicolor-icon-theme')
  install=pidgin.install

  cd "${srcdir}/pidgin-${pkgver}"

  # For linking
  make -C libpurple DESTDIR="${pkgdir}" install-libLTLIBRARIES

  make -C pidgin DESTDIR="${pkgdir}" install
  make -C doc DESTDIR="${pkgdir}" install

  # Remove files that are packaged in libpurle
  make -C libpurple DESTDIR="${pkgdir}" uninstall-libLTLIBRARIES
}

package_libpurple(){
  pkgdesc="IM library extracted from Pidgin"
  depends=('libsasl' 'nss')

  cd "${srcdir}/pidgin-${pkgver}"

  for _dir in libpurple share/sounds share/ca-certs m4macros; do
    make -C "$_dir" DESTDIR="${pkgdir}" install
  done
}


