# Based upon the PKGBUILD from Arch Linux
pkgname=cdparanoia
pkgver=10.2
pkgrel=1
pkgdesc="Compact Disc Digital Audio extraction tool"
arch=('i686' 'x86_64')
url="http://www.xiph.org/paranoia/"
license=('GPL')
options=('!makeflags')
source=("http://downloads.xiph.org/releases/cdparanoia/cdparanoia-III-${pkgver}.src.tgz"
        'gcc.patch')
md5sums=('b304bbe8ab63373924a744eac9ebc652'
         '12da14958d2b84c6719fe69890436445')

build() {
  cd "${srcdir}/${pkgname}-III-${pkgver}"
  patch -p0 -i "${srcdir}"/gcc.patch
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${pkgname}-III-${pkgver}"
  make prefix="${pkgdir}/usr" MANDIR="${pkgdir}/usr/share/man" install
}
