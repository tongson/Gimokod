# Based upon the PKGBUILD from Arch Linux
pkgname=pixman
pkgver=0.24.2
pkgrel=1
pkgdesc="Pixman library"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org"
license=('custom')
options=('!libtool')
source=(http://xorg.freedesktop.org/releases/individual/lib/${pkgname}-${pkgver}.tar.bz2
        revert-reject-trapezoids-where-top-is-above-the-edges.patch)
sha1sums=('233eed3cd16ee1b2a842140477503d2c68b74418'
          'ee36251d47ccefd5e276cabdba9917fbb2be787a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  Patch revert-reject-trapezoids-where-top-is-above-the-edges.patch
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --disable-static \
              --disable-gtk \
              --enable-sse2 \
              --enable-mmx
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
