# Based upon the PKGBUILD from Arch Linux
pkgname=freeglut
pkgver=2.6.0
pkgrel=1
pkgdesc="Provides functionality for small OpenGL programs"
arch=('i686' 'x86_64')
url="http://freeglut.sourceforge.net/"
license=('MIT')
depends=('libxxf86vm' 'mesa' 'libxi')
options=('!libtool')
source=(http://downloads.sourceforge.net/freeglut/${pkgname}-${pkgver}.tar.gz \
        2.6.0-GFX_radeon.patch)
md5sums=('39f0f2de89f399529d2b981188082218' '6d0a018fe4f0bc9ace2b244ca59514d3')
sha1sums=('68306c4486c13d005a4e4d54035e0c0b1bdc220b' '2e392c7d0b60016ec89e6a5edf504f5722351b95')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 -i "${srcdir}/2.6.0-GFX_radeon.patch"

  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --disable-warnings \
              --disable-warnings-as-errors \
              --enable-replace-glut \
              --disable-debug
  make all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
