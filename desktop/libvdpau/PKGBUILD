# Based upon the PKGBUILD from Arch Linux
pkgname=libvdpau
pkgver=0.4.1
pkgrel=1
pkgdesc="Nvidia VDPAU library"
arch=(i686 x86_64)
url=http://cgit.freedesktop.org/~aplattner/libvdpau
depends=
conflicts=('nvidia-utils<190.42-2')
options=('!libtool' 'force')
license=('custom')
source=(http://people.freedesktop.org/~aplattner/vdpau/${pkgname}-${pkgver}.tar.gz)
makedepends=('libx11')
sha256sums=('061b6f86c64912f79851bfefcac4f77b401e9a939dbbb7b1ec795bc7d5aaf59b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --disable-documentation --disable-dependency-tracking
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
