# Based upon the PKGBUILD from Arch Linux
pkgname=enchant
pkgver=1.6.0
pkgrel=1
pkgdesc="A wrapper library for generic spell checking"
arch=('x86_64')
url="http://www.abisource.com/enchant/"
license=('LGPL')
depends=('glib' 'hunspell')
makedepends=('pkg-config')
options=('!libtool')
source=("http://www.abisource.com/downloads/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('de11011aff801dc61042828041fb59c7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e 's:noinst_PROGRAMS:check_PROGRAMS:' tests/Makefile.am
  autoreconf -vfi
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --disable-static \
              --disable-ispell \
              --disable-aspell \
              --enable-myspell \
              --with-system-myspell \
              --disable-zemberek \
              --with-myspell-dir=/usr/share/myspell
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
