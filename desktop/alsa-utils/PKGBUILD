# Based upon the PKGBUILD from Arch Linux
pkgname=alsa-utils
pkgver=1.0.24.2
pkgrel=2
pkgdesc="ALSA utilities"
arch=('i686' 'x86_64')
url="http://www.alsa-project.org"
depends=('alsa-lib' 'dialog' 'ncurses')
options=('emptydirs')
license=('GPL')
source=(ftp://ftp.alsa-project.org/pub/utils/${pkgname}-${pkgver}.tar.bz2)
md5sums=('8238cd57cb301d1c36bcf0ecb59ce6b2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's:sbin:bin:' alsactl/90-alsa-restore.rules
  ./configure --prefix=/usr \
              --sbindir=/usr/bin \
              --build=${CBUILD} \
              --host=${CHOST} \
              --disable-nls \
              --disable-xmlto \
              --disable-alsaconf \
              --with-udev-rules-dir=/usr/lib/udev/rules.d \
              --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -d "${pkgdir}/var/lib/alsa"
}
