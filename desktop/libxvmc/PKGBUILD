# Based upon the PKGBUILD from Arch Linux
pkgname=libxvmc
pkgver=1.0.7
pkgrel=1
pkgdesc="X11 Video Motion Compensation extension library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxv')
makedepends=('pkg-config')
options=('!libtool')
source=(${url}/releases/individual/lib/libXvMC-${pkgver}.tar.bz2)
sha1sums=('b8ee482514dfdf226694c830bdf292cc14e1cb91')

build() {
  cd "${srcdir}"/libXvMC-${pkgver}
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --disable-static
  make
}

package() {
  cd "${srcdir}"/libXvMC-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
