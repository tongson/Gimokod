# Based upon the PKGBUILD from Arch Linux
pkgname=xcursor-vanilla-dmz
pkgver=0.4.3
pkgrel=1
pkgdesc="Vanilla DMZ cursor theme"
arch=('any')
url="http://jimmac.musichall.cz/themes.php?skin=7"
license=('MIT')
md5sums=('412256b1b8a65dd05b46f65b32e2f5e6')
makedepends=('xorg-xcursorgen')
source=(http://ftp.de.debian.org/debian/pool/main/d/dmz-cursor-theme/dmz-cursor-theme_${pkgver}.tar.gz)

build() {
  cd "${srcdir}/dmz-cursor-theme-${pkgver}"
  cd DMZ-White/pngs
  ./make.sh
}

package() {
  cd "${srcdir}/dmz-cursor-theme-${pkgver}"
  install -d "${pkgdir}"/usr/share/icons/Vanilla-DMZ/cursors
  cp -a DMZ-White/xcursors/* "${pkgdir}"/usr/share/icons/Vanilla-DMZ/cursors
  install -Dm644 DMZ-White/index.theme "${pkgdir}"/usr/share/icons/Vanilla-DMZ/index.theme
}
