# Based upon the PKGBUILD from Arch Linux
pkgname=pango
pkgver=1.28.4
pkgrel=1
pkgdesc="A library for layout and rendering of text"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('glib' 'cairo' 'libxft' 'freetype2')
makedepends=('libxt')
options=('!libtool' '!emptydirs')
install=pango.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/1.28/${pkgname}-${pkgver}.tar.bz2)
url="http://www.pango.org/"
sha256sums=('7eb035bcc10dd01569a214d5e2bc3437de95d9ac1cfa9f50035a687c45f05a9f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --with-included-modules=basic-fc \
              --disable-gtk-doc-html \
              --enable-introspection=no \
              --enable-debug=no
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
