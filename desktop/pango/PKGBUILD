# Based upon the PKGBUILD from Arch Linux
pkgname=pango
pkgver=1.30.0
pkgrel=1
pkgdesc="A library for layout and rendering of text"
arch=('x86_64')
license=('LGPL')
depends=('glib' 'cairo' 'libxft' 'freetype')
makedepends=('libxt')
options=('!libtool' '!emptydirs')
install=pango.install
url="http://www.pango.org/"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('7c6d2ab024affaed0e942f9279b818235f9c6a36d9fc50688f48d387f4102dff')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
 ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --with-included-modules= \
              --disable-dependency-tracking \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --disable-gtk-doc-html \
              --enable-introspection=no \
              --enable-debug=no
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
