# Based upon the PKGBUILD from Arch Linux
pkgname=pango
pkgver=1.29.4
pkgrel=2
pkgdesc="A library for layout and rendering of text"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('glib' 'cairo' 'libxft' 'freetype')
makedepends=('libxt')
options=('!libtool' '!emptydirs')
install=pango.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/1.29/${pkgname}-${pkgver}.tar.bz2)
url="http://www.pango.org/"
sha256sums=('f15deecaecf1e9dcb7db0e4947d12b5bcff112586434f8d30a5afd750747ff2b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --with-x \
              --x-includes=/usr/include \
              --x-libraries=/usr/lib \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --with-included-modules=basic-fc \
              --disable-gtk-doc-html \
              --enable-introspection=no \
              --enable-debug=no
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
