# Based upon the PKGBUILD from Arch Linux
pkgname=libxaw
pkgver=1.0.11
pkgrel=1
pkgdesc="X11 Athena Widget library"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxmu' 'libxpm')
makedepends=('xorg-util-macros')
options=('!libtool')
source=(${url}/releases/individual/lib/libXaw-${pkgver}.tar.bz2)
sha256sums=('87a874acbb71d1ee59b8047312fc91d89dcb373e2970f121184cdae29a3d9492')

build() {
  cd "${srcdir}/libXaw-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --disable-static \
              --without-fop \
              --without-xmlto \
              --disable-specs
  make
}

package() {
  cd "${srcdir}/libXaw-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
