pkgname=attr
pkgver=2.4.46
pkgrel=2
pkgdesc="Extended attribute support library for ACL support"
arch=('x86_64')
url="http://savannah.nongnu.org/projects/attr"
license=('LGPL')
groups=('base')
options=('!libtool')
build() {
  cd "${pkgname}-${pkgver}"
  unset PLATFORM
  export OPTIMIZER="${CFLAGS}"
  export DEBUG=-DNDEBUG
  export INSTALL_USER=root INSTALL_GROUP=root
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --libdir=/usr/lib \
              --libexecdir=/usr/lib \
              --disable-gettext \
              --enable-gettext=no
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DIST_ROOT="${pkgdir}" install install-lib install-dev
  rm -f "${pkgdir}"/usr/lib/libattr.a
  chmod 0755 "${pkgdir}"/usr/lib/libattr.so.*.*.*
  rm -rf "${pkgdir}"/usr/share/man/man2
}

source=(http://download.savannah.gnu.org/releases/${pkgname}/${pkgname}-${pkgver}.src.tar.gz)

sha256sums=('dcd69bdca7ff166bc45141eddbcf21967999a6b66b0544be12a1cc2fd6340e1f')


