pkgbase=lvm2
pkgname=('lvm2' 'device-mapper')
pkgver=2.02.95
pkgrel=1
arch=('x86_64')
url="http://sourceware.org/lvm2/"
license=('GPL2' 'LGPL2')
groups=('base')
source=(ftp://sources.redhat.com/pub/lvm2/LVM2.${pkgver}.tgz)
sha256sums=('0335af36a151dafe5778da55a684546bfb98e7d3193834915b5efe95dbed9125')

build() {
  cd "${srcdir}/LVM2.${pkgver}"
  cd udev
  sed -i -e 's:/usr/sbin:/usr/bin:g' -e 's:/sbin:/usr/bin:g' * >/dev/null 2>&1
  cd ..
  Patch lvm2-2.02.63-always-make-static-libdm.patch
  Patch lvm2-2.02.92-locale-muck.patch
  Patch lvm2-2.02.92-dynamic-static-ldflags.patch
  autoreconf -v
  ./configure --prefix= \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --libdir=/usr/lib \
              --sbindir=/usr/bin \
              --datarootdir=/usr/share \
              --includedir=/usr/include \
              --with-usrlibdir=/usr/lib \
              --enable-pkgconfig \
              --enable-static_link \
              --enable-lvmetad \
              --with-thin=internal \
              --disable-readline \
              --disable-nls \
              --disable-selinux \
              --disable-valgrind-pool \
              --disable-debug \
              --disable-testing \
              --disable-profiling \
              --enable-cmdlib \
              --enable-applib \
              --with-udevdir=/usr/lib/udev/rules.d/ \
              --with-default-locking-dir=/run \
              --enable-udev_sync \
              --enable-udev_rules
  make
}

package_device-mapper() {
  pkgdesc="Device mapper userspace library and tools"
  url="http://sourceware.org/dm/"
  depends=('udev')

  cd "${srcdir}/LVM2.${pkgver}"
  make DESTDIR="${pkgdir}" install_device-mapper
  install -d -m755 "${pkgdir}/usr/bin"
  mv "${pkgdir}"/sbin/* "${pkgdir}"/usr/bin
  rmdir "${pkgdir}"/sbin
}

package_lvm2() {
  pkgdesc="Logical Volume Manager 2 utilities"
  depends=("device-mapper>=${pkgver}" 'udev')
  install=lvm2.install
  options=('!makeflags')

  cd "${srcdir}/LVM2.${pkgver}"
  make DESTDIR="${pkgdir}" install_lvm2
  make -C liblvm DESTDIR="${pkgdir}" install
  install -d "${pkgdir}"/etc/lvm/{archive,backup}
  install -d -m755 "${pkgdir}/usr/bin"
  mv "${pkgdir}"/sbin/* "${pkgdir}"/usr/bin
  rmdir "${pkgdir}"/sbin

}

source=(${source[@]-}
        lvm2-2.02.63-always-make-static-libdm.patch
        lvm2-2.02.92-dynamic-static-ldflags.patch
        lvm2-2.02.92-locale-muck.patch)

sha256sums=(${sha256sums[@]-}
            '1c498b5efce77ed16bbbfcd9e0ec6da404a9c70c7ad0959ed7b60733adfcfc21'
            '90b880572186d4d1abf049d39e5f11e4cfedb3707c29eeb9740ceccdb3b62468'
            '3b24abd3c1254de1727a91b6ade26c36dc8dbee8ecbfab4bf6afbad21fdd59ff')
