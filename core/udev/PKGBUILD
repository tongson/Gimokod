# Based upon the PKGBUILD from Arch Linux
pkgname=udev
pkgver=174
pkgrel=1
arch=(i686 x86_64)
url="http://www.kernel.org/pub/linux/utils/kernel/hotplug/udev.html"
license=('GPL')
groups=('base')
options=('!makeflags' '!libtool')
backup=(etc/udev/udev.conf)
depends=('util-linux' 'libusb-compat' 'glib' 'pciutils')
makedepends=('gperf')
source=(http://people.freedesktop.org/~kay/$pkgname-$pkgver.tar.bz2
        udev-173-no-docs.patch)

md5sums=('3b84706046a33d7a7e00491a1136804d'
         '9033a9ea911f471b3278dbaf288cd0b3')

build() {
  cd "${srcdir}/$pkgbase-${pkgver}"
  patch -Np1 -i "${srcdir}"/udev-173-no-docs.patch
  ./configure --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --with-rootlibdir=/lib \
              --libexecdir=/lib/udev \
              --sbindir=/sbin \
              --with-systemdsystemunitdir=/lib/systemd/system \
              --enable-gtk-doc-html=no \
              --disable-rule-generator \
              --disable-udev_acl \
              --enable-logging \
              --enable-static \
              --disable-hwdb \
              --disable-gudev \
              --disable-introspection \
              --disable-keymap \
              --disable-floppy \
              --disable-edd
  make
}

package() {
  cd "${srcdir}/$pkgbase-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Replace dialout/tape/cdrom group in rules with uucp/storage/optical group
  for i in "${pkgdir}"/lib/udev/rules.d/*.rules; do
    sed -i -e 's#GROUP="dialout"#GROUP="uucp"#g;
               s#GROUP="tape"#GROUP="storage"#g;
               s#GROUP="cdrom"#GROUP="optical"#g' $i
  done
}

