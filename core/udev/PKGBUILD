# Based upon the PKGBUILD from Arch Linux
pkgname=udev
pkgver=181
pkgrel=1
arch=('i686' 'x86_64')
url="http://git.kernel.org/?p=linux/hotplug/udev.git;a=summary"
license=('GPL')
groups=('base')
options=('!makeflags' '!libtool')
install=udev.install
depends=('util-linux' 'kmod')
makedepends=('gperf' 'libxslt' 'usbutils' 'kmod' 'glib')
source=(ftp://ftp.kernel.org/pub/linux/utils/kernel/hotplug/$pkgname-$pkgver.tar.xz
        udev-180-nodocs.diff)
md5sums=('0d7af750702620a871b9f9b98d8ad859'
         '6e8f82d61287f2a46116a29239f0bcc8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  Patch udev-180-nodocs.diff
  cd rules/rules.d
  sed -i 's:/sbin:/usr/bin:g' * >/dev/null 2>&1
  sed -i '5s:modprobe -bv:echo:' 80-drivers.rules
  cd ../..
  ./configure --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --with-rootlibdir=/usr/lib \
              --libexecdir=/usr/lib \
              --sbindir=/usr/bin \
              --with-systemdsystemunitdir=/usr/lib/systemd/system \
              --enable-gtk-doc-html=no \
              --disable-rule-generator \
              --disable-udev_acl \
              --enable-logging \
              --enable-static \
              --disable-gudev \
              --disable-introspection \
              --disable-keymap \
              --disable-floppy \
              --disable-edd
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  for i in "${pkgdir}"/usr/lib/udev/rules.d/*.rules; do
    sed -i -e 's#GROUP="dialout"#GROUP="uucp"#g;
               s#GROUP="tape"#GROUP="storage"#g;
               s#GROUP="cdrom"#GROUP="optical"#g' $i
  done
}

