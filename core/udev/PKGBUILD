# Based upon the PKGBUILD from Arch Linux
pkgname=udev
pkgver=182
pkgrel=2
arch=('x86_64')
url="http://git.kernel.org/?p=linux/hotplug/udev.git;a=summary"
license=('GPL')
groups=('base')
options=('!makeflags' '!libtool')
install=udev.install
depends=('util-linux' 'kmod')
makedepends=('gperf' 'libxslt' 'usbutils')
source=(ftp://ftp.kernel.org/pub/linux/utils/kernel/hotplug/$pkgname-$pkgver.tar.xz
        0001-reinstate-TIMEOUT-handling.patch)
md5sums=('023877e6cc0d907994b8c648beab542b'
         '94b816896bf23275c0598fc8e07270c3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  Patch 0001-reinstate-TIMEOUT-handling.patch
  sed 's/^\(.*\SUBDIRS .*\=.*\)src\/docs\(.*\)$/\1\2/' -i $(find . -name Makefile.am -o -name Makefile.in)
  sed 's/^\(.*\SUBDIRS .*\=.*\)src\/gudev\/docs\(.*\)$/\1\2/' -i $(find . -name Makefile.am -o -name Makefile.in)
  pushd rules
    sed -i 's:/sbin:/usr/bin:g' * >/dev/null 2>&1
    sed -i '5s:modprobe -bv:echo:' 80-drivers.rules
    grep -v floppy 50-udev-default.rules > 50-udev-default.rules.tmp
    mv 50-udev-default.rules.tmp 50-udev-default.rules
  popd
  ./configure --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --with-rootlibdir=/usr/lib \
              --libexecdir=/usr/lib \
              --sbindir=/usr/bin \
              --with-systemdsystemunitdir=/usr/lib/systemd/system \
              --enable-gtk-doc-html=no \
              --disable-rule-generator \
              --disable-udev_acl \
              --enable-logging \
              --enable-static \
              --disable-gudev \
              --disable-introspection \
              --disable-keymap \
              --disable-floppy \
              --disable-edd
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  for i in "${pkgdir}"/usr/lib/udev/rules.d/*.rules; do
    sed -i -e 's#GROUP="dialout"#GROUP="uucp"#g;
               s#GROUP="tape"#GROUP="storage"#g;
               s#GROUP="cdrom"#GROUP="optical"#g' $i
  done
}

