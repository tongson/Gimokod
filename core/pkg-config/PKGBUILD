# Based upon the PKGBUILD from Arch Linux
pkgname=pkg-config
pkgver=0.26
pkgrel=1
pkgdesc="A system for managing library compile/link flags"
arch=('i686' 'x86_64')
url="http://pkgconfig.freedesktop.org/wiki/"
license=('GPL')
groups=('base-devel')
depends=('popt' 'glib')
makedepends=('popt' 'glib')
provides=("pkgconfig=${pkgver}")
conflicts=('pkgconfig')
replaces=('pkgconfig')
source=(http://pkgconfig.freedesktop.org/releases/${pkgname}-${pkgver}.tar.gz)
md5sums=('47525c26a9ba7ba14bf85e01509a7234')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Use system popt
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --with-installed-popt
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
