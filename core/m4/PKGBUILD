# Based upon the PKGBUILD from Arch Linux
pkgname=m4
pkgver=1.4.16
pkgrel=1
pkgdesc="The GNU macro processor"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/m4"
license=('GPL3')
groups=('base-devel')
source=(ftp://ftp.gnu.org/gnu/m4/${pkgname}-${pkgver}.tar.xz)
md5sums=('7548ec061a1ba993790159764f522d0e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST}
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's:diff -c:diff:' tests/test-version-etc.sh  # busybox diff
  sed -i '41s:ENOENT:EINVAL:' tests/test-readlink.h   # changed in 2.6.39
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix="${pkgdir}"/usr install
}
