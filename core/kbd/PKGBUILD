# Based upon the PKGBUILD from Arch Linux
pkgname=kbd
pkgver=1.15.3
pkgrel=3
pkgdesc="Keytable files and keyboard utilities"
arch=('i686' 'x86_64')
url="ftp://ftp.altlinux.org/pub/people/legion/kbd/"
license=('GPL')
groups=('base')
source=(ftp://ftp.altlinux.org/pub/people/legion/kbd/${pkgname}-${pkgver}.tar.gz)
md5sums=('8143e179a0f3c25646ce5085e8777200')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # rename keymap files with the same names
  # this is needed because when only name of keymap is specified
  # loadkeys loads the first keymap it can find, which is bad (see FS#13837)
  # this should be removed when upstream adopts the change
  mv data/keymaps/i386/qwertz/cz{,-qwertz}.map
  mv data/keymaps/i386/olpc/es{,-olpc}.map
  mv data/keymaps/i386/olpc/pt{,-olpc}.map
  mv data/keymaps/i386/dvorak/no{,-dvorak}.map
  mv data/keymaps/i386/fgGIod/trf{,-fgGIod}.map

  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --datadir=/usr/share/kbd \
              --mandir=/usr/share/man \
              --disable-nls
  make KEYCODES_PROGS=yes RESIZECONS_PROGS=yes
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make KEYCODES_PROGS=yes RESIZECONS_PROGS=yes DESTDIR="${pkgdir}" install
}
