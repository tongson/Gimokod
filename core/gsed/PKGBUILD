# Based upon the PKGBUILD from Arch Linux
pkgname=gsed
_pkgname=sed
pkgver=4.2.1
pkgrel=1
pkgdesc="GNU stream editor"
arch=('x86_64')
url="http://www.gnu.org/software/sed"
license=('GPL3')
groups=('base-devel')
install=gsed.install
source=(ftp://ftp.gnu.org/pub/gnu/sed/${_pkgname}-${pkgver}.tar.gz{,.sig}
        sed-4.2.1-handle-incomplete-sequences-as-if-they-were-invalid.patch)
md5sums=('f0fd4d7da574d4707e442285fd2d3b86'
         '91871b227dbf0916e019ba1fb0fbfdf3'
         '357265390fb087daf7b436155ddb889b')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  Patch sed-4.2.1-handle-incomplete-sequences-as-if-they-were-invalid.patch
  ./configure --prefix=/usr \
              --program-prefix=g \
              --build=${CBUILD} \
              --host=${CHOST} \
              --bindir=/usr/bin \
              --disable-acl \
              --disable-nls \
              --with-included-regex
  make
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
