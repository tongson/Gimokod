# Based upon the PKGBUILD from Arch Linux
pkgname=zlib
pkgver=1.2.7
pkgrel=1
pkgdesc='A compression/decompression Library'
arch=('x86_64')
license=('custom')
groups=('base')
url="http://www.zlib.net/"
options=('!makeflags')
source=("http://zlib.net/current/zlib-${pkgver}.tar.gz")
md5sums=('60df6a37c56e7c1366cca812414f7b85')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # work around gcc bug; see https://bugs.archlinux.org/task/20647
  # export CFLAGS="${CFLAGS/-O2/-O3} -fno-tree-vectorize -DUNALIGNED_OK -fPIC -DPIC"
  ./configure --prefix=/usr \
              --libdir=/usr/lib \
              --shared
  make

  grep -A 24 '^  Copyright' zlib.h > LICENSE
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make install DESTDIR="${pkgdir}"
  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/zlib/LICENSE
}
