# Based upon the PKGBUILD from Arch Linux
pkgname=bison
pkgver=2.5
pkgrel=2
pkgdesc="The GNU general-purpose parser generator"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.gnu.org/software/bison/bison.html"
depends=('m4')
groups=('base-devel')
source=(ftp://ftp.gnu.org/gnu/bison/${pkgname}-${pkgver}.tar.bz2{,.sig}
        bison-2.5-undefined-reference.patch)
md5sums=('9dba20116b13fc61a0846b0058fbe004'
         '610b73db67bd4760209458efe7554ca3'
         '2015c5421b2ad99032154f27bf5c4772')
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # workaround for Arch FS#26794
  patch -Np1 -i $srcdir/bison-2.5-undefined-reference.patch
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --datadir=/usr/share \
              --disable-nls
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
