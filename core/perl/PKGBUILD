# Based upon the PKGBUILD from Arch Linux
pkgname=perl
pkgver=5.14.2
pkgrel=2
pkgdesc="A highly capable, feature-rich programming language"
arch=(i686 x86_64)
license=('GPL' 'PerlArtistic')
url="http://www.perl.org"
groups=('base-devel')
depends=('gdbm' 'db')
changelog=ChangeLog
source=(http://www.cpan.org/src/5.0/perl-${pkgver}.tar.bz2
        strippm.sh
        perlbin.sh
        perlbin.csh
        0001-Append-CFLAGS-and-LDFLAGS-to-their-Config.pm-counter.patch)
install=perl.install
options=('!makeflags' '!purge')
md5sums=('04a4c5d3c1f9f19d77daff8e8cd19a26'
         '74a952f1277f770d9c552a46ddf0fca0'
         '5ed2542fdb9a60682f215bd33701e61a'
         '1f0cbbee783e8a6d32f01be5118e0d5e'
         'c25d86206d649046538c3daab7874564')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mv pod/{perlpod,perl}.pod ..
  mv pod/perldiag.pod ..
  ../strippm.sh pod
  mv ../perl.pod pod/perl.pod
  mv ../perlpod.pod pod/perlpod.pod
  mv ../perldiag.pod pod/perldiag.pod

  export LC_ALL="C"

  if [ "${CARCH}" = "x86_64" ]; then
    # for x86_64
    arch_opts="-Dcccdlflags='-fPIC'"
  else
    # for i686
    arch_opts=""
  fi

  ./Configure -Dcc="/usr/bin/gcc" -des -Dusethreads -Duseshrplib -Doptimize="${CFLAGS}" -DDEBUGGING=none \
    -Dprefix=/usr -Dinstallprefix="${pkgdir}"/usr -Dvendorprefix=/usr \
    -Dprivlib=/usr/share/perl5/core_perl \
    -Darchlib=/usr/lib/perl5/core_perl \
    -Dsitelib=/usr/share/perl5/site_perl \
    -Dsitearch=/usr/lib/perl5/site_perl \
    -Dvendorlib=/usr/share/perl5/vendor_perl \
    -Dvendorarch=/usr/lib/perl5/vendor_perl \
    -Dscriptdir=/usr/bin/core_perl \
    -Dsitescript=/usr/bin/site_perl \
    -Dvendorscript=/usr/bin/vendor_perl \
    -Dinc_version_list=none \
    -Dman1ext=1perl -Dman3ext=3perl ${arch_opts} \
    -Dlddlflags="-shared ${LDFLAGS}" -Dldflags="${LDFLAGS}"
  patch -Np1 -i "${srcdir}"/0001-Append-CFLAGS-and-LDFLAGS-to-their-Config.pm-counter.patch
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make install

  ### Perl Settings ###
  # Change man page extensions for site and vendor module builds.
  # Use archlinux email address instead of my own.
  sed -e '/^man1ext=/ s/1perl/1p/' -e '/^man3ext=/ s/3perl/3pm/' \
      -e "/^cf_email=/ s/'.*'/'kevin@archlinux.org'/" \
      -e "/^perladmin=/ s/'.*'/'kevin@archlinux.org'/" \
      -i "${pkgdir}"/usr/lib/perl5/core_perl/Config_heavy.pl

  ### CPAN Settings ###
  # Set CPAN default config to use the site directories.
  sed -e '/(makepl_arg =>/   s/""/"INSTALLDIRS=site"/' \
      -e '/(mbuildpl_arg =>/ s/""/"installdirs=site"/' \
      -i "${pkgdir}"/usr/share/perl5/core_perl/CPAN/FirstTime.pm

  ### CPANPLUS Settings ###
  # Set CPANPLUS default config to use the site directories.
  sed -e "/{'makemakerflags'}/ s/'';/'INSTALLDIRS=site';/" \
      -e "/{'buildflags'}/     s/'';/'installdirs=site';/" \
      -i "${pkgdir}"/usr/share/perl5/core_perl/CPANPLUS/Config.pm

  # Profile script to set paths to perl scripts.
  install -D -m755 "${srcdir}"/perlbin.sh \
                   "${pkgdir}"/etc/profile.d/perlbin.sh
  # Profile script to set paths to perl scripts on csh. (FS#22441) 
  install -D -m755 "${srcdir}"/perlbin.csh \
                  "${pkgdir}"/etc/profile.d/perlbin.csh

  (cd "${pkgdir}"/usr/bin; mv perl${pkgver} perl)
  (cd "${pkgdir}"/usr/bin/core_perl;  ln -sf c2ph pstruct; ln -sf s2p psed)
  grep -rl "${pkgdir}" "${pkgdir}"/usr | \
      xargs sed -i "s^${pkgdir}^^g"

  # Remove all pod files *except* those under /usr/share/perl5/core_perl/pod/
  # (FS#16488)
  rm -f "${pkgdir}"/usr/share/perl5/core_perl/*.pod
  for d in "${pkgdir}"/usr/share/perl5/core_perl/*; do
    if [ -d $d -a $(basename $d) != "pod" ]; then
      find $d -name *.pod -delete
    fi
  done
  find "${pkgdir}"/usr/lib -name *.pod -delete
  find ${pkgdir} -name .packlist -delete
  # Add /usr/lib/perl5/core_perl/CORE/ to standard library path (FS#24660) 
  install -dv "${pkgdir}"/etc/ld.so.conf.d
  echo "/usr/lib/perl5/core_perl/CORE" > "${pkgdir}"/etc/ld.so.conf.d/perl.conf
}
