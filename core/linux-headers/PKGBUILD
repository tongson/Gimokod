# Based upon the PKGBUILD from Arch Linux
pkgname=linux-headers
pkgver=3.1
_basever=3.1
pkgrel=1
pkgdesc="Kernel headers sanitized for use in userspace"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/libc"
makedepends=('perl')
license=('GPL2')
source=(http://www.kernel.org/pub/linux/kernel/v3.x/linux-${_basever}.tar.{xz,sign})
sha256sums=('6f220258f3ce9f11d0e53860ccc2d9fe746117056212099876dbe5ee1de80af3'
            'c9a3e442b26b898c686496f3bd130880409138e25916584243ae612a8eb2cc3f')

build() {
  cd "${srcdir}"/linux-${_basever}
  [[ "${pkgver}" != $_basever ]] && patch -Np1 -i "${srcdir}"/patch-${pkgver}

  make mrproper
  make headers_check
}

package() {
  cd "${srcdir}"/linux-${_basever}
  make INSTALL_HDR_PATH="${pkgdir}"/usr headers_install

  # use headers from libdrm
  rm -rf "${pkgdir}"/usr/include/drm

  # clean-up unnecessary files generated during install
  find ${pkgdir} -name .install -or -name ..install.cmd | xargs rm -f
}
