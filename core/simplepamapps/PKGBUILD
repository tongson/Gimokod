# Based upon Openwall's 'SimplePAMApps.spec'
pkgname=simplepamapps
_realpkgname=SimplePAMApps
pkgver=0.60
pkgrel=3
pkgdesc="Simple PAM-based Applications"
arch=('i686' 'x86_64')
url="http://www.kernel.org/pub/linux/libs/pam/"
license=('GPL')
groups=('base')
depends=('pam' 'tcb')
install=simplepamapps.install
makedepends=('pam')

build() {
  cd "$srcdir/$_realpkgname-$pkgver"
  patch -Np1 -i ../SimplePAMApps-0.60-owl-alt-login.diff
  patch -Np1 -i ../SimplePAMApps-0.60-owl-passwd.diff
  patch -Np1 -i ../SimplePAMApps-0.60-owl-alt-su.diff
  patch -Np1 -i ../SimplePAMApps-0.60-owl-alt-login-su-ut_id.diff
  patch -Np1 -i ../SimplePAMApps-0.60-alt-owl-login-su-env.diff
  patch -Np1 -i ../SimplePAMApps-0.60-alt-login-su-strip-argv0.diff
  patch -Np1 -i ../SimplePAMApps-0.60-alt-owl-warnings.diff
  patch -Np1 -i ../SimplePAMApps-0.60-owl-log.diff
  patch -Np1 -i ../SimplePAMApps-0.60-owl-su-pam_acct_mgmt.diff
  ./configure --without-pniam --without-pwdb
  make
}

package() {
  cd "$srcdir/$_realpkgname-$pkgver"
  install -D -m 700 pamapps/login/login "${pkgdir}/usr/bin/login"
  install -D -m 4710 -o 0 -g 10 pamapps/su/su "${pkgdir}/usr/bin/su"
  install -D -m 2711 -o 0 -g 42 pamapps/passwd/passwd "${pkgdir}/usr/bin/passwd"

  mkdir -p "${pkgdir}/usr/share/man/man1"
  install -m 644 pamapps/{login/login.1,su/su.1,passwd/passwd.1} \
         "${pkgdir}/usr/share/man/man1"

  install -D -m 600 ../login "${pkgdir}/etc/pam.d/login"
  install -D -m 640 -o 0 -g 42 ../su "${pkgdir}/etc/pam.d/su"
  install -D -m 640 -o 0 -g 42 ../passwd "${pkgdir}/etc/pam.d/passwd"
}

source=(http://ftp.eu.openbsd.org/pub/linux/libs/pam/pre/applications/$_realpkgname-$pkgver.tar.bz2)
sha256sums=('4d026f06073be0a8b69d93a052ef78698ff14d31ac088e334201370a9fb742d7')

source=(${source[@]-}
        SimplePAMApps-0.60-alt-login-su-strip-argv0.diff
        SimplePAMApps-0.60-alt-owl-login-su-env.diff
        SimplePAMApps-0.60-alt-owl-warnings.diff
        SimplePAMApps-0.60-owl-alt-login-su-ut_id.diff
        SimplePAMApps-0.60-owl-alt-login.diff
        SimplePAMApps-0.60-owl-alt-su.diff
        SimplePAMApps-0.60-owl-log.diff
        SimplePAMApps-0.60-owl-passwd.diff
        SimplePAMApps-0.60-owl-su-pam_acct_mgmt.diff
        login
        passwd
        su)

sha256sums=(${sha256sums[@]-}
            'f87b4888f4622b2fed03f80ffa5d86aef6cb31aaaa80ab5f0baf9f15db71d8d6'
            '1e1af706db72715ab75c03beb9448f8303526c9d3fdfc91727dda9b84f8ee3ac'
            '2bd10113e72e1c78799f7371611275b6b7c79d132e92788790497fb1fd53f664'
            '6e93e14ef77434400bacef0a0a8bf9ccceef00628efb29467cc6d227c982d4fa'
            '2484f3b916a9f27e515c32715f23c18a4ed4802761fc5f3a0873bee90a89c793'
            '3eb6012f4e257e9ce8c4222c1ed49f55d9f20d144a24ce4173a7903d77ab9066'
            '9a230b5ef9a324cfb1b0d73aa7616f936c2a475561428478440abb74f4f31fcd'
            '71818ca204fb444198a9c65208580618079484cf6e80d4d40147fe8a632c7055'
            '0311af234856387e496d42c3915fdad5457001131e9dbaa993681cb3c7071884'
            '1a8f0ffb22995278970abd4f33fffe5f1e94159fd3acb29532aad5a0c9f50ddc'
            '29ac78e44aa33ded8f43b4ff6822adb2ce6481b40110640dc4393b206731a22f'
            '8c535a9cdbe8c0067d1a7b08e50e5cd6343473b58e428f75174ce051acd73fd7')
