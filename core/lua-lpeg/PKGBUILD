pkgname=lua-lpeg
_pkgname=lpeg
pkgver=0.10.2
pkgrel=2
pkgdesc="LPeg is a pattern-matching library for Lua, based on Parsing Expression Grammars"
arch=('x86_64')
url="http://www.inf.puc-rio.br/~roberto/lpeg/"
license=('MIT')
depends=('lua')
groups=('base')
source=("$url/$_pkgname-"${pkgver}".tar.gz")
sha256sums=('d1a7698e4bcd0ac305633774062d22b27300a41673c3d733ea9993244a64ea6f')

build() {
  cd "${_pkgname}-${pkgver}"
  cc ${CFLAGS} -ansi -DNDEBUG -fPIC -c -o lpeg.o lpeg.c
  cc ${CFLAGS} -ansi -DNDEBUG -fPIC -shared -o lpeg.so.${pkgver} lpeg.o
  ln -s lpeg.so.${pkgver} lpeg.so
}

check() {
  cd "${_pkgname}-${pkgver}"
  cp "${srcdir}/strict.lua" .
  sed -i -e '1s:lua5.1:lua:' test.lua
  sed -i -e '388s:loadstring:load:' test.lua
  /usr/bin/lua test.lua
}

package() {
  cd "${_pkgname}-${pkgver}"
  install -Dm0755 lpeg.so.${pkgver} "${pkgdir}$(pkg-config --variable INSTALL_CMOD lua)/lpeg.so.${pkgver}"
  install -Dm0644 re.lua "${pkgdir}$(pkg-config --variable INSTALL_LMOD lua)/re.lua"
  install -Dm0644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  pushd "${pkgdir}$(pkg-config --variable INSTALL_CMOD lua)"
    ln -s lpeg.so.${pkgver} lpeg.so
  popd
}

source=(${source[@]-}
        LICENSE
        strict.lua)

sha256sums=(${sha256sums[@]-}
            '6be7688c9c648e7c7cde693081be938c869a10aa4de09fe0a4c4c1d66b3cf03d'
            '09f5ab6efb88d4c0c5dd238ce427cc5bfcba09aeac5be5af6b4d6a34d79eb6cc')
