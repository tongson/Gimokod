# Based upon the PKGBUILD from Arch Linux
pkgname=iproute2
pkgver=3.2.0
pkgrel=3
pkgdesc="IP Routing Utilities"
arch=('x86_64')
license=('GPL2')
url="http://www.linux-foundation.org/en/Net:Iproute2"
groups=('base')
options=('!makeflags')
makedepends=('bison' 'flex' 'libnl')
install=iproute2.install
source=(http://www.kernel.org/pub/linux/utils/net/$pkgname/$pkgname-$pkgver.tar.xz
       remove-libnl-headers.patch
       fix-ip-l.patch
       iproute2-fhs.patch)
sha1sums=('1e217f22b0bbfc870ddf746de883ee375cd9e533'
          '1cb89ea0945fd190e6943fa7b1c3a4f254d0c1b3'
          '2ec5513c44f89046438d65e2cda1a014010e3b73'
          '78d6d2cb17b33ac89094aa3ed4810d81bb43c3d9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i '/^TARGETS=/s: arpd : :' misc/Makefile
  patch -Np1 -i "${srcdir}"/iproute2-fhs.patch
  Patch fix-ip-l.patch
  Patch remove-libnl-headers.patch
  ./configure --build=${CBUILD} --host=${CHOST}
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m0644 include/libnetlink.h "${pkgdir}"/usr/include/libnetlink.h
  install -D -m0644 lib/libnetlink.a "${pkgdir}"/usr/lib/libnetlink.a
}
