# Based upon the PKGBUILD from Arch Linux
pkgname=ggrep
_pkgname=grep
pkgver=2.11
pkgrel=1
pkgdesc="A string search utility"
arch=('x86_64')
license=('GPL3')
url="http://www.gnu.org/software/grep/grep.html"
groups=('base-devel')
install=ggrep.install
source=(ftp://ftp.gnu.org/gnu/"${_pkgname}"/"${_pkgname}"-"${pkgver}".tar.xz{,.sig}
        grep-2.11-exclude-dir-crash.patch)
md5sums=('ad9c6dbdeab93e50d2bc380f10ed3643'
         'e4fea5e355973c951f93f48bc86f92fa'
         'e882644cf8c1a4e37da7ba8c0867e70b')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}"/grep-2.11-exclude-dir-crash.patch
  pushd gnulib-tests
    sed -i 's:\$ERR:0:' test-version-etc.sh # minimal diff
  popd
  ./configure --prefix=/usr \
              --program-prefix=g \
              --build=${CBUILD} \
              --host=${CHOST} \
              --with-included-regex \
              --disable-nls \
              --disable-perl-regexp
  make
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
