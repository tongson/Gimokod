# Based upon the PKGBUILD from Arch Linux
pkgname=ggrep
_pkgname=grep
pkgver=2.12
pkgrel=1
pkgdesc="A string search utility"
arch=('x86_64')
license=('GPL3')
url="http://www.gnu.org/software/grep/grep.html"
groups=('base-devel')
checkdepends=('diffutils')
install=ggrep.install
source=(ftp://ftp.gnu.org/gnu/$_pkgname/$_pkgname-$pkgver.tar.xz{,.sig})
md5sums=('8d2f0346d08b13c18afb81f0e8aa1e2f'
         '7a25f2ece647b8637f4ae1de474712e7')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --program-prefix=g \
              --build=${CBUILD} \
              --host=${CHOST} \
              --with-included-regex \
              --disable-nls \
              --disable-perl-regexp
  make
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
