# Based upon the PKGBUILD from Arch Linux
pkgname=guile
pkgver=1.8.8
pkgrel=1
pkgdesc="a portable, embeddable Scheme implementation written in C"
url="http://www.gnu.org/software/guile/"
arch=(i686 x86_64)
license=('GPL')
groups=('base-devel')
depends=('gmp' 'libtool' 'ncurses')
source=(ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz)
options=('!libtool')
md5sums=('18661a8fdfef13e2fcb7651720aa53f3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i '360s:doc::' Makefile.in
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --disable-static  \
              --disable-networking \
              --disable-error-on-warning \
              --enable-posix \
              --enable-regex \
              --disable-elisp \
              --disable-nls \
              --disable-rpath \
              --disable-debug-freelist \
              --disable-debug-malloc \
              --disable-guile-debug \
              EMACS=no \
              have_makeinfo=no
  make LDFLAGS+="-lpthread"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
