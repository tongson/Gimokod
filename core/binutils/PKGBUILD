# Based upon the PKGBUILD from Arch Linux. Debian and Gentoo's patches
pkgname=binutils
pkgver=2.21.1
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
groups=('base-devel')
depends=('zlib')
options=('!libtool' '!distcc' '!ccache')
build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # Gentoo
  patch -Np0 -i ../08_all_binutils-RPATH_ENVVAR-smack.patch
  patch -Np0 -i ../20_all_ld-sysroot.patch
  patch -Np1 -i ../40_all_binutils-ld-gcc-4.6-tests.patch

  # Debian
  patch -Np1 -i ../128_build_id.patch
  patch -Np1 -i ../131_ld_bootstrap_testsuite.patch

  patch -Np1 -i ../pr12654.patch
  patch -Np1 -i ../pr12851a.patch
  patch -Np0 -i ../pr12851b.patch
  patch -Np1 -i ../pr12851c.patch
  patch -Np1 -i ../pr12975.patch
  patch -Np1 -i ../pr13195.patch
  patch -Np1 -i ../pr13201.patch
  patch -Np1 -i ../pr13233.patch
  patch -Np1 -i ../binutils-2.21.1-claimfile-fix.patch

  cd ${srcdir}
  mkdir binutils-build && cd binutils-build
  [[ $CARCH == "x86_64" ]] && CONFIGFLAG="--enable-64-bit-bfd --disable-multilib"
  CFLAGS=${CFLAGS/-Os/-O2}
  CXXFLAGS=${CXXFLAGS/-Os/-O2}
  ${srcdir}/${pkgname}-${pkgver}/configure --prefix=/usr \
                               --build ${CHOST} \
                               --disable-gold \
                               --enable-plugins \
                               --enable-threads \
                               --enable-shared \
                               --without-included-gettext \
                               --disable-nls $CONFIGFLAG

  make configure-host
  make tooldir="${pkgdir}"/usr
}

check() {
  cd "${srcdir}"/binutils-build
  # do not abort on errors - manually check log files
   make -k -j1 check || true
}

package() {
  cd "${srcdir}"/binutils-build
  make prefix="${pkgdir}"/usr tooldir="${pkgdir}"/usr install

  # Add some useful headers
  install -m644 "${srcdir}"/${pkgname}-${pkgver}/include/libiberty.h "${pkgdir}"/usr/include
  install -m644 "${srcdir}"/${pkgname}-${pkgver}/include/demangle.h "${pkgdir}"/usr/include

  # Rebuild libiberty.a with -fPIC
  make -C libiberty clean
  make CFLAGS="$CFLAGS -fPIC" -C libiberty
  install -m644 libiberty/libiberty.a "${pkgdir}"/usr/lib

  # Rebuild libbfd.a with -fPIC
  make -C bfd clean
  # hidden visability prevent 3rd party shared libraries exporting bfd non-stable API
  make CFLAGS="$CFLAGS -fPIC -fvisibility=hidden" -C bfd
  install -m644 bfd/libbfd.a "${pkgdir}"/usr/lib

  # Remove Windows/Novell specific man pages
  rm -f "${pkgdir}"/usr/share/man/man1/{dlltool,nlmconv,windres,windmc}*

  # Remove these symlinks, they are not ABI stable.
  # Programs should compile static to the .a file.
  rm -f "${pkgdir}"/usr/lib/lib{bfd,opcodes}.so
  echo "INPUT ( /usr/lib/libbfd.a -liberty -lz )" >"${pkgdir}"/usr/lib/libbfd.so
  echo "INPUT ( /usr/lib/libopcodes.a -lbfd )" >"${pkgdir}"/usr/lib/libopcodes.so
}
source=(ftp://gcc.gnu.org/pub/binutils/snapshots/$pkgname-$pkgver.tar.bz2)
sha256sums=('cdecfa69f02aa7b05fbcdf678e33137151f361313b2f3e48aba925f64eabf654')

source=(${source[@]-}
        08_all_binutils-RPATH_ENVVAR-smack.patch
        128_build_id.patch
        131_ld_bootstrap_testsuite.patch
        20_all_ld-sysroot.patch
        40_all_binutils-ld-gcc-4.6-tests.patch
        binutils-2.21.1-claimfile-fix.patch
        pr12654.patch
        pr12851a.patch
        pr12851b.patch
        pr12851c.patch
        pr12975.patch
        pr13195.patch
        pr13201.patch
        pr13233.patch)

sha256sums=(${sha256sums[@]-}
            'e6ad9a2161a635fdcbeff180b5e36c76d283be80e9b8de7ae3c1e79335ba3680'
            '308ed294365f59077d53a3cd0d95a6df2f71ec4bdd87a659478ec071e16726cb'
            '854856477128b2bc3ae70db123aaa0e9cb5072d5f3ed26cf8014c40a2688310c'
            'b85eab89949d47314e10795256938198ae114b8c9a65cba18488790a2b3298a1'
            '55688e1aa077ca81d687859c3e0b3306c8495a1cdcf57138c25669ab766dc2fc'
            'e76c3e6df52edf5cd97f518d115d84b01525a8a0f40c826622b9ef086b06ea49'
            '59ae2528b805b94c26f06484e5059060ce7f5c26b2b88696af808dfa99421d60'
            '4565570ee901a60a547af445f85c88d631d6c54e20517a463ff5b1d6e338989f'
            '22c9ffe4af24f9863e599479a47549505ab2eea757c30ab72c6554bdfc3bfe77'
            '0ac9f4dd5888f3ba8d4b658ba11e9709691b4c571aa81ab438ac42b247296f82'
            'a4c67e646742d1f6068faa91ad36fb136e9271bffff365aa41646b4152a1f6ad'
            '5127df32122f849ad9957acc42627cb8c4472b152d00deaba67a1c65e462fe64'
            'ca9aba8c1804254a45f470cde24930964c6bc34dcb1af6a45bad6baa97d07080'
            '4de3477ccba1d31509446ac1ad1bf440aae1d185f11175b181f34e82d3a6028e')
