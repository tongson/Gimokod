# Based upon the PKGBUILD from Arch Linux. Debian and Gentoo's patches
pkgname=binutils
pkgver=2.22
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
groups=('base-devel')
depends=('zlib')
checkdepends=('dejagnu')
install=binutils.install
options=('!libtool' '!distcc' '!ccache')
build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Debian
  Patch 001_ld_makefile_patch.patch
  Patch 006_better_file_error.patch
  Patch 012_check_ldrunpath_length.patch
  Patch 013_bash_in_ld_testsuite.patch
  Patch 128_build_id.patch
  Patch 131_ld_bootstrap_testsuite.patch
  Patch 157_ar_scripts_with_tilde.patch
  Patch 167_pr13302.diff
  Patch 168_readelf_go.patch
  Patch pr13534-01.diff
  Patch pr13534-02.diff
  Patch pr13534-03.diff
  Patch pr13534-04.diff
  Patch pr13534-05.diff

  # Gentoo
  Patch 08_all_binutils-RPATH_ENVVAR-smack.patch
  Patch 20_all_ld-sysroot.patch

  Patch pr13817.diff

  cd ${srcdir}
  mkdir binutils-build && cd binutils-build
  CFLAGS=${CFLAGS/-Os/-O2}
  CXXFLAGS=${CXXFLAGS/-Os/-O2}
  ${srcdir}/${pkgname}-${pkgver}/configure --prefix=/usr \
                               --build ${CHOST} \
                               --disable-gold \
                               --enable-plugins \
                               --enable-threads \
                               --enable-shared \
                               --without-included-gettext \
                               --disable-nls \
                               --enable-64-bit-bfd \
                               --disable-multilib
  echo "MAKEINFO = :" >> Makefile
  make configure-host
  make tooldir="${pkgdir}"/usr

  # Rebuild libiberty.a with -fPIC
  cp -a libiberty libiberty-pic
  make -C libiberty-pic clean
  make CFLAGS="$CFLAGS -fPIC" -C libiberty-pic

  # Rebuild libbfd.a with -fPIC
  # hidden visability prevent 3rd party shared libraries exporting bfd non-stable API
  cp -a bfd bfd-pic
  make -C bfd-pic clean
  make CFLAGS="$CFLAGS -fPIC -fvisibility=hidden" -C bfd-pic

  # Rebuild libopcodes.a with -fPIC
  cp -a opcodes opcodes-pic
  make -C opcodes-pic clean
  make CFLAGS="$CFLAGS -fPIC" -C opcodes-pic
}

check() {
  cd "${srcdir}"/binutils-build
  make -k -j1 check || true
  # FAIL -> RUNTESTFLAGS=-v
}

package() {
  cd "${srcdir}"/binutils-build
  make prefix="${pkgdir}"/usr tooldir="${pkgdir}"/usr install

  # Add some useful headers
  install -m644 ${srcdir}/binutils-${pkgver}/include/libiberty.h ${pkgdir}/usr/include
  install -m644 ${srcdir}/binutils-${pkgver}/include/demangle.h ${pkgdir}/usr/include

  # install libraries rebuilt with -fPIC
  install -m644 libiberty-pic/libiberty.a ${pkgdir}/usr/lib
  install -m644 bfd-pic/libbfd.a ${pkgdir}/usr/lib
  install -m644 opcodes/libopcodes.a ${pkgdir}/usr/lib

  # Remove Windows/Novell specific man pages
  rm -f ${pkgdir}/usr/share/man/man1/{dlltool,nlmconv,windres,windmc}*

  # Remove these symlinks, they are not ABI stable.
  # Programs should compile static to the .a file.
  rm -f ${pkgdir}/usr/lib/lib{bfd,opcodes}.so
  echo "INPUT ( /usr/lib/libbfd.a -liberty -lz )" >${pkgdir}/usr/lib/libbfd.so
  echo "INPUT ( /usr/lib/libopcodes.a -lbfd )" >${pkgdir}/usr/lib/libopcodes.so

  mv "${pkgdir}/usr/bin/strings" "${pkgdir}/usr/bin/gstrings"
}
source=(http://ftp.gnu.org/gnu/binutils/$pkgname-$pkgver.tar.bz2)
sha256sums=('6c7af8ed1c8cf9b4b9d6e6fe09a3e1d3d479fe63984ba8b9b26bf356b6313ca9')

source=(${source[@]-}
        001_ld_makefile_patch.patch
        006_better_file_error.patch
        012_check_ldrunpath_length.patch
        013_bash_in_ld_testsuite.patch
        08_all_binutils-RPATH_ENVVAR-smack.patch
        128_build_id.patch
        131_ld_bootstrap_testsuite.patch
        157_ar_scripts_with_tilde.patch
        167_pr13302.diff
        168_readelf_go.patch
        20_all_ld-sysroot.patch
        pr13534-01.diff
        pr13534-02.diff
        pr13534-03.diff
        pr13534-04.diff
        pr13534-05.diff
        pr13817.diff)

sha256sums=(${sha256sums[@]-}
            'abb214d3e337e79e6009693ed12b2d5f3f8ce9e89706d158a78de1f2aabad79e'
            '9d874988e80a6941bc607722be9366a30fc7d55dbd2d2aada5354faa6dc37c74'
            'c600d0deebdfe4b5e6e94489c035c802caa7b84aeed238f0c0d90882ae8fff56'
            '75c4df149b23d7980c70c220b3f70cc01aa65e59c4e7eba91c9400fb27094ba7'
            'e6ad9a2161a635fdcbeff180b5e36c76d283be80e9b8de7ae3c1e79335ba3680'
            '308ed294365f59077d53a3cd0d95a6df2f71ec4bdd87a659478ec071e16726cb'
            '854856477128b2bc3ae70db123aaa0e9cb5072d5f3ed26cf8014c40a2688310c'
            'f4b57b9ebd3c46580e227db17d6cb27b3a250de2a46f362c93e736d3162d3729'
            '17fdb22f62eae42ee754873101d401bba465613962917a50f027ec92ffef41bc'
            'b7344bbd896a2868e6f814bf30b37c373b4ea785b3847f1d04a50a7fec5a60ab'
            'b85eab89949d47314e10795256938198ae114b8c9a65cba18488790a2b3298a1'
            '65a22a1e940ae39744729fa0a5fa4ec98ec20e2f723f261df6748656bd8ce8c1'
            'fc7e6daf35509b7b56da889878a938d73558fe3f6a76ce37882bc9892c8fbc6c'
            '7db9b9a4f5dbe66f76a94cdb91288a0a673b2478088120b125ab96a7b85302e9'
            'a1e665ad3db7583f620ee7ea8b221c87ef0176c3489c69676979d8b3dd88a545'
            '457d641ccd77da96706b8437205058d2f6313f4530a0aad04c493bfa4ab41865'
            '87fecd1b3e558e1b45d0759a20154ac43385b4c2e5dfe02522ecf1d9b7608d9d')
