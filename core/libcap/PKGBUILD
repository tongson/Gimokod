# Based upon the PKGBUILD from Arch Linux
pkgname=libcap
pkgver=2.22
pkgrel=1
pkgdesc="POSIX 1003.1e capabilities"
arch=('i686' 'x86_64')
url="http://sites.google.com/site/fullycapable/"
license=('GPL2')
groups=('base')
depends=('attr')
source=(ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('b4896816b626bea445f0b3849bdd4077')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export BUILD_CC=/usr/bin/gcc
  export CC=/usr/bin/gcc
  export AR=/usr/bin/ar
  export RANLIB=/usr/bin/ranlib
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix=/usr SBINDIR=${pkgdir}/usr/bin DESTDIR="${pkgdir}" LIBDIR="${pkgdir}"/usr/lib RAISE_SETFCAP=no install
  rm "${pkgdir}"/usr/lib/*.a
  chmod 755 "${pkgdir}"/usr/lib/libcap.so.${pkgver}
}
