# Based upon the PKGBUILD from Arch Linux
pkgname=sudo
_ver=1.8.4p4
pkgver=1.8.4.p4
pkgrel=2
pkgdesc="Give certain users the ability to run some commands as root"
arch=('i686' 'x86_64')
url="http://www.sudo.ws/sudo/"
license=('custom')
depends=('zlib' 'pam')
groups=('base')
install=sudo.install
backup=('etc/sudoers' 'etc/pam.d/sudo')
options=('!libtool' '!makeflags')
source=(ftp://ftp.sudo.ws/pub/sudo/$pkgname-$_ver.tar.gz{,.sig}
        sudo.pam)
md5sums=('b9be6df7ecefedff2263052ed9fc5e93'
         '24da0bf96506b81678726778aad9ea4c'
         '4e7ad4ec8f2fe6a40e12bcb2c0b256e3')

build() {
  cd "${srcdir}/${pkgname}-$_ver"

  ./configure --prefix=/usr \
              --sbindir=/usr/bin \
              --build=${CBUILD} \
              --host=${CHOST} \
              --enable-zlib=system \
              --with-pam \
              --libexecdir=/usr/lib \
              --with-env-editor \
              --disable-nls \
              --without-linux-audit \
              --with-logfac=auth
  make
}

check() {
  cd "$srcdir/$pkgname-$_ver"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-$_ver"
  make DESTDIR="${pkgdir}" install
  install -Dm640 -o 0 -g 42 "${srcdir}/sudo.pam" "${pkgdir}/etc/pam.d/sudo"
  install -Dm644 doc/LICENSE "${pkgdir}/usr/share/licenses/sudo/LICENSE"
}
