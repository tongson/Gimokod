# Based upon the PKGBUILD from Arch Linux and ebuild from Gentoo
pkgname=lua
pkgver=5.2.0
pkgmver=5.2
pkgrel=8
pkgdesc="A powerful light-weight programming language designed for extending applications"
arch=('x86_64')
url="http://www.lua.org/"
license=('MIT')
groups=('base')
options=('!makeflags' '!emptydirs')
source=(http://www.lua.org/ftp/${pkgname}-${pkgver}.tar.gz)
sha256sums=('cabe379465aa8e388988073d59b69e76ba0025429d2c1da80821a252cdf6be0d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  Patch 01_all_memory_hoarding.upstream.patch
  Patch 02_all_hex_number_handling.upstream.patch
  Patch lua-5.2-make.patch
  sed -i -e 's:-lreadline -lhistory -lncurses::' src/Makefile
  sed -i -e 's/\(LIB_VERSION = \)6:1:1/\17:0:2/' src/Makefile
  sed -i -e '103s:/usr/local:/usr:' src/luaconf.h
  sed -i -e '/#define LUA_USE_READLINE/d' src/luaconf.h
  cp "${srcdir}"/configure.in .
  aclocal
  libtoolize --copy --force --install
  aclocal
  autoconf
  ./configure --prefix=/usr \
              --host=${CHOST} \
              --build=${CBUILD}
  sed -i -e 's:\(-export-dynamic\):-static \1:' src/Makefile
  cd src
  make CFLAGS="-DLUA_USE_LINUX -fPIC ${CFLAGS}" \
       SYSLDFLAGS="${LDFLAGS}" \
       RPATH="/usr/lib" \
       LIB_LIBS="-lpthread -ldl -lm" \
       V=${pkgver} \
       gentoo_all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make INSTALL_TOP="${pkgdir}"/usr INSTALL_LIB="${pkgdir}"/usr/lib \
                  V=${pkgver} gentoo_install
  install -D -m644 "${srcdir}"/lua.pc "${pkgdir}/usr/lib/pkgconfig/lua.pc"
  sed -i -e "s:^V=.*:V= ${pkgmver}:" \
         -e "s:^R=.*:R= ${pkgver}:" \
         -e "s:/,lib,:/lib:g"  "${pkgdir}/usr/lib/pkgconfig/lua.pc"
  install -D -m644 "${srcdir}"/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Install the documentation
  install -d "${pkgdir}/usr/share/doc/lua"
  install -m0644 doc/*.{gif,png,css,html} "${pkgdir}/usr/share/doc/lua"
  install -D -m0644 doc/lua.1 "${pkgdir}"/usr/share/man/man1/lua.1
  install -D -m0644 doc/luac.1 "{$pkgdir}"/usr/share/man/man1/luac.1
}

source=(${source[@]-}
        01_all_memory_hoarding.upstream.patch
        02_all_hex_number_handling.upstream.patch
        LICENSE
        configure.in
        lua-5.2-make.patch
        lua.pc)

sha256sums=(${sha256sums[@]-}
            '1794d833097afb18c6ec4e0ed06a6a03b58dfcc4a56f0fb37b2c9fbc03754531'
            '608bf0ccbfc1a0b86e974bd56b0abc836eba7bb5eff412cd4288e3d0ff692e88'
            '272c07b0d7b79bf2f9dd4625b3b162a494cc4269eeb30b085eef623f6f18b7af'
            '1b4477324c94c0793df2117e80bd325bb2cc1cf6be16658151b272d4aca023c6'
            '384e6c9fad1c0677868afc69115e0c0f2b6314bc3956a39a66e10e980864c917'
            'ff7ae80c41698f27f4d1168c4cb2931d7b232e66528d9a2b0939723f4bb00145')
