# Based upon the PKGBUILD from Arch Linux
pkgname=libassuan
pkgver=2.0.3
pkgrel=1
pkgdesc="A IPC library used by some GnuPG related software"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnupg.org/related_software/libassuan"
groups=('base')
depends=('libgpg-error')
options=('!libtool')
source=(ftp://ftp.gnupg.org/gcrypt/"${pkgname}"/"${pkgname}"-"${pkgver}".tar.bz2)
md5sums=('179d1918325fdb928c7bd90b8a514fc7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST}
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
