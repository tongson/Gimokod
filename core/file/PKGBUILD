# Based upon the PKGBUILD from Arch Linux
pkgname=file
pkgver=5.11
pkgrel=1
pkgdesc="File type identification utility"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.darwinsys.com/file/"
depends=('zlib')
groups=('base-devel')
options=('!libtool')
source=(ftp://ftp.astron.com/pub/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('16a407bd66d6c7a832f3a5c0d609c27b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's#\$(datadir)/misc#\$(datadir)#' configure
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --datadir=/usr/share/file
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}"/usr/share/misc
  ln -s ../file/magic.mgc "${pkgdir}"/usr/share/misc
  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING
}
