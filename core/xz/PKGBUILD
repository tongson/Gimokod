# Based upon the PKGBUILD from Arch Linux
pkgname=xz
pkgver=5.0.3
pkgrel=1
pkgdesc='Library and command line tools for XZ and LZMA compressed files'
arch=('i686' 'x86_64')
url='http://tukaani.org/xz/'
license=('GPL' 'LGPL' 'custom')
groups=('base')
depends=
provides=('lzma' 'lzma-utils' 'xz-utils')
replaces=('lzma' 'lzma-utils' 'xz-utils')
conflicts=('lzma' 'lzma-utils' 'xz-utils')
options=('!libtool')
source=("http://tukaani.org/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('fefe52f9ecd521de2a8ce38c21a27574')

PKGEXT='.pkg.tar.gz'

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --disable-rpath \
              --disable-nls \
              --enable-threads \
              --enable-werror

  sed -i 's|^hardcode_libdir_flag_spec=.*|hardcode_libdir_flag_spec=""|g' libtool
  sed -i 's|^runpath_var=LD_RUN_PATH|runpath_var=DIE_RPATH_DIE|g' libtool

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}"/usr/share/licenses/xz/
  ln -sf /usr/share/doc/xz/COPYING "${pkgdir}"/usr/share/licenses/xz/
  ln -sf /usr/share/licenses/common/GPL2/license.txt "${pkgdir}"/usr/share/doc/xz/COPYING.GPLv2
}
