# Based upon the PKGBUILD from Arch Linux
pkgname=less
pkgver=444
pkgrel=1
pkgdesc="A terminal based program for viewing text files"
license=('GPL3')
arch=('i686' 'x86_64')
url="http://www.greenwoodsoftware.com/less"
groups=('base')
depends=('ncurses')
source=(http://www.greenwoodsoftware.com/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('56f9f76ffe13f70155f47f6b3c87d421')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --build=${CHOST} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --with-regex=posix
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make prefix="${pkgdir}"/usr install
  install -dm755 "${pkgdir}"/bin
  mv "${pkgdir}"/usr/bin/${pkgname} "${pkgdir}"/bin
}
