# Based upon the PKGBUILD from Arch Linux
pkgname=popt
pkgver=1.16
pkgrel=1
pkgdesc="A commandline option parser"
arch=('i686' 'x86_64')
url="http://rpm5.org"
license=('custom')
groups=('base')
options=('!libtool')
source=(http://rpm5.org/files/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('3743beefa3dd6247a73f8f7a32c14c33')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --disable-nls
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # move libs in /lib
  install -Dm755 "${pkgdir}"/usr/lib/libpopt.so.0.0.0 \
    "${pkgdir}"/lib/libpopt.so.0.0.0

  rm "${pkgdir}"/usr/lib/libpopt.so*
  ln -sf /lib/libpopt.so "${pkgdir}"/usr/lib/libpopt.so
  ln -sf libpopt.so.0 "${pkgdir}"/lib/libpopt.so
  ln -sf libpopt.so.0.0.0 "${pkgdir}"/lib/libpopt.so.0

  # install license
  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
