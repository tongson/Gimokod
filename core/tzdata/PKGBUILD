# Based upon the PKGBUILD from Arch Linux
pkgname=tzdata
pkgver=2011m
pkgrel=1
_tzcode=2011i
_tzdata=2011m
pkgdesc="Sources for time zone and daylight saving time data"
arch=('i686' 'x86_64')
url="http://www.twinsun.com/tz/tz-link.htm"
license=('GPL')
groups=('base')
options=('!emptydirs')
source=(ftp://elsie.nci.nih.gov/pub/tzcode${_tzcode}.tar.gz
        ftp://munnari.oz.au/pub/${pkgname}${_tzdata}.tar.gz
        Makefile.patch)
md5sums=('cf7f4335b7c8682899fa2814e711c1b2'
         '6dc4455b62c951dcf367a239ca249e69'
         'a64ed97d1fc03c66ee8612c0d9f40507')

build() {
  cd ${srcdir}

  tar -xf tzcode${_tzcode}.tar.gz
  tar -xf ${pkgname}${_tzdata}.tar.gz

  patch -Np1 -i "${srcdir}/Makefile.patch"

  export CC=/usr/bin/gcc
  export NLS=0
  make
}

check() {
  cd ${srcdir}
  make -k check || return 0
}

package() {
  cd ${srcdir}

  make DESTDIR="${pkgdir}" install
  rm "${pkgdir}/usr/share/zoneinfo/localtime"
}
