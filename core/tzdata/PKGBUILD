# Based upon the PKGBUILD from Arch Linux
pkgname=tzdata
pkgver=2011n
pkgrel=1
_tzcode=2011i
_tzdata=2011n
pkgdesc="Sources for time zone and daylight saving time data"
arch=('i686' 'x86_64')
url="http://www.iana.org/time-zones"
license=('GPL')
groups=('base')
options=('!emptydirs')
source=(http://www.iana.org/time-zones/repository/releases/tzcode${_tzcode}.tar.gz
        http://www.iana.org/time-zones/repository/releases/${pkgname}${_tzdata}.tar.gz
        Makefile.patch)
md5sums=('cf7f4335b7c8682899fa2814e711c1b2'
         '20dbfb28efa008ddbf6dd34601ea40fa'
         'a64ed97d1fc03c66ee8612c0d9f40507')

build() {
  cd ${srcdir}

  tar -xf tzcode${_tzcode}.tar.gz
  tar -xf ${pkgname}${_tzdata}.tar.gz

  patch -Np1 -i "${srcdir}/Makefile.patch"

  export CC=/usr/bin/gcc
  export NLS=0
  make
}

check() {
  cd ${srcdir}
  make -k check || return 0
}

package() {
  cd ${srcdir}

  make DESTDIR="${pkgdir}" install
  rm "${pkgdir}/usr/share/zoneinfo/localtime"
  mv "${pkgdir}"/usr/sbin/* "${pkgdir}"/usr/bin
  rmdir "${pkgdir}"/usr/sbin
}
