# Based upon the PKGBUILD from Arch Linux
pkgname=gawk
pkgver=4.0.1
pkgrel=1
pkgdesc="GNU version of awk"
arch=('x86_64')
url="http://www.gnu.org/directory/GNU/gawk.html"
license=('GPL')
groups=('base-devel')
install=gawk.install
source=(ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig})
md5sums=('bab2bda483e9f32be65b43b8dab39fa5'
         '7cf4e4896509c655dd00ecd4ca9098ef')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
              --build=${CHOST} \
              --host=${CHOST} \
              --libexecdir=/usr/lib \
              --disable-nls
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  unlink "${pkgdir}"/usr/bin/awk
}
