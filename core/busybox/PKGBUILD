pkgname=busybox
pkgver=1.19.4
pkgrel=5
pkgdesc='Utilities for rescue, embedded and minimal systems'
arch=('x86_64')
url='http://busybox.net'
license=('GPL')
options=('emptydirs')
install=busybox.install
provides=('wget' 'sed' 'diffutils' 'findutils' 'awk' 'gzip' 'unzip' 'tar' 'which' 'sharutils'
          'sh' 'run-parts' 'vi' 'procps' 'cpio' 'cron' 'eject' 'runit' 'psmisc' 'bridge-utils'
          'iputils' 'net-tools' 'inetutils' 'man-db' 'logger' 'ntp' 'dhcpcd' 'grep' 'ifenslave')
groups=('base')

symlinks=(/usr/bin/uuencode
          /usr/bin/uudecode
          /usr/bin/smemcap
          /usr/bin/inotifyd
          /usr/bin/run-parts
          /usr/bin/crond
          /usr/bin/eject
          /usr/bin/volname
          /usr/bin/chpst
          /usr/bin/runsv
          /usr/bin/runsvdir
          /usr/bin/sv
          /usr/bin/svlogd
          /usr/bin/setuidgid
          /usr/bin/envuidgid
          /usr/bin/envdir
          /usr/bin/softlimit
          /usr/bin/gunzip
          /usr/bin/uncompress
          /usr/bin/gzip
          /usr/bin/unzip
          /usr/bin/which
          /usr/bin/kill
          /usr/bin/ps
          /usr/bin/sysctl
          /usr/bin/free
          /usr/bin/pgrep
          /usr/bin/pkill
          /usr/bin/pmap
          /usr/bin/pwdx
          /usr/bin/top
          /usr/bin/uptime
          /usr/bin/watch
          /usr/bin/fuser
          /usr/bin/killall
          /usr/bin/pstree
          /usr/bin/brctl
          /usr/bin/tftp
          /usr/bin/tftpd
          /usr/bin/hostname
          /usr/bin/netstat
          /usr/bin/arp
          /usr/bin/ifconfig
          /usr/bin/iptunnel
          /usr/bin/nameif
          /usr/bin/route
          /usr/bin/telnet
          /usr/bin/ftpd
          /usr/bin/telnetd
          /usr/bin/man
          /usr/bin/syslogd
          /usr/bin/klogd
          /usr/bin/logread
          /usr/bin/ntpd
          /usr/bin/udhcpc
          /usr/bin/ifenslave)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cp "${srcdir}"/config .config

  patch -Np1 -i ../busybox-1.19.2-bb.patch
  patch -Np1 -i ../busybox-1.19.2-man.patch

  sed -i -r \
    -e 's:[[:space:]]?-(Werror|Os|falign-(functions|jumps|loops|labels)=1|fomit-frame-pointer)\>::g' \
    Makefile.flags
  sed -i '/^#error Aborting compilation./d' applets/applets.c
  sed -i 's:-Wl,--gc-sections::' Makefile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -D -m0755 busybox "${pkgdir}"/usr/bin/busybox

  for x in $(seq 0 $((${#symlinks[@]} - 1))); do
    ln -sf busybox "${pkgdir}"/${symlinks[$x]}
  done

  install -o 160 -g 16 -m0750 -d "${pkgdir}"/etc/cron
  install -o 160 -g 49 -m1730 -d "${pkgdir}"/etc/cron/crontabs

  install -D -m0644 "${srcdir}"/sysctl.conf "${pkgdir}"/etc/sysctl.conf
  install -D -m0644 "${srcdir}"/man_db.conf "${pkgdir}"/etc/man_db.conf
  install -D -m0755 "${srcdir}"/runsvdirstart.script \
    "${pkgdir}"/usr/bin/runsvdirstart.script
  install -D -m0755 "${srcdir}"/runsvdirlog.script \
    "${pkgdir}"/usr/bin/runsvdirlog.script
  install -D -m0755 "${srcdir}"/udhcpc.script \
    "${pkgdir}"/usr/bin/udhcpc.script
  install -D -m0644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  if [[ ${CARCH} == "x86_64" ]]; then
    echo "kernel.shmmax = 0xffffffffffffffff" >> "${pkgdir}/etc/sysctl.conf"
    echo "kernel.shmall = 0x0fffffffffffff00" >> "${pkgdir}/etc/sysctl.conf"
  else
    echo "kernel.shmmax = 0xffffffff" >> "${pkgdir}/etc/sysctl.conf"
    echo "kernel.shmall = 0x0fffffff" >> "${pkgdir}/etc/sysctl.conf"
  fi
}

source=(${url}/downloads/${pkgname}-${pkgver}.tar.bz2)
#{,.sign})
sha256sums=('9b853406da61ffb59eb488495fe99cbb7fb3dd29a31307fcfa9cf070543710ee')
#            '04526172ae950625388bde790f1b78ab126307a6396f80e9548e54621b513171')

source=(${source[@]-}
        busybox-1.19.2-bb.patch
        busybox-1.19.2-man.patch
        config
        man_db.conf
        runsvdirlog.script
        runsvdirstart.script
        sysctl.conf
        udhcpc.script)

sha256sums=(${sha256sums[@]-}
            '98f92c2edbcf61d1bacef783ea8b08cce07051b0a4489ed3f4579296846f89f1'
            '733e23d851e68ecd27ed8bb8f4c4b8ea27b0d143069ec49aadb04deeb65dd9f5'
            'aa4b216d8f9d88ba204a5d906159adb5d23cd25d08f0bb1203783ad3466ce959'
            'bf0092798438c18fd3e7f6d9a8a281d6575cf4258d2457b6320d9c6bc8443eae'
            'cb9a7e2773ee7dff82a7902d8b6db6c10b225633ba36db77b609911b150afbc4'
            '0f88f033e562769b86e486027e37b5c27a8d0896f1a09f3c6a33374b2ff1eb33'
            '2470c45daabf74cb4905f9e3766da7db1a638621522c51d62952ba2f02ebb342'
            '0a09f17990345c331e8aed037c3303ddc18c8e28e91eb11b6884455127913a01')

