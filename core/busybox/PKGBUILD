pkgname=busybox
pkgver=1.19.3
pkgrel=1
pkgdesc='Utilities for rescue, embedded and minimal systems'
arch=('i686' 'x86_64')
url='http://busybox.net'
license=('GPL')
options=('emptydirs')
install=busybox.install
provides=('wget' 'sed' 'diffutils' 'findutils' 'awk' 'gzip' 'unzip' 'tar' 'which' 'sharutils'
          'sh' 'run-parts' 'vi' 'procps' 'cpio' 'cron' 'eject' 'runit' 'psmisc' 'bridge-utils'
          'iputils' 'net-tools' 'inetutils' 'man-db' 'logger' 'ntp' 'dhcpcd' 'grep' 'ifenslave')
groups=('base')

symlinks=(/usr/bin/uuencode
          /usr/bin/uudecode
          /usr/bin/smemcap
          /usr/bin/inotifyd
          /usr/bin/run-parts
          /usr/bin/crontab
          /usr/bin/crond
          /usr/bin/eject
          /usr/bin/volname
          /usr/bin/chpst
          /usr/bin/runsv
          /usr/bin/runsvdir
          /usr/bin/sv
          /usr/bin/svlogd
          /usr/bin/setuidgid
          /usr/bin/envuidgid
          /usr/bin/envdir
          /usr/bin/softlimit
          /usr/bin/gunzip
          /usr/bin/uncompress
          /usr/bin/gzip
          /usr/bin/unzip
          /usr/bin/which
          /usr/bin/kill
          /usr/bin/ps
          /usr/bin/sysctl
          /usr/bin/free
          /usr/bin/pgrep
          /usr/bin/pkill
          /usr/bin/pmap
          /usr/bin/pwdx
          /usr/bin/top
          /usr/bin/uptime
          /usr/bin/watch
          /usr/bin/fuser
          /usr/bin/killall
          /usr/bin/pstree
          /usr/bin/brctl
          /usr/bin/ping
          /usr/bin/ping6
          /usr/bin/traceroute
          /usr/bin/traceroute6
          /usr/bin/arping
          /usr/bin/tftp
          /usr/bin/tftpd
          /usr/bin/hostname
          /usr/bin/netstat
          /usr/bin/arp
          /usr/bin/ifconfig
          /usr/bin/iptunnel
          /usr/bin/nameif
          /usr/bin/route
          /usr/bin/telnet
          /usr/bin/ftpd
          /usr/bin/telnetd
          /usr/bin/man
          /usr/bin/syslogd
          /usr/bin/klogd
          /usr/bin/logread
          /usr/bin/ntpd
          /usr/bin/udhcpc
          /usr/bin/ifenslave)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cp "${srcdir}"/config .config

  patch -Np1 -i ../busybox-1.19.2-bb.patch
  patch -Np1 -i ../busybox-1.19.2-man.patch

  sed -i -r \
    -e 's:[[:space:]]?-(Werror|Os|falign-(functions|jumps|loops|labels)=1|fomit-frame-pointer)\>::g' \
    Makefile.flags
  sed -i '/^#error Aborting compilation./d' applets/applets.c
  sed -i 's:-Wl,--gc-sections::' Makefile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make install
  rm _install/bin/busybox
  tar -cf busybox-links.tar -C _install .
  install -D -m0755 busybox "${pkgdir}"/bin/busybox
  install -D -m0644 busybox-links.tar "${pkgdir}/usr/share/busybox/busybox-links.tar"
  install -m0755 -d "${pkgdir}"/usr/bin

  for x in $(seq 0 $((${#symlinks[@]} - 1))); do
    ln -sf /bin/busybox "${pkgdir}"/${symlinks[$x]}
  done

  install -o 160 -g 16 -m0750 -d "${pkgdir}"/var/spool/cron
  install -o 160 -g 409 -m1730 -d "${pkgdir}"/var/spool/cron/crontabs

  install -D -m0644 "${srcdir}"/sysctl.conf "${pkgdir}"/etc/sysctl.conf
  install -D -m0644 "${srcdir}"/man_db.conf "${pkgdir}"/etc/man_db.conf
  install -D -m0755 "${srcdir}"/runsvdir-start "${pkgdir}"/usr/bin/runsvdir-start
  install -D -m0755 "${srcdir}"/runsvdir-log "${pkgdir}"/usr/bin/runsvdir-log
  install -D -m0755 "${srcdir}"/udhcpc-script "${pkgdir}"/usr/bin/udhcpc-script
  install -D -m0644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  if [[ ${CARCH} == "x86_64" ]]; then
    echo "kernel.shmmax = 0xffffffffffffffff" >> "${pkgdir}/etc/sysctl.conf"
    echo "kernel.shmall = 0x0fffffffffffff00" >> "${pkgdir}/etc/sysctl.conf"
  else
    echo "kernel.shmmax = 0xffffffff" >> "${pkgdir}/etc/sysctl.conf"
    echo "kernel.shmall = 0x0fffffff" >> "${pkgdir}/etc/sysctl.conf"
  fi
}

source=(${url}/downloads/${pkgname}-${pkgver}.tar.bz2{,.sign})
sha256sums=('a1a9a35732c719ef384f02b6e357c324d8be25bc154af91a48c4264b1e6038f0'
            '04526172ae950625388bde790f1b78ab126307a6396f80e9548e54621b513171')
source=(${source[@]-}
        busybox-1.19.2-bb.patch
        busybox-1.19.2-man.patch
        config
        man_db.conf
        runsvdir-log
        runsvdir-start
        sysctl.conf
        udhcpc-script)

sha256sums=(${sha256sums[@]-}
            '98f92c2edbcf61d1bacef783ea8b08cce07051b0a4489ed3f4579296846f89f1'
            '733e23d851e68ecd27ed8bb8f4c4b8ea27b0d143069ec49aadb04deeb65dd9f5'
            '87c07fd39b180917c08e723b8d177b43ccca65f2233f8b80c286767de83bc089'
            'bf0092798438c18fd3e7f6d9a8a281d6575cf4258d2457b6320d9c6bc8443eae'
            '0996e549d86446842b5b32d88e90f3b6257365ab0e2a3506e3e8e7f75f3b5138'
            '26c4234e12c9a12ceac3e5a7a963708a5b21b5b8154c6d127be0de3d6114c203'
            '2470c45daabf74cb4905f9e3766da7db1a638621522c51d62952ba2f02ebb342'
            '0ef8e162587898f7a940ce09d32158028bf935ca247d79c5f95f1b177304e17a')
