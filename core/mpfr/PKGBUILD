# Based upon the PKGBUILD from Arch Linux
pkgname=mpfr
_pkgver=3.0.1
_patchlevel=p4
pkgver=${_pkgver}.${_patchlevel}
pkgrel=1
pkgdesc="Multiple-precision floating-point library"
arch=('i686' 'x86_64')
url="http://www.mpfr.org/"
license=('LGPL')
groups=('base-devel')
depends=('gmp')
options=('!libtool' '!makeflags')
source=(http://www.mpfr.org/mpfr-current/mpfr-${_pkgver}.tar.xz
        mpfr-${_pkgver}.${_patchlevel}.patch)
md5sums=('645882d9d179113a70af84d27086ed9f'
         '52c2ac5ffbbbf0c68f08e89f016ba7aa')

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  patch -Np1 -i "${srcdir}"/mpfr-${_pkgver}.${_patchlevel}.patch
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --enable-thread-safe \
              --enable-shared
  make
}

check() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  make check
  msg "${pkgname} TESTS SUCCESSFUL"
}

package() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  make DESTDIR="${pkgdir}" install
}
