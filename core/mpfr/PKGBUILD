# Based upon the PKGBUILD from Arch Linux
pkgname=mpfr
_pkgver=3.1.0
_patchlevel=p7
pkgver="${_pkgver}.${_patchlevel}"
pkgrel=1
pkgdesc="Multiple-precision floating-point library"
arch=('i686' 'x86_64')
url="http://www.mpfr.org/"
license=('LGPL')
groups=('base-devel')
depends=('gmp')
options=('!libtool' '!makeflags')
source=(http://www.mpfr.org/mpfr-current/mpfr-${_pkgver}.tar.xz{,.asc}
        mpfr-${_pkgver}.${_patchlevel}.patch)
md5sums=('6e495841bb026481567006cec0f821c3'
         '293374ee4b01527f8f7889fdfa9016f8'
         'fb1bf603a2ad5e6ecc1903c3c53f5e13')

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  patch -Np1 -i $srcdir/mpfr-${_pkgver}.${_patchlevel}.patch
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --enable-thread-safe \
              --enable-shared
  CFLAGS=${CFLAGS/-Os/-O2}
  make
}

check() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  make DESTDIR="${pkgdir}" install
}
