# Based upon the PKGBUILD from Arch Linux
pkgname=libarchive
pkgver=2.8.5
pkgrel=1
pkgdesc="library that can create and read several streaming archive formats"
arch=('i686' 'x86_64')
url="http://libarchive.googlecode.com/"
license=('BSD')
groups=('base')
install=libarchive.install
depends=('zlib' 'bzip2' 'xz' 'acl' 'expat')
source=("http://libarchive.googlecode.com/files/libarchive-${pkgver}.tar.gz"
        release-2.8-fixes.patch)
sha256sums=('13993e0ffbd121ccda46ea226b1f8eac218de0fa8da7d8b1f998093d5c32a72d'
            'fcadc5ac3020a9fab5922d343baca28f067cc051e9b6f59e480349ba5b0a5060')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np0 < ../release-2.8-fixes.patch
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --without-xml2 \
              --without-openssl
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/libarchive/COPYING
}
