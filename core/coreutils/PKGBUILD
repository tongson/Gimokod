# Based upon the PKGBUILD from Arch Linux
pkgname=coreutils
pkgver=8.15
pkgrel=1
pkgdesc="The basic file, shell and text manipulation utilities of the GNU operating system"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.gnu.org/software/coreutils"
groups=('base')
depends=('acl' 'libcap' 'attr')
options=('!emptydirs')
source=(ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        coreutils-8.7-no-docs-tests-translations.patch
        coreutils-uname.patch)
md5sums=('094909fafa86110140b32e4948941545'
         '2cdd273d93a77d03739fa81d7c2acfe5'
         'c77658ccbce6135fd6b9d5cad6736cce'
         'c4fcca138b6abf6d443d48a6f0cd8833')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}"/coreutils-8.7-no-docs-tests-translations.patch

  # linux specific uname improvement (from gentoo portage)
  patch -Np1 -i "${srcdir}"/coreutils-uname.patch

  autoreconf -v
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --libexecdir=/usr/lib/coreutils \
              --enable-no-install-program=groups,hostname,kill,su,uptime \
              --disable-pam \
              --enable-acl \
              --enable-xattr \
              --disable-nls \
              --without-gmp
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
