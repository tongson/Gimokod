# Based upon the PKGBUILD from Arch Linux
pkgname=coreutils
pkgver=8.17
pkgrel=1
pkgdesc="The basic file, shell and text manipulation utilities of the GNU operating system"
arch=('x86_64')
license=('GPL3')
url="http://www.gnu.org/software/coreutils"
groups=('base')
depends=('acl' 'libcap' 'attr')
options=('!emptydirs')
source=(ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        coreutils-8.7-no-docs-tests-translations.patch)
md5sums=('bbda656ce8ca2c6903948f9faa204ba3'
         'ebecd29b095aa21b0b2f833f1ec20d70'
         'c77658ccbce6135fd6b9d5cad6736cce')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  Patch coreutils-8.7-no-docs-tests-translations.patch
  autoreconf -v
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --libexecdir=/usr/lib/coreutils \
              --enable-no-install-program=groups,hostname,kill,su,uptime \
              --disable-pam \
              --enable-acl \
              --enable-xattr \
              --disable-nls \
              --without-gmp
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
