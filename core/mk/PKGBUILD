pkgname=mk
_pkgname=9base
pkgver=6
pkgrel=1
pkgdesc="mk from 9base"
url="http://tools.suckless.org/9base"
arch=('i686' 'x86_64')
license=('custom')
groups=('base')
source=(http://dl.suckless.org/tools/$_pkgname-$pkgver.tar.gz)
sha256sums=('2997480eb5b4cf3092c0896483cd2de625158bf51c501aea2dc5cf74176d6de9')

build()
{
  cd "${srcdir}/${_pkgname}-${pkgver}"
  case "${CARCH}" in
    i686) sed -i 's#^OBJTYPE\s.*$#OBJTYPE = 386#' config.mk ;;
    x86_64) sed -i 's#^OBJTYPE\s.*$#OBJTYPE = x86_64#' config.mk ;;
  esac
  echo "PREFIX = /" >>config.mk
  echo "CFLAGS += -D_GNU_SOURCE -DPLAN9PORT -idirafter /usr/include" >>config.mk
  sed -i 's/__linux__/__not_linux__/;s/__sun__/__linux__/' lib9/dirread.c
  sed -i '/sys.termios.h/d' lib9/readcons.c
  sed -i '/^.touch./d' Makefile  # we don't have futimes
  make -C lib9
  make -C mk
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make -C mk DESTDIR="$pkgdir" install
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}

