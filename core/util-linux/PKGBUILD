# Based upon the PKGBUILD from Arch Linux
pkgname=util-linux
pkgver=2.20.1
pkgrel=1
pkgdesc="Miscellaneous system utilities for Linux"
url="http://userweb.kernel.org/~kzak/util-linux-ng/"
arch=('i686' 'x86_64')
groups=('base')
license=('GPL2')
depends=('ncurses' 'zlib')
options=('!libtool')
source=(ftp://ftp.infradead.org/pub/${pkgname}/v2.20/${pkgname}-${pkgver}.tar.bz2)
md5sums=('079b37517fd4e002a2e6e992e8b4e361')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # hardware clock
  sed -e 's%etc/adjtime%var/lib/hwclock/adjtime%' -i include/pathnames.h

  ./configure --build=${CBUILD} \
              --host=${CHOST} \
              --enable-arch \
              --enable-write \
              --enable-raw \
              --disable-wall \
              --enable-partx \
              --enable-libmount-mount \
              --disable-nls

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -dm755 "${pkgdir}/var/lib/hwclock"
  make DESTDIR="${pkgdir}" install
  chmod 0700 "${pkgdir}/bin/mount"
  chmod 0700 "${pkgdir}/bin/umount"
}
