# Based upon the PKGBUILD from Arch Linux
pkgname=util-linux
pkgver=2.21.1
pkgrel=1
pkgdesc="Miscellaneous system utilities for Linux"
url="http://www.kernel.org/pub/linux/utils/util-linux/"
arch=('x86_64')
groups=('base')
license=('GPL2')
depends=('ncurses' 'zlib')
options=('!libtool')
source=(ftp://ftp.kernel.org/pub/linux/utils/${pkgname}/v2.21/${pkgname}-${pkgver}.tar.xz)
md5sums=('ad602dcd528f340b1329cfa6200d8f80')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # hardware clock
  sed -e 's%etc/adjtime%var/lib/hwclock/adjtime%' -i include/pathnames.h

  ./configure --build=${CBUILD} \
              --host=${CHOST} \
              --bindir=/usr/bin \
              --sbindir=/usr/bin \
              --libdir=/usr/lib \
              --enable-arch \
              --enable-write \
              --enable-raw \
              --disable-wall \
              --enable-partx \
              --enable-libmount-mount \
              --enable-fs-paths-extra=/usr/bin \
              --disable-nls
  make
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  cd "${pkgdir}"
  install -dm755 "${pkgdir}/var/lib/hwclock"
  rm -rf usr/share/man/ru
  chmod 0700 "${pkgdir}/usr/bin/mount"
  chmod 0700 "${pkgdir}/usr/bin/umount"
  mv "${pkgdir}"/usr/sbin/* "${pkgdir}"/usr/bin
  rmdir "${pkgdir}"/usr/sbin
}
