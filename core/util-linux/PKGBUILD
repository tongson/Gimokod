# Based upon the PKGBUILD from Arch Linux
pkgname=util-linux
pkgver=2.21
pkgrel=1
pkgdesc="Miscellaneous system utilities for Linux"
url="http://www.kernel.org/pub/linux/utils/util-linux/"
arch=('x86_64')
groups=('base')
license=('GPL2')
depends=('ncurses' 'zlib')
options=('!libtool')
source=(ftp://ftp.kernel.org/pub/linux/utils/${pkgname}/v${pkgver}/${pkgname}-${pkgver}.tar.xz
        stable-fixes-2.21.patch)
md5sums=('208aa058f4117759d2939d1be7d662fc'
         '415b850a4efa9174cd9c34022949f177')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # hardware clock
  sed -e 's%etc/adjtime%var/lib/hwclock/adjtime%' -i include/pathnames.h

  ./configure --build=${CBUILD} \
              --host=${CHOST} \
              --bindir=/usr/bin \
              --sbindir=/usr/bin \
              --libdir=/usr/lib \
              --enable-arch \
              --enable-write \
              --enable-raw \
              --disable-wall \
              --enable-partx \
              --enable-libmount-mount \
              --disable-nls

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  cd "${pkgdir}"
  install -dm755 "${pkgdir}/var/lib/hwclock"
  rm -rf usr/share/man/ru
  chmod 0700 "${pkgdir}/usr/bin/mount"
  chmod 0700 "${pkgdir}/usr/bin/umount"
  mv "${pkgdir}"/usr/sbin/* "${pkgdir}"/usr/bin
  rmdir "${pkgdir}"/usr/sbin
}
