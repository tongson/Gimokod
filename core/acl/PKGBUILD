pkgname=acl
pkgver=2.2.51
pkgrel=2
pkgdesc="Access control list utilities, libraries and headers"
arch=('x86_64')
url="http://savannah.nongnu.org/projects/acl"
license=('LGPL')
groups=('base')
depends=('attr')
options=('!libtool')
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}"/acl-quote-strchr.patch
  unset PLATFORM
  export INSTALL_USER=root INSTALL_GROUP=root
  export OPTIMIZER="${CFLAGS}"
  export DEBUG=-DNDEBUG
  CONFIG_SHELL=/bin/sh ./configure --prefix=/usr \
                                   --build=${CBUILD} \
                                   --host=${CHOST} \
                                   --libdir=/usr/lib \
                                   --libexecdir=/usr/lib \
                                   --enable-gettext=no
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DIST_ROOT="${pkgdir}" install install-lib install-dev
  rm -f "${pkgdir}"/usr/lib/libacl.a
  chmod 0755 "${pkgdir}"/usr/lib/libacl.so.*.*.*
}

source=(http://download.savannah.gnu.org/releases/${pkgname}/${pkgname}-${pkgver}.src.tar.gz
        acl-quote-strchr.patch)

sha256sums=('06854521cf5d396801af7e54b9636680edf8064355e51c07657ec7442a185225'
            'd172b07ae4ccb4c8da95f90cc836075e6e582b3b57e029987e4197e58f5daf15')
