# Based upon the PKGBUILD from Arch Linux
pkgname=db
pkgver=5.2.28
pkgrel=1
pkgdesc="The Berkeley DB embedded database system"
arch=('i686' 'x86_64')
url='http://www.oracle.com/technology/software/products/berkeley-db/index.html'
license=('custom')
groups=('base-devel')
options=('!libtool')
source=(http://download.oracle.com/berkeley-db/db-${pkgver}.tar.gz)
sha1sums=('e6572e3356cf8bc998da4e889db3b12638833435')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/build_unix"
  ../dist/configure --prefix=/usr \
                    --build=${CBUILD} \
                    --host=${CHOST} \
                    --enable-compat185 \
                    --enable-shared \
                    --enable-static \
                    --disable-cxx \
                    --enable-dbm
  make LIBSO_LIBS=-lpthread
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build_unix"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/docs
  install -Dm644 "${srcdir}"/"${pkgname}"-"${pkgver}"/LICENSE \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
