# Based upon the PKGBUILD from Arch Linux
pkgname=db
pkgver=5.3.15
pkgrel=1
pkgdesc="The Berkeley DB embedded database system"
arch=('i686' 'x86_64')
url='http://www.oracle.com/technology/software/products/berkeley-db/index.html'
license=('custom')
groups=('base-devel')
options=('!libtool')
source=(http://download.oracle.com/berkeley-db/db-${pkgver}.tar.gz)
sha1sums=('7683b632a01ff3543c379a120809a942ec457738')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/build_unix"
  ../dist/configure --prefix=/usr \
                    --build=${CBUILD} \
                    --host=${CHOST} \
                    --enable-compat185 \
                    --enable-shared \
                    --enable-static \
                    --disable-cxx \
                    --enable-dbm
  make LIBSO_LIBS=-lpthread
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build_unix"
  make DESTDIR="${pkgdir}" install
  rm -r "${pkgdir}"/usr/docs
  install -Dm644 "${srcdir}"/"${pkgname}"-"${pkgver}"/LICENSE \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
