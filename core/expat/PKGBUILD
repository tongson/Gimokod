# Based upon the PKGBUILD from Arch Linux and the expat ebuild from Gentoo
pkgname=expat
pkgver=2.1.0
pkgrel=1
pkgdesc="An XML parser library"
arch=('x86_64')
url="http://expat.sourceforge.net"
license=('custom')
groups=('base')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/expat/${pkgname}-${pkgver}.tar.gz)
md5sums=('dd7dab7a5fea97d2a6a43f511449b7cd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir "${srcdir}"-build{,u,w}

  local d
  for d in build buildu buildw; do
    pushd "${srcdir}"-${d}
      [[ ${d} == buildu ]] && export _CPPFLAGS="-UXML_UNICODE"
      [[ ${d} == buildw ]] && export _CPPFLAGS="-UXML_UNICODE -DXML_UNICODE_WCHAR_T"
      CPPFLAGS="${CPPFLAGS} ${_CPPFLAGS}" ${srcdir}/${pkgname}-${pkgver}/configure \
                                            --prefix=/usr \
                                            --build=${CBUILD} \
                                            --host=${CHOST} \
                                            --mandir=/usr/share/man
    popd
  done

  cd "${srcdir}"-build
  make
  cd "${srcdir}"-buildu
  make buildlib LIBRARY=libexpatu.la
  cd "${srcdir}"-buildw
  make buildlib LIBRARY=libexpatw.la
}

check() {
  cd "${srcdir}"-build
  make check
}


package() {
  cd "${srcdir}"-build
  make DESTDIR="${pkgdir}" install
  cd "${srcdir}"-buildu
  make installlib DESTDIR="${pkgdir}" LIBRARY=libexpatu.la
  cd "${srcdir}"-buildw
  make installlib DESTDIR="${pkgdir}" LIBRARY=libexpatw.la
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
