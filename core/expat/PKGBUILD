# Based upon the PKGBUILD from Arch Linux
pkgname=expat
pkgver=2.0.1
pkgrel=1
pkgdesc="An XML parser library"
arch=('i686' 'x86_64')
url="http://expat.sourceforge.net"
license=('custom')
groups=('base')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/expat/${pkgname}-${pkgver}.tar.gz
        CVE-2009-3560.patch
        CVE-2009-3720.patch)
md5sums=('ee8b492592568805593f81f8cdf2a04c'
         '50603cac0f03aabc7087415251f592be'
         'f3eeb796f28945899216b815e5901996')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}"/CVE-2009-3560.patch
  patch -Np1 -i "${srcdir}"/CVE-2009-3720.patch
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
