# Based upon the PKGBUILD from Arch Linux
pkgname=pciutils
pkgver=3.1.7
pkgrel=1
pkgdesc="PCI bus configuration space access library and tools"
arch=(i686 x86_64)
license=('GPL2')
depends=
groups=('base')
url="http://mj.ucw.cz/pciutils.html"
source=(ftp://ftp.kernel.org/pub/software/utils/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('35222edb86b50a6641352c66fe829b2e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i 's:wget --no-timestamping:wget:' ./update-pciids.sh
  ./update-pciids.sh
  make OPT="${CFLAGS} -fPIC -DPIC" \
       ZLIB=no \
       SHARED=no \
       PREFIX=/usr \
       SHAREDIR=/usr/share/hwdata \
       MANDIR=/usr/share/man \
       all

  cp lib/libpci.a "${srcdir}/"

  make clean
  make OPT="${CFLAGS}" \
       ZLIB=no \
       SHARED=yes \
       PREFIX=/usr \
       SHAREDIR=/usr/share/hwdata \
       MANDIR=/usr/share/man \
       all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make SHARED=yes PREFIX=/usr SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man DESTDIR="${pkgdir}" install install-lib
  install -m644 "${srcdir}/libpci.a" "${pkgdir}/usr/lib/"
}
