pkgname=musl
pkgver=0.8.7
pkgrel=1
pkgdesc="A lightweight, fast, simple, and free C library implementation"
arch=('x86_64')
url="http://www.etalabs.net/musl/"
license=('LGPL2')
makedepends=('make')
groups=('base-devel')
options=('!binchecks')
source=(http://www.etalabs.net/musl/releases/"${pkgname}"-"${pkgver}".tar.gz)
sha256sums=('b1c8cbea94e03c5b58f489fa393d457fb7b131f6d575d3eed79a2ba3626e5b9f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  if [ "$CARCH" = "x86_64" ]; then
    sed 's/^ARCH.*/ARCH = x86_64/' -i config.mak
  fi
  CFLAGS="${CFLAGS} -nostdinc -ffreestanding -std=c99 -D_XOPEN_SOURCE=700"
  LDFLAGS="${LDFLAGS} -nostdlib -shared -fPIC -Wl,-e,_start -Wl,-Bsymbolic-functions"
  PIC="-fPIC"
  make prefix=/usr/lib/musl \
       exec_prefix=/usr/lib/musl \
       syslibdir=/usr/lib/musl/lib \
       CFLAGS="${CFLAGS}" LDFLAGS="${LDFLAGS}" PIC="${PIC}"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" prefix=/usr/lib/musl exec_prefix=/usr/lib/musl syslibdir=/usr/lib/musl/lib install
}
