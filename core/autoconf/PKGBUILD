# Based upon the PKGBUILD from Arch Linux
pkgname=autoconf
pkgver=2.69
pkgrel=1
pkgdesc="A GNU tool for automatically configuring source code"
arch=('any')
license=('GPL2' 'GPL3' 'custom')
url="http://www.gnu.org/software/autoconf"
groups=('base-devel')
depends=('m4')
install=autoconf.install
source=(ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz)
md5sums=('50f97f4159805e374639a73e2636f22e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST}
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # license exception
  install -Dm644 COPYING.EXCEPTION \
    "${pkgdir}"/usr/share/licenses/autoconf/COPYING.EXCEPTION

  # conflict with bintuils
  rm -f "${pkgdir}"/usr/share/info/standards.info
}
