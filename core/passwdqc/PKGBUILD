pkgname=passwdqc
pkgver=1.2.2
pkgrel=1
pkgdesc="A password/passphrase strength checking and policy enforcement toolset"
arch=('i686' 'x86_64')
url="http://www.openwall.com/passwdqc"
license=('BSD')
groups=('base')
depends=('pam')
options=('!emptydirs')
install=passwdqc.install
build() {
  cd "$srcdir/$pkgname-$pkgver"
  CFLAGS="${CFLAGS} -Wall -W -fPIC -DLINUX_PAM"
  LDFLAGS="${LDFLAGS} -lcrypt -lpam"
  make CFLAGS="${CFLAGS}" LDFLAGS="${LDFLAGS}"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make install DESTDIR="${pkgdir}" MANDIR=/usr/share/man \
       SHARED_LIBDIR=/usr/lib DEVEL_LIBDIR=/usr/lib SECUREDIR=/usr/lib/security
  install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

source=(http://www.openwall.com/${pkgname}/${pkgname}-${pkgver}.tar.gz
        LICENSE)

sha256sums=('4a05a8c6c29f2648f67fc096ac6b8972f62ef9440c13e22066c5304a7ee2cb80'
            '99e904d13eeb6fa9e1c9c47d3a0a13111b3ead882af1ebc9b87f41fbfed1c8af')
