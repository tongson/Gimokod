# Based upon the PKGBUILD from Arch Linux
pkgname=pacman
pkgver=4.0.3
pkgrel=2
pkgdesc="A library-based package manager with dependency support"
arch=('x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base')
depends=('libarchive' 'gpgme' 'gimokod-keyring')
install=pacman.install
options=(!libtool)
source=(ftp://ftp.archlinux.org/other/pacman/"${pkgname}"-"${pkgver}".tar.gz)
sha256sums=('a48696ae51aaccf31a69ea7916ba9b06e7836eb933b8a281c666729c9d55ce25')

build() {
  cd "${srcdir}"/"${pkgname}"-"${pkgver}"
  Patch pacman-4.0.0-makepkg.diff
  Patch pacman-4.0.0-PKGNAME.diff
  Patch pacman-4.0.2-usrmove.diff
  rm test/pacman/tests/sync200.py # requires Curl
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --disable-doc \
              --disable-nls \
              --without-openssl \
              --without-libcurl \
              --with-gpgme
  make
}

check() {
  make -C "$pkgname-$pkgver" check
}


package() {
  cd "${srcdir}"/"${pkgname}"-"${pkgver}"
  make DESTDIR="${pkgdir}" install
  mkdir -p "${pkgdir}"/etc
  install -m644 "${srcdir}"/pacman.conf "${pkgdir}"/etc/
}

source=(${source[@]-}
        pacman-4.0.0-PKGNAME.diff
        pacman-4.0.0-makepkg.diff
        pacman-4.0.2-usrmove.diff
        pacman.conf)

sha256sums=(${sha256sums[@]-}
            '56ee72e2fbc53b5d3201895a167b40295e638aaae93adb8cb3b9c8d0d6f4442a'
            '51a9dfb31c13ca40a1f7378d7be4f28d27a4a7f981d1c51cb19ea2c300415e88'
            '7ddd5c009d5f6a033be3c284310190ff457357963b83d77a117929a0ccef68e3'
            '387eb22abab045c3f8c755ab67ea2749449779b5db18252b58d70bd268391755')
