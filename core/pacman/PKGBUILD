# Based upon the PKGBUILD from Arch Linux
pkgname=pacman
pkgver=4.0.0
pkgrel=1
pkgdesc="A library-based package manager with dependency support"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base')
depends=('libarchive' 'gpgme')
backup=(etc/pacman.conf etc/makepkg.conf)
install=pacman.install
options=(!libtool)
source=(ftp://ftp.archlinux.org/other/pacman/"${pkgname}"-"${pkgver}".tar.gz
        pacman-4.0.0-dload_h.patch
        pacman-4.0.0-makepkg.patch
        pacman.conf
        pacman.conf.x86_64
        makepkg.conf)
md5sums=('8c97f4804d2a0847956c45888b0ea517'
         '4e9c51348456dc8dc1687bbf6b05ee7b'
         'b73f08ce20f8b6ef9378e93d42038bec'
         'ae4abf6df12483d26b39529ab27c0837'
         'eb78f7191a61921416ce1a090f8fd3ba'
         'db051afbd12993b7743ccd4d58668499')

PKGEXT='.pkg.tar.gz'

build() {
  cd "${srcdir}"/"${pkgname}"-"${pkgver}"
  patch -Np1 -i ../pacman-4.0.0-dload_h.patch
  patch -Np1 -i ../pacman-4.0.0-makepkg.patch
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --disable-doc \
              --disable-nls \
              --with-openssl \
              --without-libcurl \
              --with-gpgme
  make
}

package() {
  cd "${srcdir}"/"${pkgname}"-"${pkgver}"
  make DESTDIR="${pkgdir}" install

  # install Arch specific stuff
  mkdir -p "${pkgdir}"/etc
  case "$CARCH" in
    i686)
      install -m644 "${srcdir}"/pacman.conf "${pkgdir}"/etc/pacman.conf
      mycarch="i686"
      mychost="i686-pc-linux-gnu"
      myflags="-march=i686 "
      ;;
    x86_64)
      install -m644 "${srcdir}"/pacman.conf.x86_64 "${pkgdir}"/etc/pacman.conf
      mycarch="x86_64"
      mychost="x86_64-unknown-linux-gnu"
      myflags="-march=x86-64 "
      ;;
  esac
  install -m644 "${srcdir}"/makepkg.conf "${pkgdir}"/etc/
  # set things correctly in the default conf file
  sed -i "${pkgdir}"/etc/makepkg.conf \
    -e "s|@CARCH[@]|$mycarch|g" \
    -e "s|@CHOST[@]|$mychost|g" \
    -e "s|@CARCHFLAGS[@]|$myflags|g"

  # install completion files
  mkdir -p "${pkgdir}"/etc/bash_completion.d/
  install -m644 contrib/bash_completion "${pkgdir}"/etc/bash_completion.d/pacman
  mkdir -p "${pkgdir}"/usr/share/zsh/site-functions/
  install -m644 contrib/zsh_completion "${pkgdir}"/usr/share/zsh/site-functions/_pacman
}

