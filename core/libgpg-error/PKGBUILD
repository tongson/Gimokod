# Based upon the PKGBUILD from Arch Linux
pkgname=libgpg-error
pkgver=1.10
pkgrel=1
pkgdesc="Support library for libgcrypt"
arch=('i686' 'x86_64')
url="http://www.gnupg.org"
license=('LGPL')
groups=('base')
options=(!libtool)
source=(ftp://ftp.gnupg.org/gcrypt/libgpg-error/${pkgname}-${pkgver}.tar.bz2)
md5sums=('736a03daa9dc5873047d4eb4a9c22a16')


build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --disable-nls
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  # Move dynamic libraries to /lib
  install -d -m755 "${pkgdir}"/lib/
  mv "${pkgdir}"/usr/lib/libgpg-error.so* "${pkgdir}"/lib/
  ln -sf /lib/libgpg-error.so "${pkgdir}"/usr/lib/libgpg-error.so
}
