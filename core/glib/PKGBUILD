pkgname=glib
pkgver=2.32.3
pkgrel=1
pkgdesc="Common C routines used by GTK+ and other libs"
url="http://www.gtk.org/"
arch=('x86_64')
license=('LGPL')
groups=('base-devel')
depends=('zlib' 'libffi')
makedepends=('zlib' 'libffi' 'gettext')
options=('!libtool' '!docs')
build() {
  cd glib-$pkgver
  sed 's/^\(.*\SUBDIRS .*\=.*\)po docs\(.*\)$/\1\2/' -i $(find . -name Makefile.am -o -name Makefile.in)
  sed 's/^\(.*\SUBDIRS .*\=.*\)tests\(.*\)$/\1\2/' -i $(find . -name Makefile.am -o -name Makefile.in)
  sed -i 's:^tests\/.*$::' configure.ac
  sed -i 's:^docs\/.*$::' configure.ac
  Patch glib-2.31.x-external-gdbus-codegen.patch
  Patch revert-warn-glib-compile-schemas.patch
  ln -sfn /bin/true py-compile # echo '#!/usr/bin/env sh' > py-compile 1.11.2
  AT_M4DIR="${srcdir}/${pkgname}-${pkgver}" autoreconf -v
  export DBUS1_CFLAGS="-I/usr/include/dbus-1.0 -I/usr/lib/dbus-1.0/include"
  export DBUS1_LIBS="-ldbus-1"
  export LIBFFI_CFLAGS="-I$(echo /usr/lib/libffi-*/include)"
  export LIBFFI_LIBS="-lffi"
  sed -i 's:^GTK_DOC_CHECK.*$::' configure
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --libdir=/usr/lib \
              --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --with-pcre=internal \
              --with-threads=posix \
              --disable-dtrace \
              --disable-systemtap \
              --disable-dependency-tracking \
              --enable-debug=yes \
              --disable-selinux \
              --enable-gtk-doc-html=no \
              --disable-fam
  make
}

package() {
  cd glib-$pkgver
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/share/gdb/" "${pkgdir}/usr/share/glib-2.0/gdb/"
  for _i in "${pkgdir}/etc/bash_completion.d/"*; do
    chmod -x "${_i}"
  done
}

source=(http://ftp.gnome.org/pub/GNOME/sources/glib/${pkgver%.*}/glib-$pkgver.tar.xz)
sha256sums=('b65ceb462807e4a2f91c95e4293ce6bbefca308cb44a1407bcfdd9e40363ff4d')

source=(${source[@]-}
        glib-2.31.x-external-gdbus-codegen.patch
        revert-warn-glib-compile-schemas.patch)

sha256sums=(${sha256sums[@]-}
            '47a0f2f2a99a50042063952994c8538097dee14ea5b2ca39f1c168092c645c6e'
            '049240975cd2f1c88fbe7deb28af14d4ec7d2640495f7ca8980d873bb710cc97')
