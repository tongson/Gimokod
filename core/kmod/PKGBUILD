# Based upon the PKGBUILD from Arch Linux
pkgname=kmod
pkgver=8
pkgrel=1
pkgdesc="Linux kernel module handling"
arch=('x86_64')
url='http://git.kernel.org/?p=utils/kernel/kmod/kmod.git;a=summary'
license=('GPL2')
depends=('zlib')
groups=('base')
options=('!libtool')
source=("ftp://ftp.kernel.org/pub/linux/utils/kernel/$pkgname/$pkgname-$pkgver.tar.xz")
md5sums=('d4e3d038b5370b1e8ff237c55666aa64')

build() {
  cd "${pkgname}-${pkgver}"
  touch libkmod/docs/gtk-doc.make
  ./configure \
    --sysconfdir=/etc \
    --disable-debug \
    --without-xz \
    --enable-tools \
    --with-rootprefix=/usr \
    --with-zlib
  make
}

package() {
  make -C "${pkgname}-${pkgver}" DESTDIR="${pkgdir}" install
  for tool in {ls,ins,rm,dep}mod mod{info,probe}; do
    ln -s kmod "$pkgdir/usr/bin/$tool"
  done
}

