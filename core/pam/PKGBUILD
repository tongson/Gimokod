# Based upon the PKGBUILD from Arch Linux
pkgname=pam
pkgver=1.1.5
pkgrel=1
pkgdesc="PAM (Pluggable Authentication Modules) library"
arch=('i686' 'x86_64')
license=('GPL2')
groups=('base')
url="http://www.kernel.org/pub/linux/libs/pam/"
install=pam.install
options=('!libtool' '!emptydirs')

build() {
  cd "${srcdir}"/Linux-PAM-${pkgver}
  Patch pam-1.1.4-disable-po-tests.diff
  ./configure --sysconfdir=/etc \
              --build=${CBUILD} \
              --host=${CHOST} \
              DESTDIR="${pkgdir}" \
              --libdir=/lib \
              --disable-nls \
              --disable-nis \
              --disable-selinux \
              --enable-db=no \
              --disable-regenerate-docu \
              --disable-cracklib
  make
}

check() {
  cd "${srcdir}"/Linux-PAM-${pkgver}
  make check
}

package() {
  cd "${srcdir}"/Linux-PAM-${pkgver}
  make INSTALL=/bin/install DESTDIR="${pkgdir}" install
  install -D -m0644 "${srcdir}/other" "${pkgdir}/etc/pam.d/other"
  install -D -m0644 "${srcdir}/system-auth" "${pkgdir}/etc/pam.d/system-auth"
  install -D -m0644 "${srcdir}/config-util" "${pkgdir}/etc/pam.d/config-util"
  chgrp shadow "${pkgdir}/etc/pam.d/system-auth"
  chmod 4755 "${pkgdir}/sbin/pam_timestamp_check"
  chmod 0755 "${pkgdir}/etc/security/namespace.init"

  rm "${pkgdir}/lib/security/pam_unix.so"
  rm "${pkgdir}/usr/share/man/man8/pam_unix.8"

  # add the realtime permissions for audio users
  sed -i 's|# End of file||' "${pkgdir}"/etc/security/limits.conf
  cat >>${pkgdir}/etc/security/limits.conf <<_EOT
*               -       rtprio          0
*               -       nice            0
@audio          -       rtprio          65
@audio          -       nice           -10
@audio          -       memlock         40000
_EOT
}

source=(http://gimokod.org/distfiles/Linux-PAM-${pkgver}.tar.bz2)
sha256sums=('65def4df04254dc4c5156859d36c34ad6d7afbcf3adbf2780530ebc4dbf2a116')

source=(${source[@]-}
        config-util
        other
        pam-1.1.4-disable-po-tests.diff
        system-auth)

sha256sums=(${sha256sums[@]-}
            'e6843769d7c7d43a87a9bbcd774f693632436b0eeb7a44b0bf1c10ed6044ac8f'
            'c5c2a7c1f8dfbefa5df66cd32a7b754266d6375471d43cea06b8bea937d24b40'
            '482f71d1d6c15030494508145e9dc3e2eab391a7df2056c0d4d651ed8af6cbb4'
            '98f8b37a5ab0c77fe7eca7958cd79ee84f5c93b6a1aa63edda6c307f4523df87')
