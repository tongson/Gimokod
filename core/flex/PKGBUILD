# Based upon the PKGBUILD from Arch Linux
pkgname=flex
pkgver=2.5.35
pkgrel=1
pkgdesc="A tool for generating text-scanning programs"
arch=('i686' 'x86_64')
url="http://flex.sourceforge.net"
license=('custom')
groups=('base-devel')
depends=('m4')
source=(http://downloads.sourceforge.net/sourceforge/flex/flex-${pkgver}.tar.bz2 
        flex-2.5.35-gcc44.patch
        flex-2.5.35-hardening.patch
        flex-2.5.35-missing-prototypes.patch
        flex-2.5.35-sign.patch
        lex.sh)
md5sums=('10714e50cea54dc7a227e3eddcd44d57'
         'e4444ef5c07db71a43280be74139bdea'
         'de952b3ed7cc074bc8c3e6ab73634048'
         '6b83f56b1b654c6a321cdc530a3ec68d'
         'd87fd9e9762ba7e230d516bdcf1c8c6f'
         'f725259ec23a9e87ee29e2ef82eda9a5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}"/flex-2.5.35-gcc44.patch
  patch -Np1 -i "${srcdir}"/flex-2.5.35-hardening.patch
  patch -Np1 -i "${srcdir}"/flex-2.5.35-missing-prototypes.patch
  patch -Np1 -i "${srcdir}"/flex-2.5.35-sign.patch

  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --disable-nls
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make prefix="${pkgdir}"/usr \
    mandir="${pkgdir}"/usr/share/man \
    infodir="${pkgdir}"/usr/share/info \
    install
  install -Dm755 "${srcdir}"/lex.sh "${pkgdir}"/usr/bin/lex

  install -Dm644 COPYING \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"/license.txt

}

