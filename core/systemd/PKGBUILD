# Based upon the PKGBUILD from Arch Linux
pkgname=systemd
pkgver=44
pkgrel=2
pkgdesc="Session and Startup manager"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2')
makedepends=('gperf' 'intltool' 'gettext' 'linux-headers' 'dbus-core' 'kbd' 'libcap'
             'cryptsetup' 'udev' 'kmod' 'xz')
groups=('base')
replaces=('libsystemd')
options=('!libtool' '!strip')
source=("http://www.freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.xz"
         0001-util-never-follow-symlinks-in-rm_rf_children.patch
         0001-logind-close-FIFO-before-ending-sessions-cleanly.patch
         0001-check-for-proper-return-from-dirent_ensure_type.patch)
md5sums=('11f44ff74c87850064e4351518bcff17'
         'b5863d6d4b47e2b5bda8eb57bde0d327'
         'd37833358ef6c23fad622ea4a0941d1f'
         '11f930fd0a3966abc794bf9127a7dde0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  pushd src
  for f in $(grep -lF '/sbin/' *);do
    sed -i 's:\/sbin\/:\/usr\/bin\/:g' $f
  done
  popd
  Patch 0001-util-never-follow-symlinks-in-rm_rf_children.patch
  Patch 0001-logind-close-FIFO-before-ending-sessions-cleanly.patch
  Patch 0001-check-for-proper-return-from-dirent_ensure_type.patch
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --with-udevrulesdir=/usr/lib/udev/rules.d \
              --with-pamlibdir=/usr/lib/security \
              --libexecdir=/usr/lib \
              --libdir=/usr/lib \
              --localstatedir=/var \
              --with-rootlibdir=/usr/lib \
              --disable-audit \
              --disable-tcpwrap \
              --enable-libcryptsetup \
              --with-distro=other \
              --with-sysvinit-path="" \
              --with-sysvrcd-path="" \
              --disable-selinux \
              --disable-plymouth \
              --disable-binfmt \
              --disable-gtk \
              --enable-pam \
              --disable-timedated \
              --disable-hostnamed \
              --disable-localed \
              --disable-quotacheck \
              --disable-manpages \
              --disable-nls

  make
}


package() {
  pkgdesc="system and service manager"
  depends=('dbus-core' 'kbd' 'libcap' 'cryptsetup' 'udev' 'kmod' 'xz')
  install=systemd.install
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/var/lib/systemd"
  printf "d /run/console 755 root root\n" > "${pkgdir}/usr/lib/tmpfiles.d/console.conf"
  chmod 644 "$pkgdir/usr/lib/tmpfiles.d/console.conf"
  pushd "${pkgdir}/usr/lib/systemd/system"
    sed -i 's:/sbin:/usr/bin:g' *.service >/dev/null 2>&1
  popd

  # --disable-manpages
  # rm -rf "${pkgdir}/usr/share/man"
}


