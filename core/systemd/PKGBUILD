# Based upon the PKGBUILD from Arch Linux
pkgname=systemd
pkgver=43
pkgrel=1
pkgdesc="Session and Startup manager"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2')
depends=('dbus-core' 'kbd' 'libcap' 'cryptsetup' 'udev' 'kmod' 'xz')
makedepends=('gperf' 'intltool' 'gettext')
groups=('base')
options=('!libtool' '!strip')
install=systemd.install
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cd src
  for f in $(grep -lF '/sbin/' *);do 
    sed -i 's:\/sbin\/:\/usr\/bin\/:g' $f
  done
  cd ..

  # Don't unset locale in getty
  # https://bugzilla.redhat.com/show_bug.cgi?id=663900
  sed -i -e '/^Environ.*LANG/s/^/#/' \
         -e '/^ExecStart/s/agetty/& -8/' units/getty@.service.m4

  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --with-udevrulesdir=/usr/lib/udev/rules.d \
              --with-pamlibdir=/usr/lib/security \
              --libexecdir=/usr/lib \
              --libdir=/usr/lib \
              --localstatedir=/var \
              --with-rootlibdir=/usr/lib \
              --disable-audit \
              --disable-tcpwrap \
              --enable-libcryptsetup \
              --with-distro=other \
              --with-sysvinit-path="" \
              --with-sysvrcd-path="" \
              --disable-selinux \
              --disable-plymouth \
              --disable-binfmt \
              --disable-gtk \
              --enable-pam \
              --disable-timedated \
              --disable-hostnamed \
              --disable-localed \
              --disable-quotacheck \
              --disable-manpages \
              --disable-nls

  make

  # fix .so links in manpages
  # sed -i 's|\.so halt\.8|.so systemd.halt.8|' man/{halt,poweroff}.8
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/var/lib/systemd"
  printf "d /run/console 755 root root\n" > "${pkgdir}/usr/lib/tmpfiles.d/console.conf"
  pushd "${pkgdir}/usr/lib/systemd/system"
    sed -i 's:/sbin:/usr/bin:g' *.service >/dev/null 2>&1
  popd
}

source=("http://www.freedesktop.org/software/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('3070e48e43bc0811fa8da5ba4832a11dac73b1625db94d42d4c15dc279335dd7')


