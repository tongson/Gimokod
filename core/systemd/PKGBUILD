# Based upon the PKGBUILD from Arch Linux
pkgname=systemd
pkgver=36
pkgrel=1
pkgdesc="Session and Startup manager"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2')
depends=('dbus-core' 'kbd' 'libcap' 'cryptsetup' 'udev>=172')
makedepends=('cryptsetup' 'intltool' 'gettext' 'gperf')
groups=('base')
options=('!libtool' '!strip')
backup=(etc/dbus-1/system.d/org.freedesktop.systemd1.conf
        etc/dbus-1/system.d/org.freedesktop.login1.conf
        etc/systemd/system.conf
        etc/systemd/user.conf
        etc/systemd/systemd-logind.conf)
install=systemd.install
source=("http://www.freedesktop.org/software/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        "os-release")
md5sums=('e1213338efb697abc8215d9a66a7f082'
         '752636def0db3c03f121f8b4f44a63cd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Don't unset locale in getty
  # https://bugzilla.redhat.com/show_bug.cgi?id=663900
  sed -i -e '/^Environ.*LANG/s/^/#/' \
         -e '/^ExecStart/s/agetty/& -8/' units/getty@.service.m4

  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --with-udevrulesdir=/lib/udev/rules.d \
              --with-pamlibdir=/usr/lib/security \
              --libexecdir=/usr/lib/systemd \
              --libdir=/usr/lib \
              --localstatedir=/var \
              --with-rootdir= \
              --with-rootlibdir=/lib \
              --disable-audit \
              --disable-tcpwrap \
              --enable-libcryptsetup \
              --with-distro=other \
              --with-sysvinit-path="" \
              --with-sysvrcd-path="" \
              --disable-selinux \
              --disable-plymouth \
              --disable-binfmt \
              --disable-gtk \
              --disable-pam \
              --disable-timedated \
              --disable-hostnamed \
              --disable-localed \
              --disable-nls

  make

  # fix .so links in manpages
  sed -i 's|\.so halt\.8|.so systemd.halt.8|' man/{halt,poweroff}.8
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/os-release" "${pkgdir}/etc/os-release"
  printf "d /run/console 755 root root\n" > "${pkgdir}/usr/lib/tmpfiles.d/console.conf"

  # fix systemd-analyze for python2
  sed -i '1s/python$/python2/' "${pkgdir}/usr/bin/systemd-analyze"
}
