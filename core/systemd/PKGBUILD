# Based upon the PKGBUILD from Arch Linux
pkgname=systemd
pkgver=37
pkgrel=2
pkgdesc="Session and Startup manager"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2')
depends=('dbus-core' 'kbd' 'libcap' 'cryptsetup' 'udev')
makedepends=('cryptsetup' 'intltool' 'gettext' 'gperf')
groups=('base')
options=('!libtool' '!strip')
install=systemd.install
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Don't unset locale in getty
  # https://bugzilla.redhat.com/show_bug.cgi?id=663900
  sed -i -e '/^Environ.*LANG/s/^/#/' \
         -e '/^ExecStart/s/agetty/& -8/' units/getty@.service.m4

  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --with-udevrulesdir=/usr/lib/udev/rules.d \
              --with-pamlibdir=/usr/lib/security \
              --libexecdir=/usr/lib/systemd \
              --libdir=/usr/lib \
              --localstatedir=/var \
              --with-rootdir=/usr \
              --with-rootlibdir=/usr/lib \
              --disable-audit \
              --disable-tcpwrap \
              --enable-libcryptsetup \
              --with-distro=other \
              --with-sysvinit-path="" \
              --with-sysvrcd-path="" \
              --disable-selinux \
              --disable-plymouth \
              --disable-binfmt \
              --disable-gtk \
              --enable-pam \
              --disable-timedated \
              --disable-hostnamed \
              --disable-localed \
              --disable-nls

  make

  # fix .so links in manpages
  sed -i 's|\.so halt\.8|.so systemd.halt.8|' man/{halt,poweroff}.8
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  printf "d /run/console 755 root root\n" > "${pkgdir}/usr/lib/tmpfiles.d/console.conf"
  pushd "${pkgdir}/usr/lib/systemd/system"
    sed -i 's:/sbin:/usr/bin:g' *.service >/dev/null 2>&1
  popd
}

source=("http://www.freedesktop.org/software/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('b38492bde73649cc3520953354ef446c268169c1d342614183655e9ee1a1bbc5')


