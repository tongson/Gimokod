# Based upon the PKGBUILD from Arch Linux
pkgbase=systemd
pkgname=('systemd' 'libsystemd')
pkgver=43
pkgrel=6
pkgdesc="Session and Startup manager"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2')
makedepends=('gperf' 'intltool' 'gettext' 'linux-headers' 'dbus-core' 'kbd' 'libcap'
             'cryptsetup' 'udev' 'kmod' 'xz')
groups=('base')
options=('!libtool' '!strip')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  pushd src
  for f in $(grep -lF '/sbin/' *);do 
    sed -i 's:\/sbin\/:\/usr\/bin\/:g' $f
  done
  popd

  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --with-udevrulesdir=/usr/lib/udev/rules.d \
              --with-pamlibdir=/usr/lib/security \
              --libexecdir=/usr/lib \
              --libdir=/usr/lib \
              --localstatedir=/var \
              --with-rootlibdir=/usr/lib \
              --disable-audit \
              --disable-tcpwrap \
              --enable-libcryptsetup \
              --with-distro=other \
              --with-sysvinit-path="" \
              --with-sysvrcd-path="" \
              --disable-selinux \
              --disable-plymouth \
              --disable-binfmt \
              --disable-gtk \
              --enable-pam \
              --disable-timedated \
              --disable-hostnamed \
              --disable-localed \
              --disable-quotacheck \
              --disable-manpages \
              --disable-nls

  make
}


package_systemd() {
  pkgdesc="systemd and service manager"
  depends=('dbus-core' 'kbd' 'libcap' 'cryptsetup' 'udev' 'kmod' 'xz' 'libsystemd')
  install=systemd.install
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/var/lib/systemd"
  printf "d /run/console 755 root root\n" > "${pkgdir}/usr/lib/tmpfiles.d/console.conf"
  chmod 644 "$pkgdir/usr/lib/tmpfiles.d/console.conf"
  pushd "${pkgdir}/usr/lib/systemd/system"
    sed -i 's:/sbin:/usr/bin:g' *.service >/dev/null 2>&1
  popd

  # --disable-manpages
  rm -rf "${pkgdir}/usr/share/man"

  ### split off libsystemd (libs, includes, pkgconfig)
  install -dm755 "$srcdir"/libsystemd/usr/{include,lib/pkgconfig}

  cd "$srcdir"/libsystemd
  mv "$pkgdir/usr/lib"/libsystemd-*.so* usr/lib
  mv "$pkgdir/usr/include/systemd" usr/include
  mv "$pkgdir/usr/lib/pkgconfig"/libsystemd-*.pc usr/lib/pkgconfig
}

package_libsystemd() {
  pkgdesc="systemd client libraries"
  depends=('libcap' 'xz')
  mv "$srcdir/libsystemd"/* "$pkgdir"
}

source=("http://www.freedesktop.org/software/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('3070e48e43bc0811fa8da5ba4832a11dac73b1625db94d42d4c15dc279335dd7')


