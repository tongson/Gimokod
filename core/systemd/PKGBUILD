# Based upon the PKGBUILD from Arch Linux
pkgname=systemd
pkgver=185
pkgrel=1
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2' 'LGPL2.1' 'MIT')
makedepends=('acl' 'cryptsetup' 'dbus-core' 'gperf'
             'intltool' 'kmod' 'libcap' 'libxslt' 'linux-headers' 'pam' 'xz')
options=('!libtool')
provides=('udev')
replaces=('udev')
conflicts=('udev')
source=(http://www.freedesktop.org/software/$pkgname/$pkgname-"${pkgver}".tar.xz)
md5sums=('a7dbbf05986eb0d2c164ec8e570eb78f')
depends=('acl' 'dbus-core' 'kmod' 'libcap' 'pam'
         'util-linux' 'xz' 'glib' 'hwids' 'kbd')

build() {
  cd "${pkgname}-${pkgver}"
  pushd src
    for f in $(grep -lF '/sbin/' *);do
      sed -i 's:\/sbin\/:\/usr\/bin\/:g' $f
    done
  popd
  pushd rules
    sed -i 's:/sbin:/usr/bin:g' * >/dev/null 2>&1
    sed -i '5s:modprobe -bv:echo:' 80-drivers.rules
    grep -v floppy 50-udev-default.rules > 50-udev-default.rules.tmp
    mv 50-udev-default.rules.tmp 50-udev-default.rules
  popd
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --with-udevrulesdir=/usr/lib/udev/rules.d \
              --with-pamlibdir=/usr/lib/security \
              --libexecdir=/usr/lib \
              --libdir=/usr/lib \
              --sbindir=/usr/bin \
              --localstatedir=/var \
              --with-rootlibdir=/usr/lib \
              --disable-audit \
              --disable-tcpwrap \
              --enable-libcryptsetup \
              --with-distro=other \
              --with-sysvinit-path="" \
              --with-sysvrcd-path="" \
              --disable-selinux \
              --disable-plymouth \
              --disable-binfmt \
              --disable-gtk \
              --enable-pam \
              --disable-timedated \
              --disable-hostnamed \
              --disable-ima \
              --disable-localed \
              --disable-quotacheck \
              --disable-manpages \
              --disable-nls \
              --enable-gtk-doc-html=no \
              --disable-rule-generator \
              --disable-udev_acl \
              --enable-logging \
              --disable-gudev \
              --disable-introspection \
              --disable-keymap \
              --disable-floppy \
              --disable-edd \
              --with-usb-ids-path=/usr/share/hwdata/usb.ids \
              --with-pci-ids-path=/usr/share/hwdata/pci.ids \
              --with-firmware-path=/usr/lib/firmware/updates:/lib/firmware/updates:/usr/lib/firmware:/lib/firmware

  make
}

package() {
  make -C "${pkgname}-${pkgver}" DESTDIR="${pkgdir}" install

  printf "d /run/console 0755 root root\n" > "${pkgdir}/usr/lib/tmpfiles.d/console.conf"

  # move bash-completion and symlink for loginctl
  install -Dm644 "${pkgdir}/etc/bash_completion.d/systemd-bash-completion.sh" \
    "${pkgdir}/usr/share/bash-completion/completions/systemctl"
  ln -s systemctl "${pkgdir}/usr/share/bash-completion/completions/loginctl"
  rm -rf "${pkgdir}/etc/bash_completion.d"

  # don't write units to /etc by default -- we'll enable this on post_install
  # as a sane default
  rm "${pkgdir}/etc/systemd/system/getty.target.wants/getty@tty1.service"
  rmdir "${pkgdir}/etc/systemd/system/getty.target.wants"

  pushd "${pkgdir}/usr/lib/systemd/system"
    sed -i 's:/sbin:/usr/bin:g' *.service >/dev/null 2>&1
  popd

  # udevd is no longer udevd because systemd. why isn't udevadm now udevctl?
  ln -s ../lib/systemd/systemd-udevd "${pkgdir}/usr/bin/udevd"
  ln -s ../systemd/systemd-udevd  "${pkgdir}/usr/lib/udev/udevd"

  # Replace dialout/tape/cdrom group in rules with uucp/storage/optical group
  sed -i 's#GROUP="dialout"#GROUP="uucp"#g;
          s#GROUP="tape"#GROUP="storage"#g;
          s#GROUP="cdrom"#GROUP="optical"#g' "${pkgdir}"/usr/lib/udev/rules.d/*.rules

  # XXX: kill off coredump rule until the journal can recover coredumps
  # this file needs to come back as part of systemd, not systemd-tools
  rm "${pkgdir}/usr/lib/sysctl.d/coredump.conf"
}

