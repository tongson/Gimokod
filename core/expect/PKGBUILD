# Based upon the PKGBUILD from Arch Linux
pkgname=expect
pkgver=5.45
pkgrel=1
pkgdesc="A tool for automating interactive applications"
arch=(i686 x86_64)
url="http://www.nist.gov/el/msid/expect.cfm"
license=('custom')
groups=('base-devel')
depends=('tcl>=8.5.3')
changelog=ChangeLog
source=(http://downloads.sourceforge.net/project/expect/Expect/${pkgver}/expect${pkgver}.tar.gz)
md5sums=('44e1a4f4c877e9ddc5a542dfa7ecc92b')

build() {
  cd "${srcdir}"/expect${pkgver}
  sed -e '/mkpasswd/s:mkpasswd::' -i Makefile.in
  ./configure --prefix=/usr \
              --build=${CHOST} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --mandir=/usr/share/man \
              --with-tk=no \
              --with-tcl=/usr/lib \
              --with-tclinclude=/usr/include

  # Keep $startdir/pkg out of library search paths.
  sed -e 's/-rpath,${LIB_RUNTIME_DIR}:/-rpath,/' -i Makefile
  make
}

package() {
  cd "${srcdir}"/expect${pkgver}
  make DESTDIR="${pkgdir}" install

  # license.
  install -D -m644 README "${pkgdir}"/usr/share/licenses/${pkgname}/README

  (cd "${pkgdir}"/usr/lib;
    ln -s expect${pkgver}/libexpect${pkgver}.so libexpect.so)
}
