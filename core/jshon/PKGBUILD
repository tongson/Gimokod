# Based upon the PKGBUILD from Arch Linux
pkgname=jshon
pkgver=20111105
pkgrel=1
pkgdesc="A json parser for the shell."
arch=('i686' 'x86_64')
url="http://kmkeen.com/jshon/"
license=('MIT')
group=('base')
makedepends=('jansson' 'dietlibc')
source=(http://kmkeen.com/"${pkgname}"/"${pkgname}"-"${pkgver}".tar.gz)
md5sums=('5d4c6d40ef4497473445931e777e280b')

build() {
  cd "${srcdir}/${pkgname}"
  install /usr/include/jansson{.,_config.}h .
  sed -i 's:uint:unsigned int:' jshon.c
  make CC="/usr/lib/diet/bin/diet gcc -nostdinc -static -I."
}

package() {
  cd "${srcdir}/${pkgname}"
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "${pkgname}.1" "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}
