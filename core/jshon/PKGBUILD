# Based upon the PKGBUILD from Arch Linux
pkgname=jshon
pkgver=20111222
pkgrel=3
pkgdesc="A json parser for the shell."
arch=('x86_64')
url="http://kmkeen.com/jshon/"
license=('MIT')
groups=('base')
makedepends=('jansson' 'musl')
source=(http://kmkeen.com/"${pkgname}"/"${pkgname}"-"${pkgver}".tar.gz)
md5sums=('bcc8b8b6e8c09af7d053471a473ef6e8')

build() {
  cd "${srcdir}/${pkgname}"
  install /usr/include/jansson{.,_config.}h .
  sed -i 's:uint:unsigned int:' jshon.c
  export LDFLAGS="${LDFLAGS} -Wl,-z,noexecstack"
  make CC="/usr/lib/musl/bin/musl-gcc -nostdinc -static -I. -L/usr/lib"
}

package() {
  cd "${srcdir}/${pkgname}"
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "${pkgname}.1" "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}
