# Based upon the PKGBUILD from Arch Linux
pkgname=ncurses
pkgver=5.9
pkgrel=1
pkgdesc="System V Release 4.0 curses emulation library"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/ncurses/"
license=('MIT')
groups=('base')
source=(ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('8cb9c412e5f2d96bc6f459aa8c6282a1')

build() {
  cd "${srcdir}"/
  mkdir ncurses{,w}-build

  cd "${srcdir}"/ncursesw-build
  ../${pkgname}-${pkgver}/configure --prefix=/usr \
                                    --build=${CBUILD} \
                                    --host=${CHOST} \
                                    --mandir=/usr/share/man \
                                    --with-shared \
                                    --with-normal \
                                    --without-debug \
                                    --without-ada \
                                    --with-install-prefix="${pkgdir}" \
                                    --enable-widec \
                                    --without-cxx-binding
  # add --enable-ext-colors with next soname bump
  make

  # libncurses.so.5 for external binary support
  cd "${srcdir}"/ncurses-build
  [ $CARCH = "x86_64" ] && CONFIGFLAG="--with-chtype=long"
  ../${pkgname}-${pkgver}/configure --prefix=/usr \
                                    --build=${CBUILD} \
                                    --host=${CHOST} \
                                    --with-shared \
                                    --with-normal \
                                    --without-debug \
                                    --without-ada \
                                    --with-install-prefix="${pkgdir}" ${CONFIGFLAG}
  make
}

package() {
  cd "${srcdir}"/ncursesw-build
  make install

  # Fool packages looking to link to non-wide-character ncurses libraries
  for lib in curses ncurses form panel menu; do
    rm -f "${pkgdir}"/usr/lib/lib${lib}.so
    echo "INPUT(-l${lib}w)" >${pkgdir}/usr/lib/lib${lib}.so
    ln -sf lib${lib}w.a "${pkgdir}"/usr/lib/lib${lib}.a
  done
  ln -sf libncurses++w.a "${pkgdir}"/usr/lib/libncurses++.a

  # Some packages look for -lcurses during build
  rm -f "${pkgdir}"/usr/lib/libcursesw.so
  echo "INPUT(-lncursesw)" >${pkgdir}/usr/lib/libcursesw.so
  ln -sf libncurses.so "${pkgdir}"/usr/lib/libcurses.so
  ln -sf libncursesw.a "${pkgdir}"/usr/lib/libcursesw.a
  ln -sf libncurses.a "${pkgdir}"/usr/lib/libcurses.a

  # non-widec compatibility library
  cd "${srcdir}"/ncurses-build
  install -Dm755 lib/libncurses.so.${pkgver} "${pkgdir}"/usr/lib/libncurses.so."${pkgver}"
  ln -sf libncurses.so.${pkgver} "${pkgdir}"/usr/lib/libncurses.so.5

  # install license, rip it from the readme
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  grep -B 100 '$Id' README > "${pkgdir}"/usr/share/licenses/"${pkgname}"/license.txt
}
