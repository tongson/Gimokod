# Based upon the PKGBUILD from Arch Linux
pkgname=fakeroot
pkgver=1.18
pkgrel=1
pkgdesc="Gives a fake root environment, useful for building packages as a non-privileged user"
arch=('i686' 'x86_64')
license=('GPL')
url="http://packages.debian.org/fakeroot"
groups=('base-devel')
install=fakeroot.install
depends=
options=('!libtool')
source=(http://ftp.debian.org/debian/pool/main/f/${pkgname}/${pkgname}_${pkgver}.orig.tar.bz2)
md5sums=('b0e7696460ee54bc378b9c89997f77c2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CHOST} \
              --host=${CHOST} \
              --libdir=/usr/lib/libfakeroot \
              --disable-static \
              --with-ipc=sysv
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}"/etc/ld.so.conf.d/
  echo '/usr/lib/libfakeroot' > "${pkgdir}"/etc/ld.so.conf.d/fakeroot.conf
}
