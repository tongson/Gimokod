# Based upon the PKGBUILD from Arch Linux
pkgname=tcl
pkgver=8.5.11
pkgrel=1
pkgdesc="The Tcl scripting language"
arch=('i686' 'x86_64')
url="http://tcl.sourceforge.net/"
license=('custom')
groups=('base-devel')
source=(http://downloads.sourceforge.net/sourceforge/tcl/tcl${pkgver}-src.tar.gz)
md5sums=('b01a9691c83990b3db0ce62d1012ca67')

build() {
  cd "${srcdir}"/tcl${pkgver}/unix

  if [ "$CARCH" = "x86_64" ]; then
    ./configure --prefix=/usr \
              --build=${CHOST} \
              --host=${CHOST} \
              --mandir=/usr/share/man \
              --enable-threads \
              --enable-64bit
  else
    ./configure --prefix=/usr \
              --build=${CHOST} \
              --host=${CHOST} \
              --mandir=/usr/share/man \
              --enable-threads \
              --disable-64bit
  fi

  make
}

package()
{
  cd "${srcdir}"/tcl${pkgver}/unix

  make INSTALL_ROOT="${pkgdir}" install install-private-headers
  ln -sf tclsh8.5 "${pkgdir}"/usr/bin/tclsh

  # install license
  install -Dm644 ../license.terms "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  # remove buildroot traces
  sed -i \
    -e "s,^TCL_BUILD_LIB_SPEC='-L.*/unix,TCL_BUILD_LIB_SPEC='-L/usr/lib," \
    -e "s,^TCL_SRC_DIR='.*',TCL_SRC_DIR='/usr/include'," \
    -e "s,^TCL_BUILD_STUB_LIB_SPEC='-L.*/unix,TCL_BUILD_STUB_LIB_SPEC='-L/usr/lib," \
    -e "s,^TCL_BUILD_STUB_LIB_PATH='.*/unix,TCL_BUILD_STUB_LIB_PATH='/usr/lib," \
    -e "s,^TCL_LIB_FILE='libtcl8.5..TCL_DBGX..so',TCL_LIB_FILE=\"libtcl8.5\$\{TCL_DBGX\}.so\"," \
    -e "s,^TCL_CC_SEARCH_FLAGS='\(.*\)',TCL_CC_SEARCH_FLAGS='\1:/usr/lib'," \
    -e "s,^TCL_LD_SEARCH_FLAGS='\(.*\)',TCL_LD_SEARCH_FLAGS='\1:/usr/lib'," \
    "${pkgdir}"/usr/lib/tclConfig.sh
}
