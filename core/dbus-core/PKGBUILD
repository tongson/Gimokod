# Based upon the PKGBUILD from Arch Linux
pkgname=dbus-core
pkgver=1.4.14
pkgrel=1
pkgdesc='Freedesktop.org message bus system'
url="http://www.freedesktop.org/Software/dbus"
arch=(i686 x86_64)
license=('GPL' 'custom')
groups=('base')
depends=('expat')
options=(!libtool)
install=dbus.install
source=(http://dbus.freedesktop.org/releases/dbus/dbus-${pkgver}.tar.gz)
md5sums=('ae6de2562a57516cfabaf56903375ba9')

build() {
  cd "${srcdir}/dbus-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST} \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --libexecdir=/usr/lib/dbus-1.0 \
              --with-dbus-user=81 \
              --with-system-pid-file=/var/run/dbus.pid \
              --enable-inotify \
              --disable-dnotify \
              --disable-verbose-mode \
              --disable-static \
              --disable-tests \
              --disable-asserts \
              --with-systemdsystemunitdir=/lib/systemd/system \
              --without-x \
              --disable-libaudit \
              --disable-selinux \
              --with-xml=expat
  make
}

package(){
  cd "${srcdir}/dbus-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}/usr/bin/dbus-launch"
  rm -f "${pkgdir}/usr/share/man/man1/dbus-launch.1"

  chown 81:81 "${pkgdir}/var/run/dbus"

  #Fix configuration file
  sed -i -e 's:<user>81</user>:<user>dbus</user>:' "${pkgdir}/etc/dbus-1/system.conf"

  #install .keep files so pacman doesn't delete empty dirs
  touch "${pkgdir}/usr/share/dbus-1/services/.keep"
  touch "${pkgdir}/usr/share/dbus-1/system-services/.keep"
  touch "${pkgdir}/etc/dbus-1/session.d/.keep"
  touch "${pkgdir}/etc/dbus-1/system.d/.keep"

  install -d -m755 "${pkgdir}/usr/share/licenses/dbus-core"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/dbus-core/"
}
