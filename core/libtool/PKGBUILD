# Based upon the PKGBUILD from Arch Linux
pkgname=('libtool' 'libltdl')
pkgver=2.4.2
pkgrel=1
pkgdesc="A generic library support script"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/libtool"
license=('GPL')
options=('!libtool')
source=(ftp://ftp.gnu.org/pub/gnu/libtool/${pkgname}-${pkgver}.tar.xz{,.sig})
md5sums=('2ec8997e0c07249eb4cbd072417d70fe'
         '1e6ba57420c82c663c85e745d11c7eed')


build() {
  cd "${srcdir}"/${pkgbase}-${pkgver}
  ./configure --prefix=/usr \
              --build=${CBUILD} \
              --host=${CHOST}
  make
}

package_libtool() {
  depends=("libltdl=${pkgver}" 'gcc=4.6.1')
  groups=('base-devel')

  cd "${srcdir}"/${pkgbase}-${pkgver}

  make DESTDIR="${pkgdir}" install-binSCRIPTS install-man install-info \
                         install-data-local
  rm -rf "${pkgdir}"/usr/share/libtool/libltdl/
}

package_libltdl() {
  pkgdesc="A system independent dlopen wrapper for GNU libtool"

  cd "${srcdir}"/${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install-libLTLIBRARIES install-includeHEADERS \
                         install-ltdlincludeHEADERS install-data-local
  rm -rf "${pkgdir}"/usr/share/{aclocal,libtool/config}
}
