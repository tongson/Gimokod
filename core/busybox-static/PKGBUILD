pkgname=busybox-static
_pkgname=busybox
pkgver=1.19.3
pkgrel=1
pkgdesc='Utilities for rescue, embedded and minimal systems'
arch=('i686' 'x86_64')
url='http://busybox.net'
license=('GPL')
options=('emptydirs' '!binchecks')
groups=('base')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  cp "${srcdir}"/config .config

  Patch busybox-1.19.2-bb.patch
  Patch busybox-1.19.2-man.patch
  Patch busybox-1.19.2-musl.patch

  sed -i -r \
    -e 's:[[:space:]]?-(Werror|Os|falign-(functions|jumps|loops|labels)=1|fomit-frame-pointer)\>::g' \
    Makefile.flags
  sed -i '/^#error Aborting compilation./d' applets/applets.c
  sed -i 's:-Wl,--gc-sections::' Makefile
  sed -i 's:LDLIBS += m crypt::' Makefile.flags
  for inc in linux/types.h \
             asm/types.h \
             asm-generic/types.h \
             asm-generic/int-ll64.h \
             asm/bitsperlong.h \
             asm-generic/bitsperlong.h \
             linux/posix_types.h \
             linux/stddef.h \
             asm/posix_types.h \
             asm/posix_types_64.h \
             linux/netlink.h \
             linux/socket.h \
             linux/rtnetlink.h \
             linux/if_link.h \
             linux/if_addr.h \
             linux/neighbour.h \
             linux/if_arp.h \
             linux/netdevice.h \
             linux/if.h \
             linux/hdlc/ioctl.h \
             linux/if_ether.h \
             linux/if_packet.h \
             ; do
    install -D -m644 /usr/include/${inc} include/${inc}
  done
  make -j1 CC="/usr/lib/musl/bin/musl-gcc -D_GNU_SOURCE -DLOGIN_NAME_MAX=256 -D__musl__ -static -Wl,-z,muldefs"
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  install -D -m0755 busybox "${pkgdir}"/usr/bin/busybox-static
  install -d -m0755 "${pkgdir}/etc/alternatives"
  pushd "${pkgdir}/etc/alternatives"
    ln -sf /usr/bin/busybox-static sh
  popd
  pushd "${pkgdir}/usr/bin"
    ln -sf /usr/bin/busybox-static sh
  popd
  install -D -m0644 "${srcdir}/sh" "${pkgdir}/var/lib/alternatives/sh"
}

source=(${url}/downloads/busybox-${pkgver}.tar.bz2{,.sign})
sha256sums=('a1a9a35732c719ef384f02b6e357c324d8be25bc154af91a48c4264b1e6038f0'
            '04526172ae950625388bde790f1b78ab126307a6396f80e9548e54621b513171')

source=(${source[@]-}
        busybox-1.19.2-bb.patch
        busybox-1.19.2-man.patch
        busybox-1.19.2-musl.patch
        config
        sh)

sha256sums=(${sha256sums[@]-}
            '98f92c2edbcf61d1bacef783ea8b08cce07051b0a4489ed3f4579296846f89f1'
            '733e23d851e68ecd27ed8bb8f4c4b8ea27b0d143069ec49aadb04deeb65dd9f5'
            'b7a6af92ffb1879d0d947b6290a0b3109a02a71c5586bbcd19e8820aa024a0dc'
            'a3e71b84fa9c9d225e6150f26917c584ff54873de7b6b1370a0c6e7a87cb26b8'
            '5b78219c1434cf0cb6fb72b24a070d1d289211e39b91bee769cbab99cc7dbe2b')
