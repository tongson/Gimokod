pkgname=fsl
_pkgname=fossil
pkgver=20111213135356
pkgrel=1
pkgdesc="Simple, high-reliability, distributed SCM (installed as fsl)"
arch=('i686' 'x86_64')
license=('BSD')
groups=('base')
url="http://www.fossil-scm.org/"
makedepends=('dietlibc' 'zlib')
source=("http://www.fossil-scm.org/download/fossil-src-${pkgver}.tar.gz")
sha256sums=('c98556cd1fa6abc2e02aa38de1af425fa9ff09247e72f631c9ff6c239c46e71d')

build() {
  cd "${_pkgname}-src-${pkgver}"
  ./configure --prefix=/usr \
              --static \
              --disable-lineedit \
              --with-openssl=none \
              --build=${CBUILD}

  cp /usr/include/{zlib,zconf}.h src
  export CFLAGS="${CFLAGS} -nostdinc"
  make TCC="/usr/lib/diet/bin/diet gcc" LIB="${LDFLAGS} -static -lz"
}

package() {
  cd "${_pkgname}-src-${pkgver}"
  install -Dm755 "${_pkgname}"  "${pkgdir}/usr/bin/fsl"
  install -Dm644 COPYRIGHT-BSD2.txt "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
}

